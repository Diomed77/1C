//-------------------------------
Перем ДокПодч;
Перем ТекущТовар;
//-------------------------------
Процедура Партии(Режим)
	начЛог = семЗаписатьЛогНач( "Отчет", "Фин. партии товаров", "Обработка", Шаблон("Формирование отчета ([Режим])") );
	Заг="";
	Если ВыбФирма.Выбран()=1 Тогда
		Заг=Заг+"По фирме: "+СокрП(ВыбФирма.Наименование)+". ";
	Иначе
		Предупреждение("Выберите фирму!");
		Возврат;
	КонецЕсли;

	// Здесь формируется отчет, который использует регистры, критичные к
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП
	ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Фин",ВыбФирма);

	// Сделаем это при помощи механизма Запросов
	Рег1=СоздатьОбъект("Регистр.ПартииТоваров");

	Если ДатаКонец>=ПолучитьДатуТА() Тогда
		ДатаКон=0;
		ДатаКонец=ПолучитьДатуТА();
	Иначе
		ДатаКон=ДатаКонец;
	КонецЕсли;

	Запрос=СоздатьОбъект("Запрос");

	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала По ДатаКонец;
	|Товар=Регистр.ПартииТоваров.Товар;
	|Фирма=Регистр.ПартииТоваров.Фирма;
	|Статус=Регистр.ПартииТоваров.Статус;
	|Контрагент=Регистр.ПартииТоваров.Контрагент;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент; 
	|Партия=Регистр.ПартииТоваров.Партия;
	|Докум=Регистр.ПартииТоваров.ТекущийДокумент;
	|ОстатокТовара=Регистр.ПартииТоваров.ОстатокТовара;
	|Стоимость=Регистр.ПартииТоваров.Стоимость; 
	|НДС=Регистр.ПартииТоваров.НДС;
	|Оборот=Регистр.ПартииТоваров.Оборот;
	|СуммаНДС=Регистр.ПартииТоваров.НДСрасхода;
	|Группировка Товар Без групп;
	|Группировка Статус;
	|Группировка Контрагент;";
	Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка ПрихДокумент; 
		|Группировка Партия;";
	КонецЕсли;
	Если (Режим="Подробный")  Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Докум;";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|Функция НачОстатокТовара=НачОст(ОстатокТовара);
	|Функция КонОстатокТовара=КонОст(ОстатокТовара);
	|Функция ПрихОстатокТовара=Приход(ОстатокТовара);
	|Функция РасхОстатокТовара=Расход(ОстатокТовара);
	|Функция НачСтоимость=НачОст(Стоимость);
	|Функция КонСтоимость=КонОст(Стоимость);
	|Функция ПрихСтоимость=Приход(Стоимость);
	|Функция РасхСтоимость=Расход(Стоимость);
	
	|Функция НачНДС=НачОст(НДС);
	|Функция КонНДС=КонОст(НДС);
	|Функция ПрихНДС=Приход(НДС);
	|Функция РасхНДС=Расход(НДС);
	
	|Функция РасхСтоимостьДляПрофит=Расход(Стоимость) Когда (Оборот>0);
	|Функция ОборотТовара=Сумма(Оборот);
	|Функция НДСТовара=Сумма(СуммаНДС) Когда(Оборот>0);
	|Условие (Фирма=ВыбФирма);
	|";

	Если ВыбТовар.Выбран()=0 Тогда
		Заг=Заг+"По всем товарам";
	ИначеЕсли ВыбТовар.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар.ПринадлежитГруппе(ВыбТовар)=1);";
		Заг=Заг+"По товарам группы "+ВыбТовар.Наименование;
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		Заг=Заг+"По товару " + ВыбТовар.Наименование;
	КонецЕсли;

	Если ВыбКлиент.Выбран()=0 Тогда
		Заг=Заг+"По всем поставщикам"+". ";
	ИначеЕсли ВыбКлиент.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Контрагент.ПринадлежитГруппе(ВыбКлиент)=1);";
		Заг=Заг+"По поставщикам группы: "+СокрП(ВыбКлиент.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Контрагент=ВыбКлиент);";
		Заг=Заг+"По поставщику: " + СокрП(ВыбКлиент.Наименование)+". ";
	КонецЕсли;

	Если (ВыбКупленный=0) И (ВыбОтданный=0) И (ВыбПринятый=0) Тогда
		Заг=Заг+"По всем статусам партий"+". ";
	Иначе
		Заг=Заг+"По статусам партий: ";
		Если ВыбКупленный=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Купленный);";
		Иначе
			Заг=Заг+"Купленный"+", ";
		КонецЕсли;
		Если ВыбОтданный=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Отданный);";
		Иначе
			Заг=Заг+"Отданный"+", ";
		КонецЕсли;
		Если ВыбПринятый=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Принятый);";
		Иначе
			Заг=Заг+"Принятый"+". ";
		КонецЕсли;
	КонецЕсли;

	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!");
		Возврат;
	КонецЕсли;
	// теперь в запросе собраны все Документы по партиям

	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
    ИтогоНП=0;
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
	Если Режим="Краткий" Тогда
		Таб.ВывестиСекцию("ШапкаТов");
	Иначе
		Таб.ВывестиСекцию("ШапкаДок");
	КонецЕсли;
	Оживить(4);

	Пока Запрос.Группировка("Товар") = 1 Цикл
		ТекТовар=Запрос.Товар;
		Если ТекТовар.Выбран()=0 Тогда
			Продолжить;
		КонецЕсли;
		ВУ=Рубли;
		ВП=Рубли;

		ПечТовар=СокрП(ТекТовар.Наименование);
		ПечНачальныйОстатокТовара=Запрос.НачОстатокТовара;
		ПечТекущийОстатокТовара=Запрос.КонОстатокТовара;
		ПечПриходОстатокТовара=Запрос.ПрихОстатокТовара;
		ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;

		ПечНачальныйСтоимость=ФРМ(Запрос.НачСтоимость,ВУ,1);
		ПечТекущийСтоимость=ФРМ(Запрос.КонСтоимость,ВУ,1);
		ПечПриходСтоимость=ФРМ(Запрос.ПрихСтоимость,ВУ,1);
		ПечРасходСтоимость=ФРМ(Запрос.РасхСтоимость,ВУ,1); 
		
		ПечНачальныйНДС=ФРМ(Запрос.НачНДС,ВУ,1);
		ПечТекущийНДС=ФРМ(Запрос.КонНДС,ВУ,1);
		ПечПриходНДС=ФРМ(Запрос.ПрихНДС,ВУ,1);
		ПечРасходНДС=ФРМ(Запрос.РасхНДС,ВУ,1);

		Если Запрос.ОборотТовара>0 Тогда
			Профит=Запрос.ОборотТовара-Запрос.РасхСтоимостьДляПрофит-Запрос.НДСТовара;
			ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВУ,1));
			ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Запрос.РасхСтоимостьДляПрофит,"Ч10.2")+" %");
			ПечОборот=ФРМ(Запрос.ОборотТовара,ВП,1);
		Иначе
			Профит=0;
			ПечПрофит="";
			ПечПрофитПроц="";
			ПечОборот="";
		КонецЕсли;

		Если НЕ(Режим="Краткий") Тогда
			Таб.ВывестиСекцию("ШапкаТов");
		КонецЕсли;
		Таб.ВывестиСекцию("Товар");
		Оживить(2);

		Пока Запрос.Группировка("Статус") = 1 Цикл
			Пока Запрос.Группировка("Контрагент") = 1 Цикл
				// теперь еще раз пройдемся по ПрихДокумент и пропишем все основательно
				Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
					Пока Запрос.Группировка("ПрихДокумент") = 1 Цикл
						Пока Запрос.Группировка("Партия") = 1 Цикл
							Док=Запрос.ПрихДокумент;
							ПечНачальныйОстатокДокум=Запрос.НачОстатокТовара;
							ПечТекущийОстатокДокум=Запрос.КонОстатокТовара;
							ПечПриходОстатокДокум=Запрос.ПрихОстатокТовара;
							ПечРасходОстатокДокум=Запрос.РасхОстатокТовара;
							ДатаКурса=?(Док.Выбран()=1,ДатаКурсаДок(Док),ДатаКонец);
							СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,Запрос.ПрихСтоимость/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,Запрос.РасхСтоимость/Запрос.РасхОстатокТовара,0));
							ПечСебестПоДок=ФРМ(СебестПоДок,ВУ,1);
							
							Если Запрос.ОборотТовара>0 Тогда
								Профит=Запрос.ОборотТовара-Запрос.РасхСтоимостьДляПрофит-Запрос.НДСТовара;
								ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВУ,1));
								ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Запрос.РасхСтоимостьДляПрофит,"Ч10.2")+" %");
								ПечОборот=ФРМ(Запрос.ОборотТовара,ВП,1);
							Иначе
								Профит=0;
								ПечПрофит="";
								ПечПрофитПроц="";
								ПечОборот="";
							КонецЕсли;
							
							ПечНомерДок=Док.НомерДок+"; "+СокрП(Запрос.Контрагент);
							
							Таб.ВывестиСекцию("Партия");
							Оживить(1);
							
							БалансТовара= Запрос.НачОстатокТовара;
							
							Если (Режим="Подробный")  Тогда
								Пока Запрос.Группировка("Докум") = 1 Цикл
									Док=Запрос.Докум;
									ВидДок=Док.Вид();
									Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
										Продолжить;
									КонецЕсли;
									
									ДатаКурса=ДатаКурсаДок(Док);
									ЦенаТовара=?(Запрос.РасхОстатокТовара<>0,Запрос.ОборотТовара/Запрос.РасхОстатокТовара,0);
									ПечЦенаТовара=?(ЦенаТовара=0,"",ФРМ(ЦенаТовара,ВП,1));
									БалансТовара=БалансТовара+Запрос.ПрихОстатокТовара-Запрос.РасхОстатокТовара;
									ПечНомерДок=СокрП(Док.НомерДок)+"; "+КлиентДок(Док);
									Приращение=Запрос.ПрихОстатокТовара+Запрос.РасхОстатокТовара;
									
									Если Запрос.ОборотТовара>0 Тогда
										Профит=Запрос.ОборотТовара-Запрос.РасхСтоимостьДляПрофит-Запрос.НДСТовара;
										ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВУ,1));
										ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Запрос.РасхСтоимостьДляПрофит,"Ч10.2")+" %");
										ПечОборот=ФРМ(Запрос.ОборотТовара,ВП,1);
										Если  ((Запрос.Докум.Вид()="МарочныйОтчет") или (Запрос.Докум.Вид()="РасходнаяНакладная"))  Тогда
											Если Запрос.Докум.ИспользоватьНП=Перечисление.Булево.Да Тогда
												НП=Запрос.Оборот*((Запрос.Товар.НалогСПродаж.Получить(Запрос.Докум.ДатаДок))/100);
												ПечНП=ФРМ(НП,ВП,1);
												ИтогоНП=ИтогоНП+НП;
											Иначе
												НП=0;
												ПечНП="";
											КонецЕсли;
										Иначе
											НП=0;
											ПечНП="";
										КонецЕсли;
									Иначе
										Профит=0;
										ПечПрофит="";
										ПечПрофитПроц="";
										ПечОборот="";
										НП=0;
										ПечНП="";
									КонецЕсли;
									
									Если (Запрос.ПрихОстатокТовара<>0) И (Запрос.РасхОстатокТовара=0) Тогда
										ПечПриращение=Приращение;
										Таб.ВывестиСекцию("Приход");
									ИначеЕсли (Запрос.ПрихОстатокТовара=0) И (Запрос.РасхОстатокТовара<>0) Тогда
										ПечПриращение=Приращение;
										Таб.ВывестиСекцию("Расход");
									КонецЕсли;
									Оживить(1);
									
								КонецЦикла;
								
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	ПечНачальныйСтоимость=ФРМ(Запрос.НачСтоимость,ВУ,1);
	ПечТекущийСтоимость=ФРМ(Запрос.КонСтоимость,ВУ,1);
	ПечПриходСтоимость=ФРМ(Запрос.ПрихСтоимость,ВУ,1);
	ПечРасходСтоимость=ФРМ(Запрос.РасхСтоимость,ВУ,1);
    
	ПечНачальныйНДС=ФРМ(Запрос.НачНДС,ВУ,1);
	ПечТекущийНДС=ФРМ(Запрос.КонНДС,ВУ,1);
	ПечПриходНДС=ФРМ(Запрос.ПрихНДС,ВУ,1);
	ПечРасходНДС=ФРМ(Запрос.РасхНДС,ВУ,1);
	
	Если Запрос.ОборотТовара>0 Тогда
		Профит=Запрос.ОборотТовара-Запрос.РасхСтоимостьДляПрофит-Запрос.НДСТовара;
		ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВУ,1));
		ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Запрос.РасхСтоимостьДляПрофит,"Ч10.2")+" %");
		ПечОборот=ФРМ(Запрос.ОборотТовара,ВП,1);
	Иначе
		Профит=0;
		ПечПрофит="";
		ПечПрофитПроц="";
		ПечОборот="";
	КонецЕсли;
	ПечИтогоНП=ФРМ(ИтогоНП,ВП,1);
	Таб.ВывестиСекцию("Итого");
	Оживить(1);

	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,4,0);
	Таб.ПараметрыСтраницы(2,,,,,,,,,1);
	Таб.Показать("Отчет фин. учета партий товаров","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------
ДатаКонец=ПолучитьДатуТА();
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;
ДокПодч=СоздатьОбъект("Документ");
ВыбФирма=Константа.ОсновнаяФирма;
