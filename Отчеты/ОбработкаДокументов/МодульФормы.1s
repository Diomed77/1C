Перем СписокНеПроводимыхДокументов;
//_____________________________________________________________________________
Процедура ПереносГруппы(Источник,Приемник)
	Для i=1 По Источник.РазмерСписка() Цикл
		Представление = "";
		Значение = Источник.ПолучитьЗначение(i,Представление);
		Приемник.ДобавитьЗначение(Значение,Представление);
	КонецЦикла;
	Источник.УдалитьВсе();
	Приемник.СортироватьПоПредставлению();
КонецПроцедуры //ПереносГруппы
//_____________________________________________________________________________
Процедура ПереносЭлемента(Источник,Приемник)
	Если Источник.ТекущаяСтрока() > 0 Тогда
		Представление = "";
		Значение = Источник.ПолучитьЗначение(Источник.ТекущаяСтрока(),Представление);
		Приемник.ДобавитьЗначение(Значение,Представление);
		ИсточникПозиция = Источник.ТекущаяСтрока();
		Если ИсточникПозиция = Источник.РазмерСписка() Тогда
			ИсточникПозиция = ИсточникПозиция - 1;
		КонецЕсли;
		Источник.УдалитьЗначение(Источник.ТекущаяСтрока());
		Источник.ТекущаяСтрока(ИсточникПозиция);
		Приемник.СортироватьПоПредставлению();
	КонецЕсли;
КонецПроцедуры //ПереносЭлемента
//_____________________________________________________________________________
Функция ОбрабатыватьПриказ(ПроверяемыйДокумент, Действие)
	Перем Разрешить;

	Разрешить = 1;
	Если ПроверяемыйДокумент.Вид() = "ПриказОПриемеНаРаботу" Тогда
		Разрешить = 0;
		Если (ПроверяемыйДокумент.Элемент.Выбран() = 0)
		И (ПроверяемыйДокумент.ЭлементВС.Выбран() = 0) Тогда
			Разрешить = 1;
		КонецЕсли;
		Если Действие <> "Пометить" Тогда
		ИначеЕсли ПроверяемыйДокумент.Проведен() = 1 Тогда
			Разрешить = 0;
		КонецЕсли;
	КонецЕсли;

    Возврат Разрешить;
КонецФункции  //ОбрабатыватьПриказ
//_____________________________________________________________________________
Процедура ПриВыбореОбработки()
	ВидОбработки = СписокВидовОбработки.ПолучитьЗначение(СписокВидовОбработки.ТекущаяСтрока());
	СохранитьЗначение("ВидОбработки",ВидОбработки);
КонецПроцедуры
//_____________________________________________________________________________
Процедура Выполнить(Обработка)
	Перем БИ;
	начЛог = семЗаписатьЛогНач( "Отчет", "Обработка документов", "Обработка", "Обработка документов" );
	Если ВыбранныеДокументы.РазмерСписка() = 0 Тогда
		Возврат;
	КонецЕсли;

	Док = СоздатьОбъект("Документ");

	Если ВыбрКлиент.Выбран() = 1 Тогда
		Док.ВыбратьПоЗначению(Дата1,Дата2,"Клиент",ВыбрКлиент);
	Иначе
		Док.ВыбратьДокументы(Дата1,Дата2);
	КонецЕсли;

	Если Обработка = "Печать" Тогда
		Док.ИспользоватьЖурнал("ЖурналОбщий",0);
		Таб = СоздатьОбъект("Таблица");

		Заголовок = "";
		Если ВыбрФирма.Выбран() = 1 Тогда
		    Заголовок = Заголовок + "по фирме " + СокрП(ВыбрФирма.Наименование);
		КонецЕсли;
		Если ВыбрКлиент.Выбран() = 1 Тогда
			Заголовок = Заголовок + "по Клиенту " + СокрП(ВыбрКлиент.Наименование);
		КонецЕсли;
		Если ПустаяСтрока(ВыбрСтрока) = 0 Тогда
		    Если ПустаяСтрока(Заголовок) = 0 Тогда
		        Заголовок = Заголовок + "; ";
		    КонецЕсли;
		    Заголовок = Заголовок + "строка: """ + СокрП(ВыбрСтрока);
		КонецЕсли;
		Если СписокПризнаков.ТекущаяСтрока() > 1 Тогда
		    Если ПустаяСтрока(Заголовок) = 0 Тогда
		        Заголовок = Заголовок + "; ";
		    КонецЕсли;

			Стр = "";
			СписокПризнаков.ПолучитьЗначение(СписокПризнаков.ТекущаяСтрока(),Стр);
		    Заголовок = Заголовок + "только " + Нрег(Стр);
		КонецЕсли;

		Таб.ВывестиСекцию("Отчет");
	ИначеЕсли (Обработка = "Провести") И (МонопольныйРежим() = 1) Тогда
		БИ = СоздатьОбъект("БухгалтерскиеИтоги");
		БИ.Актуальность(1);
	Иначе
		НачатьТранзакцию();
	КонецЕсли;

	НПП = 0;
	Пока Док.ПолучитьДокумент() = 1 Цикл

		//Проверка фирмы, к которой относится документ
		Если ВыбрФирма.Выбран() = 0 Тогда
		ИначеЕсли Док.Фирма <> ВыбрФирма Тогда
		    Продолжить;
		КонецЕсли;

		//Проверка вида документа
		Если ВыбранныеДокументы.НайтиЗначение(Док.Вид()) = 0 Тогда
		    Продолжить;
		КонецЕсли;

		//Проверка строки операции
		Если ПустаяСтрока(ВыбрСтрока) = 1 Тогда
		ИначеЕсли Док.СуществуетОперация() = 0 Тогда
			Продолжить;
		ИначеЕсли Найти(ВРег(Док.Операция.Содержание),ВРег(ВыбрСтрока)) = 0 Тогда
			Продолжить;
		КонецЕсли;

		//Проверка признака документа
		Признак = СписокПризнаков.ПолучитьЗначение(СписокПризнаков.ТекущаяСтрока());

		Если Признак = "Проведенные" Тогда
		    Если Док.Проведен() = 0 Тогда
			    Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "Непроведенные" Тогда
		    Если (Док.Проведен() = 1) или (Док.Вид() = "Операция") Тогда
			    Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "Помеченные" Тогда
		    Если Док.ПометкаУдаления() = 0 Тогда
		        Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "НеПомеченные" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;

		Состояние("Обработка "+Док.ДатаДок);

		НПП=НПП+1;
		Если Обработка = "Выкл" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    ИначеЕсли Док.СуществуетОперация() = 0 Тогда
		    ИначеЕсли Док.Операция.ВключитьПроводки() = 0 Тогда
		    Иначе
		    	Сообщить("Выключение проводок "+Док);
		    	Док.Операция.ВключитьПроводки(0);
		    КонецЕсли;

		ИначеЕсли Обработка = "Вкл" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    ИначеЕсли Док.СуществуетОперация() = 0 Тогда
		    ИначеЕсли Док.Операция.ВключитьПроводки() = 1 Тогда
		    Иначе
		    	Сообщить("Включение проводок "+Док);
		    	Док.Операция.ВключитьПроводки(1);
		    КонецЕсли;

		ИначеЕсли Обработка = "Пометить" Тогда
			Если Док.ПометкаУдаления() = 1 Тогда
			ИначеЕсли ОбрабатыватьПриказ(Док.ТекущийДокумент(),Обработка) = 1 Тогда
				Сообщить("Пометка на удаление "+Док);
				Док.Удалить(0);
			КонецЕсли;

		ИначеЕсли Обработка = "ОтменитьПометку" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    	Сообщить("Отмена пометки на удаление "+Док);
		    	Док.СнятьПометкуУдаления();
		    КонецЕсли;

		ИначеЕсли Обработка = "ОтменаПроведения" Тогда
			Если Док.Проведен() = 0 Тогда
			ИначеЕсли Док.Вид() <> "ПриказОПриемеНаРаботу" Тогда
				Сообщить("Отмена проведения "+Док);
				Док.СделатьНеПроведенным();
			КонецЕсли;

		ИначеЕсли Обработка = "Провести" Тогда
			Если Док.ПометкаУдаления() = 1 Тогда
			ИначеЕсли СписокНеПроводимыхДокументов.НайтиЗначение(Док.Вид()) <> 0 Тогда
			ИначеЕсли ОбрабатыватьПриказ(Док.ТекущийДокумент(),Обработка) = 1 Тогда
				Сообщить("Проведение "+Док);
				Если Док.Провести() = 0 Тогда
//					Возврат;
				КонецЕсли;
			КонецЕсли;

		ИначеЕсли Обработка = "Печать" Тогда
			//Таб.ВывестиСекцию(Док.Вид());
			Таб.ВывестиСекцию("Строка");
		КонецЕсли;
	КонецЦикла;

	Если Обработка = "Печать" Тогда
		Таб.ВывестиСекцию("КонецОтчета");
		Таб.Опции(0,0,3,0,"ОпцииПечатиРеестра","ОкноРеестра");
		Таб.ТолькоПросмотр(1);
		Таб.Показать("Реестр Документов","");
	Иначе
		Если (Обработка = "Провести") И (МонопольныйРежим() = 1) Тогда
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;

		Предупреждение("Обработка закончена!");
	КонецЕсли;
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии()
	СписокНеПроводимыхДокументов = СоздатьОбъект("СписокЗначений");
	ВсеДокументы.УдалитьВсе();

	Для Индекс=1 По Метаданные.Документ() Цикл
		Значение = Метаданные.Документ(Индекс).Идентификатор;
		Представление = Метаданные.Документ(Индекс).Представление();
		Комментарий = Метаданные.Документ(Индекс).Комментарий;
		Если ПустаяСтрока(Комментарий) = 0 Тогда
			Представление = Представление + " (" + Комментарий +")";
		КонецЕсли;
		ВсеДокументы.ДобавитьЗначение(Значение, Представление);

		Если Метаданные.Документ(Индекс).РазрешитьПроведение = 0 Тогда
			СписокНеПроводимыхДокументов.ДобавитьЗначение(Значение, Представление);
		КонецЕсли;
	КонецЦикла;

	ВсеДокументы.СортироватьПоПредставлению();

	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();

	ВыбрКлиент.ВыборГруппы(0);

    СписокВидовОбработки.УдалитьВсе();
    СписокВидовОбработки.ДобавитьЗначение("Выкл","Выключить проводки");
    СписокВидовОбработки.ДобавитьЗначение("Вкл","Включить проводки");
    СписокВидовОбработки.ДобавитьЗначение("Пометить","Пометить на удаление");
    СписокВидовОбработки.ДобавитьЗначение("ОтменитьПометку","Отменить пометки на удаление");
    СписокВидовОбработки.ДобавитьЗначение("ОтменаПроведения","Сделать не проведенными");
    СписокВидовОбработки.ДобавитьЗначение("Провести","Провести");

    ВидОбработки = ВосстановитьЗначение("ВидОбработки");
    СписокВидовОбработки.ТекущаяСтрока(СписокВидовОбработки.НайтиЗначение(ВидОбработки));

    СписокПризнаков.УдалитьВсе();
	СписокПризнаков.ДобавитьЗначение("Все","Все, удовлетворяющие заданным условиям");
	СписокПризнаков.ДобавитьЗначение("Проведенные","Проведенные");
	СписокПризнаков.ДобавитьЗначение("Непроведенные","Непроведенные");
	СписокПризнаков.ДобавитьЗначение("Помеченные","Помеченные на удаление");
	СписокПризнаков.ДобавитьЗначение("НеПомеченные","Не помеченные на удаление");

КонецПроцедуры
//_____________________________________________________________________________