Перем ВВ;  
Перем ТипУч; 
Перем Заг1;
//----------------------------------------------- 
Процедура ПриОткрытии()
	Тип.ДобавитьЗначение(1,"Упр.");
	Тип.ДобавитьЗначение(2,"Фин.");  
	//ТипУч="Упр";
	Заг1="(упр.учет)"; 
	ВыбФирма=Константа.ОсновнаяФирма; 
	Форма.ВыбФирма.Видимость(0); 
	Форма.ХВыбФ.Видимость(0);
КонецПроцедуры 	
//-----------------------------------------------    
Процедура СменаТипа()               
	Если Тип.ТекущаяСтрока()=1 Тогда
		ТипУч="Упр";
		Заг1="(упр.учет)";
		Форма.ВыбФирма.Видимость(0); 
		Форма.ХВыбФ.Видимость(0);
		Тип.ТекущаяСтрока(1);
	Иначе   
		ТипУч="Фин";      
		Заг1="(фин.учет)";   
		Форма.ВыбФирма.Видимость(1); 
		Форма.ХВыбФ.Видимость(1);
	КонецЕсли;
КонецПроцедуры 
//-----------------------------------------------    
Процедура Взаиморасчеты(Режим)
	//  Создание Таблицы для выходного отчета
	начЛог = семЗаписатьЛогНач( "Отчет", "Взаиморасчеты с сотрудниками", "Обработка", Шаблон("Формирование отчета ([Режим])") );
	Если ДатаКонец>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонец=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонец; 
	КонецЕсли;
	
	Заг="";
	Если ВыбСотрудник.Выбран()=0 Тогда
		Заг = "По всем сотрудникам";
	Иначе
		Заг="По сотруднику " + ВыбСотрудник.Наименование;
	КонецЕсли; 
	ПустаяФирма=глФирма; 
	Если ТипУч="Упр"Тогда
		ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Упр");
	Иначе
		ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Фин",ВыбФирма);  
		Заг=Заг+" По фирме: " + СокрП(ВыбФирма.Наименование)+".";
	КонецЕсли;	
	
	//Если ВыбВалюта.Выбран()=1 Тогда
	//    ВВ=ВыбВалюта;
	//Иначе
	//    ВВ=Константа.ВалютаВзаиморасчетов;
	//    ВыбВалюта=Константа.ВалютаВзаиморасчетов;
	//КонецЕсли;
	
	//	ДобСтроку(Заг,". Валюта отчета: ",ВВ);
	
	Запрос=СоздатьОбъект("Запрос");
	
	ТекстЗапроса="ПЕРИОД С ДатаНачала";
	
	Если Не(ДатаКон=0) Тогда
		ТекстЗапроса = ТекстЗапроса + " По ДатаКон;";
	Иначе
		ТекстЗапроса = ТекстЗапроса + ";";
	КонецЕсли;
	ПромФирма=глФирма;
	ТекстЗапроса = ТекстЗапроса +
	"//{{ЗАПРОС(Сотрудники)
	|Сотрудник=Регистр.ВзаиморасчетыСотрудников.Сотрудник;  
	|Фирма=Регистр.ВзаиморасчетыСотрудников.Фирма;
	|Докум=Регистр.ВзаиморасчетыСотрудников.ТекущийДокумент;
	|СуммаДолг=Регистр.ВзаиморасчетыСотрудников.Сумма;
	|Группировка Сотрудник упорядочить по Сотрудник.Наименование;
	|Группировка Докум; 
	|Функция НачДолг= НачОст(СуммаДолг);
	|Функция КонДолг= КонОст(СуммаДолг);
	|Функция РасхДолг= Расход(СуммаДолг);
	|Функция ПрихДолг= Приход(СуммаДолг);
	|"//}}ЗАПРОС
	;  
	Если ВыбСотрудник.Выбран()=0 Тогда
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие (Сотрудник=ВыбСотрудник);";
	КонецЕсли;
	Если ТипУч="Упр" Тогда         
		ТекстЗапроса=ТекстЗапроса+" 
		|Условие (Фирма=ПустаяФирма);";
	Иначе  
		ТекстЗапроса=ТекстЗапроса+" 
		|Условие (Фирма=ВыбФирма);";
	КонецЕсли;
	Если Запрос.Выполнить(ТекстЗапроса)=0 тогда
		Предупреждение("Запрос по Сотрудникам не выполнился!");
		Возврат;
	КонецЕсли;
	ВВ=Рубли; 
	ЧислоСтрок=0;
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("Отчет");
	Оживить(4);
	ИтогНачДолгВал=0;
	ИтогПрихДолгВал=0;
	ИтогРасхДолгВал=0;
	ИтогНашДолгВал=0;
	ИтогДолгКлиентаВал=0;
	Пока Запрос.Группировка("Сотрудник") = 1 Цикл
		Сотрудник=Запрос.Сотрудник;
		Если Сотрудник.Выбран()=0 Тогда
			Продолжить;
		КонецЕсли;
		НачОбщДолг=Запрос.НачДолг;
		ПечНачОбщДолг=ФРМ(НачОбщДолг,ВВ,1);
		ПрихДолг=Запрос.ПрихДолг;
		РасхДолг=Запрос.РасхДолг;
		ПечПрихДолг=ФРМ(ПрихДолг,ВВ,1);
		ПечРасхДолг=ФРМ(РасхДолг,ВВ,1);
		ДолгКлиентаВал = Запрос.КонДолг;
		Если ДолгКлиентаВал<0 Тогда
			НашДолгВал=-ДолгКлиентаВал;   
			ДолгКлиентаВал=0;
		Иначе
			НашДолгВал=0;
		КонецЕсли;
		ПечДолгКлиентаВал=ФРМ(ДолгКлиентаВал,ВВ,1); 
		ПечНашДолгВал=ФРМ(НашДолгВал,ВВ,1);
		Таб.ВывестиСекцию("Сотрудник");
		Оживить(2);
		ИтогНачДолгВал=ИтогНачДолгВал+НачОбщДолг;
		ИтогПрихДолгВал=ИтогПрихДолгВал+ПрихДолг;
		ИтогРасхДолгВал=ИтогРасхДолгВал+РасхДолг;
		ИтогНашДолгВал=ИтогНашДолгВал+НашДолгВал;
		ИтогДолгКлиентаВал=ИтогДолгКлиентаВал+ДолгКлиентаВал;
		
		Если Режим="Подробно" Тогда 
			Пока Запрос.Группировка("Докум") = 1 Цикл 
				Док=Запрос.Докум;
				ПечДокум=""+Док.ДатаДок+" "+СокрП(Док.Вид())+" № "+СокрП(Док.НомерДок);
				ПрихДолг=Запрос.ПрихДолг;
				РасхДолг=Запрос.РасхДолг;
				Если НЕ(ПрихДолг=0) Тогда
					ПечПрихДолг=ФРМ(ПрихДолг,ВВ,1);
					Таб.ВывестиСекцию("Приход");
				КонецЕсли;
				Если НЕ(РасхДолг=0) Тогда
					ПечРасхДолг=ФРМ(РасхДолг,ВВ,1);
					Таб.ВывестиСекцию("Расход");
				КонецЕсли;
				Оживить(1);
			КонецЦикла; 
		КонецЕсли;
	КонецЦикла;
	ПечИтогНачДолгВал=ФРМ(ИтогНачДолгВал,ВВ,1);
	ПечИтогПрихДолгВал=ФРМ(ИтогПрихДолгВал,ВВ,1);
	ПечИтогРасхДолгВал=ФРМ(ИтогРасхДолгВал,ВВ,1);
	ПечИтогНашДолгВал=ФРМ(ИтогНашДолгВал,ВВ,1);
	ПечИтогДолгКлиентаВал=ФРМ(ИтогДолгКлиентаВал,ВВ,1);
	Таб.ВывестиСекцию("Итоги");
	Оживить(2);
	
	
	
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.Опции(0,0,4,0,ПарСтрОтчДлинн);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Взаиморасчеты с сотрудниками","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------

//-------------------------------
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;  
ДатаКонец=РабочаяДата();
Если ДатаКонец>ПолучитьДатуТА() Тогда
	ДатаКонец=ПолучитьДатуТА(); 
КонецЕсли; 
ТипУч="Упр";
//ВыбВалюта=Константа.ВалютаВзаиморасчетов;
//-------------------------------

