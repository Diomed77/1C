Процедура ПриОткрытии()
	
	глПриОткрытии(Контекст,"ОстаткиТоваров");
	
КонецПроцедуры


//-------------------------------
Процедура ОдновременыйКлик()
	Если Одновременно = 0 Тогда
		Форма.ВыбСклад.Доступность(1);
	Иначе
		Форма.ВыбСклад.Доступность(0);
	КонецЕсли;
КонецПроцедуры
//-------------------------------
Процедура УстДата()
	Если ДатаКонца>=ПолучитьДатуТА() Тогда
		Предупреждение("Нельзя устанавливать дату отчета больше Точки Актуальности!");
		ДатаКонца=ПолучитьДатуТА();
	КонецЕсли;
КонецПроцедуры
//----------------------
Процедура ОстаткиТоваров()
	начЛог = семЗаписатьЛогНач( "Отчет", "Остатки товаров", "Обработка", "Остатки товаров на складах");	

	Рег=СоздатьОбъект("Регистр.РезервыТоваров");
	Запрос=СоздатьОбъект("Запрос");

	Если ДатаКонца>=ПолучитьДатуТА() Тогда
		ТекстЗапроса="//{{ЗАПРОС(ОстаткиТоваров)";
		ДатаКонца=ПолучитьДатуТА();
	Иначе
		ТекстЗапроса="//{{ЗАПРОС(ОстаткиТоваров)
		|ПЕРИОД С ДатаКонца По ДатаКонца;";
	КонецЕсли;

	ТекстЗапроса=ТекстЗапроса+"
	|Склад=Регистр.ОстаткиТоваров.Склад;
	|Товар=Регистр.ОстаткиТоваров.Товар;
	|Кол=Регистр.ОстаткиТоваров.ОстатокТовара;
	|Группировка Товар упорядочить по Товар.Наименование;
	|Группировка Склад Все ВошедшиеВЗапрос;
	|Функция КонКол= КонОст(Кол);
	|"//}}ЗАПРОС
	;
	Если ВыбТовар.Выбран() = 0 Тогда
		Заг="По всем товарам";
	ИначеЕсли ВыбТовар.ЭтоГруппа() = 1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"Условие (Товар в ВыбТовар);";
		Заг = "По товарам группы " + ВыбТовар.Наименование;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "Условие (Товар = ВыбТовар);";
		Заг = "По товару " + ВыбТовар.Наименование;
	КонецЕсли;
	Если ВыбСклад.Выбран() = 0 Тогда
		Заг1="По всем складам";
	ИначеЕсли ВыбСклад.ЭтоГруппа() = 1 Тогда
		ТекстЗапроса = ТекстЗапроса +
		"Условие (Склад  в ВыбСклад);";
		Заг1 = "По складам группы " + ВыбСклад.Наименование;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "Условие (Склад = ВыбСклад);";
		Заг1 = "По складу " + ВыбСклад.Наименование;
	КонецЕсли;
	
	Если Запрос.Выполнить(ТекстЗапроса) = 0 тогда
		Возврат;
	КонецЕсли;

	Если ДатаКонца<ПолучитьДатуТА() Тогда
		Рег.ВременныйРасчет();
		РассчитатьРегистрыПО(ДатаКонца);
	КонецЕсли;

	ЧислоСтрок = 0;

	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Отчет");
	Оживить(4);

	Таб.ВывестиСекцию("Шапка|ТоварРезерв");
	Пока Запрос.Группировка("Товар") = 1 Цикл
		Если Запрос.Товар.ЭтоГруппа()=1 Тогда
			Продолжить;
		КонецЕсли;
		Пока Запрос.Группировка("Склад") = 1 Цикл
			Если (Запрос.Склад.Выбран()=0) Тогда
				Продолжить;
			КонецЕсли;
			Таб.ПрисоединитьСекцию("Шапка|Склад");
		КонецЦикла;
		Прервать;
	КонецЦикла;
	Оживить(1);

	Запрос.вНачалоВыборки();
	Пока Запрос.Группировка("Товар") = 1 Цикл
		Если Запрос.Товар.ЭтоГруппа()=1 Тогда
			Таб.ВывестиСекцию("Группа|ТоварРезерв");
			Оживить(1);
			Продолжить;
		Иначе
			Резерв=Регистр.РезервыТоваров.СводныйОстаток(Запрос.Товар,,"РезервТовара");
			Таб.ВывестиСекцию("Товар|ТоварРезерв");
			Оживить(1);
		КонецЕсли;
		Пока Запрос.Группировка("Склад") = 1 Цикл
			Если (Запрос.Склад.Выбран()=0) Тогда
				Продолжить;
			КонецЕсли;
			Таб.ПрисоединитьСекцию("Товар|Склад");
		КонецЦикла;
		Оживить(1);
	КонецЦикла;
	Рег=0;
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.Опции(0,0,6,0,ПарСтрОтчШирок);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Остатки товаров на складах","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------
Процедура ДвиженияТоваров(Режим)
	Перем Запрос, ТекстЗапроса;
	начЛог = семЗаписатьЛогНач( "Отчет", "Остатки товаров", "Обработка", Шаблон("Движения товаров ([Режим])"));	

	Запрос=СоздатьОбъект("Запрос");
	Если ДатаКонца >= ПолучитьДатуТА() Тогда
		ДатаКон = 0;
	Иначе
		ДатаКон = ДатаКонца;
	КонецЕсли;
	ТекстЗапроса = "ПЕРИОД С ДатаНачала";
	Если Не(ДатаКон = 0) Тогда
		ТекстЗапроса = ТекстЗапроса + " По ДатаКон;";
	Иначе
		ТекстЗапроса = ТекстЗапроса + ";";
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса +
	"//{{ЗАПРОС(ТоварыЗаПериоды)
	|Склад=Регистр.ОстаткиТоваров.Склад;
	|ФлагУчета=Регистр.ОстаткиТоваров.ФлагУчета;
	|Товар=Регистр.ОстаткиТоваров.Товар;
	|Док=Регистр.ОстаткиТоваров.ТекущийДокумент;
	|Кол=Регистр.ОстаткиТоваров.ОстатокТовара;
	|Функция НачКол  = НачОст(Кол);
	|Функция КонКол  = КонОст(Кол);
	|Функция ПрихКол = Приход(Кол);
	|Функция РасхКол = Расход(Кол);
	|Группировка Товар упорядочить по Товар.Наименование;
	|Группировка Склад упорядочить по Склад.Наименование;
	|"//}}ЗАПРОС
	;
	Если Режим = "Подробно" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Группировка Док;";
	КонецЕсли;

	Если ВыбТовар.Выбран() = 0 Тогда
		Заг="По всем товарам";
	ИначеЕсли ВыбТовар.ЭтоГруппа() = 1 Тогда
		ТекстЗапроса = ТекстЗапроса + "Условие (Товар.ПринадлежитГруппе(ВыбТовар) = 1);";
		Заг = "По товарам группы " + ВыбТовар.Наименование;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "Условие (Товар = ВыбТовар);";
		Заг = "По товару " + ВыбТовар.Наименование;
	КонецЕсли;

    Если Одновременно=0 Тогда
	    Если ВыбСклад.Выбран()=0 Тогда
			Заг1="По всем складам";
		Иначе
			ТекстЗапроса = ТекстЗапроса + "Условие (Склад = ВыбСклад);";
			Заг1 = "По складу " + ВыбСклад.Наименование;
		КонецЕсли;
	Иначе
		Заг1="Одновременно по всем складам. ";
		ТекстЗапроса = ТекстЗапроса + "Условие (ФлагУчета <> 1);";
	КонецЕсли;

	Если Запрос.Выполнить(ТекстЗапроса)=0 тогда
		Возврат;
	КонецЕсли;

	ВБ=Рубли;

 	ЧислоСтрок=0;
	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ДвиженияТоваров");
	Таб.ВывестиСекцию("Отчет");
	Оживить(4);
	Пока Запрос.Группировка("Товар") = 1 Цикл
		НаимТовара=Запрос.ЗначениеУпорядочивания(1,1);
		Если Запрос.Товар.ЭтоГруппа()=1 Тогда
			Таб.ВывестиСекцию("Группа");
			Оживить(1);
			Продолжить;
		Иначе
			ПечНачКол=Формат(Запрос.НачКол,"Ч015.3");
			ПечПрихКол=Формат(Запрос.ПрихКол,"Ч015.3");
			ПечРасхКол=Формат(Запрос.РасхКол,"Ч015.3");
			ПечКонКол=Формат(Запрос.КонКол,"Ч015.3");
			Таб.ВывестиСекцию("Товар");
			Оживить(1);
		КонецЕсли;
	    Если Одновременно=1 Тогда
			НК=Запрос.НачКол;
		КонецЕсли;
		Пока Запрос.Группировка("Склад") = 1 Цикл
			НаимСклада=Запрос.ЗначениеУпорядочивания(2,1);
			ПечНачКол=Формат(Запрос.НачКол,"Ч015.3");
			ПечПрихКол=Формат(Запрос.ПрихКол,"Ч015.3");
			ПечРасхКол=Формат(Запрос.РасхКол,"Ч015.3");
			ПечКонКол=Формат(Запрос.КонКол,"Ч015.3");

		    Если Одновременно=0 Тогда
				Если ВыбСклад.Выбран()=0 Тогда
					Таб.ВывестиСекцию("Склад");
					Оживить(1);
				КонецЕсли;
			КонецЕсли;

            Если Режим="Подробно" Тогда
			    Если Одновременно=0 Тогда
					НК=Запрос.НачКол;
				КонецЕсли;
				Пока Запрос.Группировка("Док") = 1 цикл
                    Док=Запрос.Док;
					Если Док.Выбран()=0 Тогда
						Продолжить;
					КонецЕсли;
					ПечДок=" "+Док.ДатаДок+" № "+СокрП(Док.НомерДок)+"; ";
					ВидДок=Док.Вид();
					Если     НЕ((ВидДок="ВводОстатковТоваров")
							ИЛИ (ВидДок="Перемещение")
							ИЛИ (ВидДок="ПереносОстаткиТоваров")
							ИЛИ (ВидДок="АктРазборки")
							ИЛИ (ВидДок="ЗаказКладовщику")
							ИЛИ (ВидДок="ЗаменаИнгредиента")
							ИЛИ (ВидДок="КалькуляционнаяКарта")
							ИЛИ (ВидДок="ПеремещениеНаСкладГотовойПродукции")
							ИЛИ (ВидДок="НарядНаСборку")
							ИЛИ (ВидДок="НарядНаРазукомплектацию")
							ИЛИ (ВидДок="СличительнаяВедомость")
							ИЛИ (ВидДок="ЗаказКалькуляция")
							ИЛИ	(ВидДок="Списание")) Тогда
								Попытка
									ПечДок=ПечДок+СокрП(Док.Клиент.Наименование);
								Исключение
									ПечДок=ПечДок+"";
								КонецПопытки;
					КонецЕсли;
					Если Запрос.ПрихКол<>0 Тогда
	                    Приращение=Запрос.ПрихКол;
						НК=НК+Приращение;
						ПечПриращение=Формат(Приращение,"Ч015.3");
						ПечНК=Формат(НК,"Ч015.3");
                    	Таб.ВывестиСекцию("Приход");
					КонецЕсли;
					Если Запрос.РасхКол<>0 Тогда
	                    Приращение=Запрос.РасхКол;
						НК=НК-Приращение;
						ПечПриращение=Формат(Приращение,"Ч015.3");
						ПечНК=Формат(НК,"Ч015.3");
                    	Таб.ВывестиСекцию("Расход");
					КонецЕсли;
					Оживить(1);
				КонецЦикла;

			КонецЕсли;

		КонецЦикла;
	КонецЦикла;
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.Опции(0,0,5,0,ПарСтрОтчДлинн);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчет о движении товаров на складах","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры
//-------------------------------
ДатаКонца=ПолучитьДатуТА();
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;
Одновременно=0;
