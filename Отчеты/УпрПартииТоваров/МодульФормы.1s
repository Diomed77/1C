//-------------------------------
Перем ДокПодч;
Перем ТекущТовар;
Перем ПустаяФирма;
//-------------------------------
 Процедура ПриОткрытии()
	Если Константа.ПартииНаСкладах<>Перечисление.Булево.Да Тогда
		Форма.ВыбСклад.Видимость(0);
		Форма.КнопкаСклад.Видимость(0); 
		Форма.ТекстСкл.Видимость(0);
		ВыбСклад=0;
	КонецЕсли;	      
	
	глПриОткрытии(Контекст,"УпрПартииТоваров");	
КонецПроцедуры  
//-------------------------------
Процедура Партии(Режим)
	начЛог = семЗаписатьЛогНач( "Отчет", "Упр. отчет по партиям товаров", "Обработка", Шаблон("Формирование отчета ([Режим])") );
	// Здесь формируется отчет, который использует регистры, критичные к 
	// последовательности проведения документов
	// поэтому сравним установленные даты периода формируемого отчета с ГП 
	ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Упр");
	
	Заг="";
	
	// Сделаем это при помощи механизма Запросов
	ПустаяФирма=глФирма;
	
	Если ДатаКонец>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонец=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонец; 
	КонецЕсли;
	
	Запрос=СоздатьОбъект("Запрос");
	
	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала По ДатаКонец;
	|Товар=Регистр.ПартииТоваров.Товар;
	|Фирма=Регистр.ПартииТоваров.Фирма;
	|Статус=Регистр.ПартииТоваров.Статус;
	|Контрагент=Регистр.ПартииТоваров.Контрагент;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент;
	|Партия=Регистр.ПартииТоваров.Партия;
	|Докум=Регистр.ПартииТоваров.ТекущийДокумент;
	|ОстатокТовара=Регистр.ПартииТоваров.ОстатокТовара;
	|Стоимость=Регистр.ПартииТоваров.Стоимость;
	|ПродСтоимость=Регистр.ПартииТоваров.ПродСтоимость;
	|НДС=Регистр.ПартииТоваров.НДС;
	|Оборот=Регистр.ПартииТоваров.Оборот;"; 
	Если ВыбСклад.Выбран()<>0 Тогда         
		ТекстЗапроса=ТекстЗапроса+"
		|Склад=Регистр.ПартииТоваров.Склад;";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|Группировка Товар Без групп;
	|Группировка Статус;
	|Группировка Контрагент;";
	Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка ПрихДокумент;
		|Группировка Партия;";
	КонецЕсли;
	Если (Режим="Подробный")  Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Докум;";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|Функция НачОстатокТовара=НачОст(ОстатокТовара);
	|Функция КонОстатокТовара=КонОст(ОстатокТовара);
	|Функция ПрихОстатокТовара=Приход(ОстатокТовара);
	|Функция РасхОстатокТовара=Расход(ОстатокТовара);
	|Функция НачСтоимость=НачОст(Стоимость);
	|Функция КонСтоимость=КонОст(Стоимость);
	|Функция ПрихСтоимость=Приход(Стоимость);
	|Функция РасхСтоимость=Расход(Стоимость);   
	|Функция РасхСтоимостьДляПрофит=Расход(Стоимость) Когда (Оборот>0);   
	|Функция ПрихПродСтоимость=Приход(ПродСтоимость);
	|Функция РасхПродСтоимость=Расход(ПродСтоимость);
	|Функция НачНДС=НачОст(НДС);
	|Функция КонНДС=КонОст(НДС);
	|Функция ПрихНДС=Приход(НДС);
	|Функция РасхНДС=Расход(НДС) Когда (Оборот>0);
	|Функция ОборотТовара=Сумма(Оборот);
	|Условие (Фирма=ПустаяФирма);
	|";
	Если ВыбСклад.Выбран() <> 0 Тогда
		Если ВыбСклад.ЭтоГруппа()=1 Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|Условие (Склад в ВыбСклад);"; 
			Заг=Заг+"   По группе складов: " + СокрП(ВыбСклад.Наименование)+". ";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|Условие (Склад=ВыбСклад);"; 
			Заг=Заг+"   По складу: " + СокрП(ВыбСклад.Наименование)+". ";
		КонецЕсли;
	Иначе
		Заг=Заг+"   По всем складам"+". ";
	КонецЕсли;  
	Если ВыбТовар.Выбран()=0 Тогда
		Заг=Заг+"По всем товарам"+". ";
	ИначеЕсли ВыбТовар.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар в ВыбТовар);";
		Заг=Заг+"По товарам группы: "+СокрП(ВыбТовар.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		Заг=Заг+"По товару: " + СокрП(ВыбТовар.Наименование)+". ";
	КонецЕсли;
	
	Если ВыбКлиент.Выбран()=0 Тогда
		Заг=Заг+"По всем поставщикам"+". ";
	ИначеЕсли ВыбКлиент.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Контрагент в ВыбКлиент);";
		Заг=Заг+"По поставщикам группы: "+СокрП(ВыбКлиент.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Контрагент=ВыбКлиент);";
		Заг=Заг+"По поставщику: " + СокрП(ВыбКлиент.Наименование)+". ";
	КонецЕсли;
	
	Если (ВыбКупленный=1) И (ВыбОтданный=1) И (ВыбПринятый=1) Тогда
		Заг=Заг+"По всем статусам партий"+". ";
	Иначе
		Заг=Заг+"По статусам партий: ";
		Если ВыбКупленный=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Купленный);";
		Иначе
			Заг=Заг+"Купленный"+", ";
		КонецЕсли;
		Если ВыбОтданный=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Отданный);";
		Иначе
			Заг=Заг+"Отданный"+", ";
		КонецЕсли;
		Если ВыбПринятый=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Принятый);";
		Иначе
			Заг=Заг+"Принятый"+". ";
		КонецЕсли;
	КонецЕсли;
	
	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	
	Если Найти(НазваниеНабораПрав(),"ЗавСкладом")>0 Тогда
		Таб.ИсходнаяТаблица("ТаблицаБезПрибыли");
	КонецЕсли;
	
	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!");
		Возврат;
	КонецЕсли;
	
	// теперь в запросе собраны все Документы по партиям 
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
	Если Режим="Краткий" Тогда
		Таб.ВывестиСекцию("ШапкаТов");
	Иначе
		Таб.ВывестиСекцию("ШапкаДок");
	КонецЕсли;
	Оживить(4);
	
	ВИ=?(ВыбВалюта.Выбран()=0,Рубли,ВыбВалюта);
	ИтогоНачальныйСтоимость=0;
	ИтогоТекущийСтоимость=0;
	ИтогоПриходСтоимость=0;
	ИтогоРасходСтоимость=0;
	ИтогоПрофит=0;
	ИтогоОборот=0;
	
	Пока Запрос.Группировка("Товар") = 1 Цикл
		ТекТовар=Запрос.Товар;
		Если ТекТовар.Выбран()=0 Тогда
			Продолжить;
		КонецЕсли;
		ВУ=ТекТовар.ВалютаУчета;
		ВП=?(ВыбВалюта.Выбран()=0,ТекТовар.тВалютаПродажи,ВыбВалюта);
		
		ПечТовар=СокрП(ТекТовар.Наименование);
		ПечНачальныйОстатокТовара=Запрос.НачОстатокТовара;
		ПечТекущийОстатокТовара=Запрос.КонОстатокТовара;
		ПечПриходОстатокТовара=Запрос.ПрихОстатокТовара;
		ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
		
		ПечНачальныйСтоимость=ФРМ(Пересчет(Запрос.НачСтоимость,ВУ,ДатаКонец,ВП,ДатаКонец),ВП,1);
		ПечТекущийСтоимость=ФРМ(Пересчет(Запрос.КонСтоимость,ВУ,ДатаКонец,ВП,ДатаКонец),ВП,1);
		ПечПриходСтоимость=ФРМ(Пересчет(Запрос.ПрихСтоимость,ВУ,ДатаКонец,ВП,ДатаКонец),ВП,1);
		ПечРасходСтоимость=ФРМ(Пересчет(Запрос.РасхСтоимость,ВУ,ДатаКонец,ВП,ДатаКонец),ВП,1);
		
		ИтогоНачальныйСтоимость=ИтогоНачальныйСтоимость+Пересчет(Запрос.НачСтоимость,ВУ,ДатаКонец,ВИ,ДатаКонец);
		ИтогоТекущийСтоимость=ИтогоТекущийСтоимость+Пересчет(Запрос.КонСтоимость,ВУ,ДатаКонец,ВИ,ДатаКонец);
		ИтогоПриходСтоимость=ИтогоПриходСтоимость+Пересчет(Запрос.ПрихСтоимость,ВУ,ДатаКонец,ВИ,ДатаКонец);
		ИтогоРасходСтоимость=ИтогоРасходСтоимость+Пересчет(Запрос.РасхСтоимость,ВУ,ДатаКонец,ВИ,ДатаКонец);
		
		Если Запрос.ОборотТовара>0 Тогда
			Профит=Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонец,ВП,ДатаКонец)
			-Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКонец,ВП,ДатаКонец);
			ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВП,1));
			ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКонец,ВП,ДатаКонец),"Ч6.2")+" %");
			ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонец,ВП,ДатаКонец),ВП,1);
		Иначе
			Профит=0;
			ПечПрофит="";
			ПечПрофитПроц="";
			ПечОборот="";
		КонецЕсли;
		
		ИтогоПрофит=ИтогоПрофит+Пересчет(Запрос.ОборотТовара-Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКонец,ВИ,ДатаКонец);
		ИтогоОборот=ИтогоОборот+Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонец,ВИ,ДатаКонец);
		
		Если НЕ(Режим="Краткий") Тогда
			Таб.ВывестиСекцию("ШапкаТов");
		КонецЕсли;
		Таб.ВывестиСекцию("Товар");
		Оживить(2);
		
		Пока Запрос.Группировка("Статус") = 1 Цикл
			Пока Запрос.Группировка("Контрагент") = 1 Цикл
				// теперь еще раз пройдемся по ПрихДокумент и пропишем все основательно 
				Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
					Пока Запрос.Группировка("ПрихДокумент") = 1 Цикл 
						Пока Запрос.Группировка("Партия") = 1 Цикл
							Док=Запрос.ПрихДокумент;            
							Если ПустоеЗначение(ВыбСклад) = 0 Тогда
								Если Док.Склад <> ВыбСклад Тогда
									Продолжить;	
								КонецЕсли;
							КонецЕсли;							
							ПечНачальныйОстатокДокум=Запрос.НачОстатокТовара;
							ПечТекущийОстатокДокум=Запрос.КонОстатокТовара;
							ПечПриходОстатокДокум=Запрос.ПрихОстатокТовара;
							ПечРасходОстатокДокум=Запрос.РасхОстатокТовара;
							ДатаКурса=?(Док.Выбран()=1,ДатаКурсаДок(Док),ДатаКонец);
							СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,(Запрос.ПрихСтоимость)/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,(Запрос.РасхСтоимость)/Запрос.РасхОстатокТовара,0));
							ПечСебестПоДок=ФРМ(Пересчет(СебестПоДок,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
							
							Если Запрос.ОборотТовара>0 Тогда
								Профит=Пересчет(Запрос.ОборотТовара,ВУ,ДатаКурса,ВП,ДатаКурса)
								-Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКурса,ВП,ДатаКурса);
								ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВП,1));
								ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
								ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКурса,ВП,ДатаКурса),"Ч6.2")+" %");
							Иначе
								Профит=0;
								ПечПрофит="";
								ПечПрофитПроц="";
								ПечОборот="";
							КонецЕсли;
							
							ПечНомерДок=Док.НомерДок+"; "+СокрП(Запрос.Контрагент);
							
							Таб.ВывестиСекцию("Партия");
							Оживить(1); 
							
							БалансТовара= Запрос.НачОстатокТовара;
							
							Если (Режим="Подробный")  Тогда
								Пока Запрос.Группировка("Докум") = 1 Цикл 
									Док=Запрос.Докум;      
									Если ПустоеЗначение(ВыбСклад) = 0 Тогда
										Если Док.Склад <> ВыбСклад Тогда
											Продолжить;	
										КонецЕсли;
									КонецЕсли;
									ВидДок=Док.Вид();
									Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
										Продолжить;
									КонецЕсли;
									
									ДатаКурса=ДатаКурсаДок(Док);
									БалансТовара=БалансТовара+Запрос.ПрихОстатокТовара-Запрос.РасхОстатокТовара;
									ПечНомерДок=СокрП(Док.НомерДок)+"; "+КлиентДок(Док);
									ПечКлиентДок=КлиентДок(Док,"Подробно");
									Приращение=Запрос.ПрихОстатокТовара+Запрос.РасхОстатокТовара;

									СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,(Запрос.ПрихСтоимость)/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,(Запрос.РасхСтоимость)/Запрос.РасхОстатокТовара,0));
									ПечСебестПоДок=ФРМ(Пересчет(СебестПоДок,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
									
									Если Запрос.ОборотТовара>0 Тогда
										Профит=Пересчет(Запрос.ОборотТовара,ВУ,ДатаКурса,ВП,ДатаКурса)
										-Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКурса,ВП,ДатаКурса);
										ПечПрофит=?(Профит=0,"",ФРМ(Профит,ВП,1));
										ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
										ПечПрофитПроц=?(Запрос.РасхСтоимостьДляПрофит=0,"",Формат(100*Профит/Пересчет(Запрос.РасхСтоимостьДляПрофит,ВУ,ДатаКурса,ВП,ДатаКурса),"Ч6.2")+" %");
										
										ЦенаТовара=?(Запрос.РасхОстатокТовара<>0,Пересчет(Запрос.ОборотТовара/Запрос.РасхОстатокТовара,ВУ,ДатаКурса,ВП,ДатаКурса),0);
									Иначе
										ЦенаТовара=?(Запрос.ПрихОстатокТовара<>0,Пересчет(Запрос.ПрихПродСтоимость/Запрос.ПрихОстатокТовара,ВУ,ДатаКурса,ВП,ДатаКурса),0);
										Профит=0;
										ПечПрофит="";
										ПечПрофитПроц="";
										ПечОборот="";
									КонецЕсли;
									ПечЦенаТовара=?(ЦенаТовара=0,"",ФРМ(ЦенаТовара,ВП,1));
									
									Если (Запрос.ПрихОстатокТовара<>0) Тогда
										ПечПриращение=Запрос.ПрихОстатокТовара;
										Таб.ВывестиСекцию("Приход");
									КонецЕсли;
									
									Если (Запрос.РасхОстатокТовара<>0) Тогда
										ПечПриращение=Запрос.РасхОстатокТовара;
										Таб.ВывестиСекцию("Расход");
									КонецЕсли;
									Оживить(1);
								КонецЦикла;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	ПечНачальныйСтоимость=ФРМ(ИтогоНачальныйСтоимость,ВИ,1);
	ПечТекущийСтоимость=ФРМ(ИтогоТекущийСтоимость,ВИ,1);
	ПечПриходСтоимость=ФРМ(ИтогоПриходСтоимость,ВИ,1);
	ПечРасходСтоимость=ФРМ(ИтогоРасходСтоимость,ВИ,1);
	
	Если ИтогоОборот<>0 Тогда
		ПечПрофит=?(ИтогоПрофит=0,"",ФРМ(ИтогоПрофит,ВИ,1));
		ПечПрофитПроц=?(ИтогоРасходСтоимость=0,"",Формат(100*ИтогоПрофит/ИтогоРасходСтоимость,"Ч6.2")+" %");
		ПечОборот=ФРМ(ИтогоОборот,ВИ,1);
	Иначе
		ПечПрофит="";
		ПечПрофитПроц="";
		ПечОборот="";
	КонецЕсли;
	Таб.ВывестиСекцию("Итого");
	Оживить(1);
	
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,4,0);
	Таб.ПараметрыСтраницы(2,,,,,,,,,1);
	Таб.Показать("Отчет по товарным партиям","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------
ДатаКонец=ПолучитьДатуТА();
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;  
ДокПодч=СоздатьОбъект("Документ");
ВыбКупленный=1;
ВыбОтданный=1;
ВыбПринятый=1;
ВыбВалюта=Рубли;