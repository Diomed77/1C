перем СписокТоваров;

Процедура Сформировать()
	начЛог = семЗаписатьЛогНач( "Отчет", "Расход ингредиентов", "Обработка", "Формирование отчета" );	
	Пуст=""; 	 
	
	Запрос=СоздатьОбъект("Запрос");
	ТекстЗапроса=
	"//{{ЗАПРОС(Сформировать)
	|Период с ДатаНачала по ДатаКонца;
	|Товар = Регистр.ПартииТоваров.Товар;
	|Кол = Регистр.ПартииТоваров.ОстатокТовара;
	|Стоим = Регистр.ПартииТоваров.Стоимость;
	|Блюдо = Регистр.ПартииТоваров.Блюдо;
	|Склад = Регистр.ПартииТоваров.Склад;
	|Функция КолРасход = Расход(Кол);
	|Функция СтоРасход = Расход(Стоим);
	|"//}}ЗАПРОС
	;   
	
	Если ПоБлюдам=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"	
		|Группировка Блюдо упорядочить по Блюдо.Наименование;
		|Группировка Товар упорядочить по Товар.Наименование;";
		ЗагШапки="Блюдо / ингредиент";
	ИначеЕсли ПоИнгредиентам=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Товар упорядочить по Товар.Наименование;
		|Группировка Блюдо упорядочить по Блюдо.Наименование;"; 
		ЗагШапки="Ингредиент / блюдо";
	КонецЕсли;	
	
	ПустоеБлюдо=ПолучитьПустоеЗначение("Справочник.Номенклатура");
	
	ТекстЗапроса=ТекстЗапроса+"Условие (Блюдо<>ПустоеБлюдо);";
	
	Если ПустоеЗначение(СписокТоваров)=0 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар в СписокТоваров);";
    ИначеЕсли ВыбТовар.Выбран()<>0 Тогда
		Если ВыбТовар.ВидТовара=Перечисление.ВидыТоваров.Блюдо Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Блюдо=ВыбТовар);";
		ИначеЕсли (ВыбТовар.ВидТовара=Перечисление.ВидыТоваров.Ингредиент) ИЛИ (ВыбТовар.ВидТовара=Перечисление.ВидыТоваров.Товар) Тогда 
			ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		КонецЕсли;	
	КонецЕсли;
	

    Если ВыбСклад.Выбран()=0 Тогда
	ИначеЕсли ВыбСклад.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Склад в ВыбСклад);";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Склад = ВыбСклад);";
	КонецЕсли;
	
	Если Запрос.Выполнить(ТекстЗапроса) = 0 тогда
		Возврат;
	КонецЕсли;
	Таб=СоздатьОбъект("Таблица"); 
	Таб.ВывестиСекцию("Шапка");
	Пока Запрос.Группировка(1)=1 Цикл  
		Если Запрос.Товар.ЭтоГруппа()=1 Тогда 
			Продолжить;
		КонецЕсли;
		Если Запрос.Блюдо.ЭтоГруппа()=1 Тогда 
			Продолжить;
		КонецЕсли;
		Если ПоБлюдам=1 Тогда
			ПромТовар=Запрос.Блюдо;
			ПромТовар2=Запрос.Товар;
			КолГр_1="- - -";
		ИначеЕсли ПоИнгредиентам=1 Тогда
			ПромТовар=Запрос.Товар; 
			ПромТовар2=Запрос.Блюдо;
			КолГр_1=Запрос.КолРасход+" "+Запрос.Товар.БазоваяЕдиницаИзмерения;
		КонецЕсли;
		Таб.ВывестиСекцию("Товар");
		Пока Запрос.Группировка(2)=1 Цикл 
			Если Запрос.Блюдо.ЭтоГруппа()=1 Тогда 
				Продолжить;
			КонецЕсли;  
			Если Запрос.Товар.ЭтоГруппа()=1 Тогда 
				Продолжить;
			КонецЕсли;
			
			Если ПоБлюдам=1 Тогда
				ПромТовар2=Запрос.Товар;
			ИначеЕсли ПоИнгредиентам=1 Тогда
				ПромТовар2=Запрос.Блюдо;
			КонецЕсли;
			
			Таб.ВывестиСекцию("Блюдо");
		КонецЦикла;
	КонецЦикла;
	Таб.Опции(0,0,3,0);   
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.Показать("Расход ингредиентов в разрезе блюд","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры   

Процедура ИзменениеПорядкаБ()
	Если ПоБлюдам=1 Тогда
		ПоИнгредиентам=0;
	ИначеЕсли ПоБлюдам=0 Тогда
		ПоИнгредиентам=1;
	КонецЕсли;	   
КонецПроцедуры   
Процедура ИзменениеПорядкаИ()
	Если ПоИнгредиентам=1 Тогда
		ПоБлюдам=0;
	ИначеЕсли ПоИнгредиентам=0 Тогда
		ПоБлюдам=1;
	КонецЕсли;	   
КонецПроцедуры

Процедура ОбработкаЯчейкиТаблицы(ЗначениеЯчейки,СтандартнаяОбработка,Таблица)
	Если ТипЗначения(ЗначениеЯчейки)=12 Тогда
		 СтандартнаяОбработка=1;
		Возврат;
	КонецЕсли;	
	ВыбТовар=ЗначениеЯчейки;  
	Одновременно=1; 
	ДатаОтчета=ДатаНачала;   
	Режим="Подробно";
	Запрос=СоздатьОбъект("Запрос");
	Если ДатаКонца >= ПолучитьДатуТА() Тогда
		ДатаКон = 0;
	Иначе
		ДатаКон = ДатаКонца;
	КонецЕсли;
	ТекстЗапроса = "ПЕРИОД С ДатаОтчета";
	Если Не(ДатаКон = 0) Тогда
		ТекстЗапроса = ТекстЗапроса + " По ДатаКон;";
	Иначе
		ТекстЗапроса = ТекстЗапроса + ";";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"//{{ЗАПРОС(ТоварыЗаПериоды)
	|Склад=Регистр.ОстаткиТоваров.Склад;
	|ФлагУчета=Регистр.ОстаткиТоваров.ФлагУчета;
	|Товар=Регистр.ОстаткиТоваров.Товар;
	|ВидТовара=Регистр.ОстаткиТоваров.Товар.ВидТовара;
	|Док=Регистр.ОстаткиТоваров.ТекущийДокумент;
	|Кол=Регистр.ОстаткиТоваров.ОстатокТовара;
	|Функция НачКол  = НачОст(Кол);
	|Функция КонКол  = КонОст(Кол);
	|Функция ПрихКол = Приход(Кол);
	|Функция РасхКол = Расход(Кол);
	|Группировка Товар упорядочить по Товар.Наименование;
	|Группировка Склад упорядочить по Склад.Наименование;
	|"//}}ЗАПРОС
	;
	Если Режим = "Подробно" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Группировка Док упорядочить по Док.ДатаДок;";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "Условие (Товар = ВыбТовар);";
	Заг = "По товару " + ВыбТовар.Наименование;
	Заг1="Одновременно по всем подразделениям. ";
	ТекстЗапроса = ТекстЗапроса + "Условие (ФлагУчета <> 1);";
	
	Если Запрос.Выполнить(ТекстЗапроса)=0 тогда
		Возврат;
	КонецЕсли;
	ВБ=Рубли;
	ЧислоСтрок=0;
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ДвиженияТоваров");
	Таб.ВывестиСекцию("Отчет");
	Оживить(4);
	Пока Запрос.Группировка("Товар") = 1 Цикл
		НаимТовара=Запрос.ЗначениеУпорядочивания(1,1);
		Если Запрос.Товар.ЭтоГруппа()=1 Тогда
			Таб.ВывестиСекцию("Группа");
			Оживить(1);
			Продолжить;
		Иначе
			ПечНачКол=Формат(Запрос.НачКол,"Ч015.3");
			ПечПрихКол=Формат(Запрос.ПрихКол,"Ч015.3");
			ПечРасхКол=Формат(Запрос.РасхКол,"Ч015.3");
			ПечКонКол=Формат(Запрос.КонКол,"Ч015.3");
			Таб.ВывестиСекцию("Товар");
			Оживить(1);
		КонецЕсли;
		Если Одновременно=1 Тогда
			НК=Запрос.НачКол;
		КонецЕсли;
		Пока Запрос.Группировка("Склад") = 1 Цикл
			НаимСклада=Запрос.ЗначениеУпорядочивания(2,1);
			ПечНачКол=Формат(Запрос.НачКол,"Ч015.3");
			ПечПрихКол=Формат(Запрос.ПрихКол,"Ч015.3");
			ПечРасхКол=Формат(Запрос.РасхКол,"Ч015.3");
			ПечКонКол=Формат(Запрос.КонКол,"Ч015.3");
			Если Режим="Подробно" Тогда
				Если Одновременно=0 Тогда
					НК=Запрос.НачКол;
				КонецЕсли;
				Пока Запрос.Группировка("Док") = 1 цикл
					Док=Запрос.Док;
					Если Док.Выбран()=0 Тогда
						Продолжить;
					КонецЕсли;
					ПечДок=" "+Док.ДатаДок+" № "+СокрП(Док.НомерДок)+"; ";
					ВидДок=Док.Вид();
					Если     НЕ((ВидДок="ВводОстатковТоваров")
					ИЛИ (ВидДок="Перемещение")
					ИЛИ (ВидДок="ПереносОстаткиТоваров")
					ИЛИ (ВидДок="СличительнаяВедомость")
					ИЛИ (ВидДок="ПеремещениеНаСкладГотовойПродукции")
					ИЛИ (ВидДок="АктРазборки")
					ИЛИ	(ВидДок="Списание")) Тогда
						ПечДок=ПечДок+СокрП(Док.Клиент.Наименование);
					КонецЕсли;
					Если Запрос.ПрихКол<>0 Тогда
						Приращение=Запрос.ПрихКол;
						НК=НК+Приращение;
						ПечПриращение=Формат(Приращение,"Ч015.3");
						ПечНК=Формат(НК,"Ч015.3");
						Таб.ВывестиСекцию("Приход");
					КонецЕсли;
					Если Запрос.РасхКол<>0 Тогда
						Приращение=Запрос.РасхКол;
						НК=НК-Приращение;
						ПечПриращение=Формат(Приращение,"Ч015.3");
						ПечНК=Формат(НК,"Ч015.3");
						Таб.ВывестиСекцию("Расход");
					КонецЕсли;
					Оживить(1);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Таб.Опции(0,0,5,0,ПарСтрОтчДлинн);
	Таб.Защита(Константа.ФлагЗащитыТаблиц);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Отчет о движении и остатках блюд и ингредиентов на складах и в подразделениях","");
КонецПроцедуры	           


Процедура ПриОткрытии()
	          
	Если СокрЛП(Форма.Параметр)<>"" Тогда
		Если ТипЗначенияСтр(Форма.Параметр)="СписокЗначений" Тогда
			ДатаНачала=Форма.Параметр.Получить("ДатаНачала")-1;
			ДатаКонца=Форма.Параметр.Получить("ДатаКонца")-1;
			СписокТоваров=Форма.Параметр.Получить("СписокТоваров");
			Сформировать();
			СтатусВозврата(0);
		КонецЕсли;         
	КонецЕсли;  
	                                          
	глПриОткрытии(Контекст,"РасходИнгредиентов");
	
КонецПроцедуры

ПоИнгредиентам=1;
ПоБлюдам=0;
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
ДатаКонца=ПолучитьДатуТА();