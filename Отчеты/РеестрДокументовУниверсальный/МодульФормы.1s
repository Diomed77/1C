 ////////////////////////////////////////////////////////////////////////////
/// Альтернатива  "реестру документов" из типовых конфигураций от  1С под ///
/// компоненту "Оперативный учет", имеющая более развитые средства отбора ///
/// по условиям,  и сушественно большую скорость формирования.  Построена ///
/// на  динамическом  запросе  и  активном  использовании  объектов  типа ///
/// "Метаданные",  оптимизирована в одинаковой степени как под "DBF", так ///
/// и под  "SQL" версии  движка.  Практически  не зависит от используемой ///
/// конфигурации  и  может быть легко адаптирована под любую оригинальную ///
/// конфигурацию.                                                         ///
///                                                                       ///
///	      © 2001 Vladimir Kozlov aka Goatmen (stm62@comset.net)           ///
///////////////////////////////////////////////////////////////////////////
Перем 	ГлТекДок,  /// текущий документ в списке отобранных доков "ВыбДок" 
        ЧислоСтрок,/// номер строки в выходной таблице
		ВыбДоки;   /// список документов для последующей обработки
//---------------------------------------------------
Функция ЕстьРеквДок(ИмяРекв,ВидДок,Шапка=1)
	Если (ПустоеЗначение(ВидДок)=1) ИЛИ (ПустоеЗначение(ИмяРекв)=1) Тогда
		Возврат 0;
	КонецЕсли;
	Если Шапка=1 Тогда
		Рекв=Метаданные.Документ(ВидДок).РеквизитШапки(ИмяРекв);
	Иначе
		Рекв=Метаданные.Документ(ВидДок).РеквизитТабличнойЧасти(ИмяРекв);
	КонецЕсли;	
	Возврат Рекв.Выбран(); 
КонецФункции 
//-------------------------------------------------
Функция ОбщСуммаДок(Док,Имя="Сумма")
	ВидДок=Док.Вид();
	Сумма=0;
	Если ЕстьРеквДок(Имя,ВидДок)=1 Тогда
		Сумма=Док.Сумма;
	ИначеЕсли ЕстьРеквДок(Имя,ВидДок,0)=1 Тогда	
		Если Метаданные.Документ(ВидДок).РеквизитТабличнойЧасти(Имя).ИтогПоКолонке=1 Тогда
			Сумма=Док.Итог(Имя);
		КонецЕсли;	
	КонецЕсли;	
	Возврат Сумма;
КонецФункции  
//-------------------------------------------------
Процедура Оживить(ДобСтрок)
	ЧислоСтрок=ЧислоСтрок+ДобСтрок; 
	Состояние("Число строк, выведенных в отчет : "+ЧислоСтрок);
КонецПроцедуры 
//-------------------------------------------------
Процедура Ругать(Стр)
	Сигнал();
	Предупреждение(Стр);
КонецПроцедуры
//-------------------------------------------- 
// навешена на флажек "ФлИтог" - управление логикой
// видимости/доступности соответствующих реквизитов
Процедура ПриУстФлИтог() 
	Если ФлИтог=1 Тогда	
		Если СпТабРекДок.РазмерСписка()>0 Тогда 
			Если СпТабРекДок.ТекущаяСтрока()>0 Тогда
				Форма.ТекстСумма.Видимость(0);
			КонецЕсли;	
		Иначе
			Форма.ТекстСумма.Видимость(1);
		КонецЕсли; 
	Иначе
		Форма.ТекстСумма.Видимость(1);
	КонецЕсли;	
	Форма.СпТабРекДок.Видимость(ФлИтог); 
	Форма.КнСбрИтог.Видимость(ФлИтог); 
КонецПроцедуры  
//--------------------------------------------------- 
// навешена на поле со списком "СпТабРекДок" - управление 
// видимостью данного реквизита формы
Процедура ПриВыбПоляИтог()  
	Форма.ТекстСумма.Видимость(?(СпТабРекДок.ТекущаяСтрока()>0,0,1));
КонецПроцедуры      
//---------------------------------------------------  
// очищает значения типов отбора,
// навешена на клопки "Х"
// параметр Пар=-1 только когда сбрасывается отбор по полю "СпТабРекДок"
Процедура СбрРекОтб(Спс,Пар=-1)  
	Спс.ТекущаяСтрока(0);
	Если Пар=-1 Тогда  
		Форма.ТекстСумма.Видимость(1);
	Иначе	
		Пар=ПолучитьПустоеЗначение();
	КонецЕсли;	
КонецПроцедуры      
//--------------------------------------------------- 
// предопределенная
Процедура ПриОткрытии()  
	начЛог = семЗаписатьЛогНач( "Отчет", "Универсальный реестр документов", "Событие", "Отчет открыт" );
	Форма.ИспользоватьЗакладки(1);
	Форма.ИспользоватьСлой("Выбор,Общий");
	Форма.Закладки.ДобавитьЗначение(1,"Документы");
	Форма.Закладки.ДобавитьЗначение(2,"Отбор");
	Форма.ПанельИнструментов(0); 
	ДатаКонца=РабочаяДата();
	ДатаНачала=НачМесяца(ДатаКонца);
	ОбрВсе=1; ОбрНеУдал=1; ФлИтог=1;
	// заполним список возможных документов
	КолДок=Метаданные.Документ();
	Для Сч=1 По КолДок Цикл
		ВсеДок.ДобавитьЗначение(Метаданные.Документ(Сч).Идентификатор);
	КонецЦикла;
	// это для эстетики только
	ВсеДок.Сортировать();   
	// список общих реквизитов документов тоже можно заполнить сразу
	КолОРек=Метаданные.ОбщийРеквизитДокумента();
	Для Сч=1 По КолОРек Цикл
		СпРекОбщ.ДобавитьЗначение(Метаданные.ОбщийРеквизитДокумента(Сч).Идентификатор);
	КонецЦикла; 
	
	Если (семЕстьПраво("Отчет","РеестрДокументовУниверсальный","Только не проведенные")=1) ИЛИ (НазваниеНабораПрав()<>"Администратор") Тогда
		ОбрВсе=3;
	КонецЕсли;
	
	ВыбДок.ДобавитьЗначение("Счет");
	ВыбДок.ДобавитьЗначение("РасходнаяНакладная");
	ВыбДок.ДобавитьЗначение("ПриходнаяНакладная");
	ВыбДок.ДобавитьЗначение("Перемещение");
	ВыбДок.Сортировать();

КонецПроцедуры 
//----------------------
// предопределенная
Процедура ПриВыбореЗакладки(Слой,Значен)
	Если Слой=1 Тогда			// закладка документы
		Форма.ИспользоватьСлой("Выбор,Общий"); 
	ИначеЕсли Слой=2 Тогда      // закладка отбор
		Форма.ИспользоватьСлой("Отбор,Общий");
		ПриУстФлИтог(); 
		Если ВыбДок.РазмерСписка()=0 Тогда
			СбрРекОтб(СпРекОбщ,ВыбРекОбщ);  
			СбрРекОтб(СпРекДок,ВыбРекДок); 
			СбрРекОтб(СпРекТаб,ВыбРекТаб); 
		КонецЕсли;	
	КонецЕсли; 
КонецПроцедуры 
//---------------------------------------------------   
// навешена на поле со списком "СпРекОбщ"
// задает тип значения поля "ВыбРекОбщ"
Процедура ПриОтбПоРекОбщ()
	НомОтб=СпРекОбщ.ТекущаяСтрока(); 
	Если НомОтб>0 Тогда						// если общий реквизит выбран
		ТекРекв=Метаданные.ОбщийРеквизитДокумента(НомОтб);
		ТипОтб=ТекРекв.Тип;
		ТекВид=ТекРекв.Вид;
		Если ПустаяСтрока(ТекВид)=0 Тогда
			ТипОтб=ТипОтб+"."+ТекВид;
		КонецЕсли;
		// назначаем тип значения отбора
		Форма.ВыбРекОбщ.НазначитьТип(ТипОтб);  
		Если (ТипОтб="Строка") И (Число(ВыбРекОбщ)=0) Тогда
			ВыбРекОбщ="";  					// просто если этого не делать,
		КонецЕсли;	           				//  то вылазит строка вида "    0" - некрасиво
	Иначе
		Форма.ВыбРекОбщ.НазначитьТип("");   // ежели не выбран, то тип сбрасываем
	КонецЕсли;	
КонецПроцедуры 
//----------------------------------------------------- 
// навешена на поля со списком отбора по ревизитам документа
// задает тип значения поля выбранного значения 
// функционально аналогична процедуре "ПриОтбПоРекОбщ()"
// параметр Таб=1/0 -  выбор табличных/шапочных реквизитов документа
Процедура ПриОтбДок(СпРек,ВыбРек,Таб)
	Если ВыбДок.РазмерСписка()>0 Тогда  
		Если (СпРек.РазмерСписка()>0) И (СпРек.ТекущаяСтрока()>0) Тогда
			ТекДок=ВыбДок.ПолучитьЗначение(ВыбДок.ТекущаяСтрока()); 
			НазвОтб=Строка(СпРек.ПолучитьЗначение(СпРек.ТекущаяСтрока()));
			Если Таб=0 Тогда
				ТекРекв=Метаданные.Документ(ТекДок).РеквизитШапки(НазвОтб);
			Иначе
				ТекРекв=Метаданные.Документ(ТекДок).РеквизитТабличнойЧасти(НазвОтб);
			КонецЕсли;	
			ТипОтб=ТекРекв.Тип;
			ТекВид=ТекРекв.Вид;
			Если ПустаяСтрока(ТекВид)=0 Тогда
				ТипОтб=ТипОтб+"."+ТекВид; 
			КонецЕсли;
			Если Таб=0 Тогда
				Форма.ВыбРекДок.НазначитьТип(ТипОтб);
			Иначе	
				Форма.ВыбРекТаб.НазначитьТип(ТипОтб);
			КонецЕсли;	
			Если (ТипОтб="Строка") И (ПустоеЗначение(ВыбРек)=1) Тогда
				ВыбРек="";
            КонецЕсли;
			СпРек.ПолучитьЗначение(СпРек.ТекущаяСтрока());   
		Иначе
			Если Таб=0 Тогда
				Форма.ВыбРекДок.НазначитьТип("");
			Иначе	
				Форма.ВыбРекТаб.НазначитьТип("");
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры 
//--------------------------------------------------- 
// удаление текущего документа из списка отобранных доков "ВыбДок"
Процедура Искл()
	Если ВыбДок.РазмерСписка()>0 Тогда
		ТекПоз=ВыбДок.ТекущаяСтрока();  
		ВыбДок.УдалитьЗначение(ТекПоз);
		ВыбДок.ТекущаяСтрока(Мин(ТекПоз,ВыбДок.РазмерСписка()));
	Иначе
		Сигнал();
	КонецЕсли;
КонецПроцедуры                                             
//--------------------------------------------------- 
// очистка списка отобранных доков "ВыбДок"
Процедура ИсклВсе()
	Если ВыбДок.РазмерСписка()>0 Тогда
		ВыбДок.УдалитьВсе();
	Иначе
		Сигнал();
	КонецЕсли;
КонецПроцедуры
//--------------------------------------------------- 
// перенос текущего документа из списка всех документов
// "ВсеДок" в список отобранных документов "ВыбДок"
Процедура Вкл()
   	ТекЗн=ВсеДок.ПолучитьЗначение(ВсеДок.ТекущаяСтрока());
   	Если ВыбДок.НайтиЗначение(ТекЗн)=0 Тогда
		ВыбДок.ДобавитьЗначение(ТекЗн);
		ВыбДок.Сортировать();
	Иначе
		Сигнал();
	КонецЕсли;
КонецПроцедуры
//---------------------------------------------------  
// перенос  всех документов из списка "ВсеДок"
// в список отобранных документов "ВыбДок"
Процедура ВклВсе()
	ВыбДок.УдалитьВсе();
	ВсеДок.Выгрузить(ВыбДок); 
	ВыбДок.ТекущаяСтрока(1);  
КонецПроцедуры
//----------------------------------------------------- 
// для динамического обновления списков отбора - вызов в 
// формуле скрытого текстового реквизита формы, именно
// по этому функция - вызов процедуры так не проходит
Функция УстСпсРек()  
	Если ВыбДок.РазмерСписка()>0 Тогда     	// список отобранных доков не пустой
		ТекДок=ВыбДок.ПолучитьЗначение(ВыбДок.ТекущаяСтрока()); 
		Если ГлТекДок<>ТекДок Тогда        	// текущий документ другой - обновляем реквизиты :
			Форма.ВыбРекДок.НазначитьТип("");  
			Форма.ВыбРекТаб.НазначитьТип(""); 
			СпРекДок.УдалитьВсе();         	// ... шапки документа
			Кол=Метаданные.Документ(ТекДок).РеквизитШапки();  
			Для Сч=1 По Кол Цикл 
				СпРекДок.ДобавитьЗначение(Метаданные.Документ(ТекДок).РеквизитШапки(Сч).Идентификатор);
			КонецЦикла;
			СпРекТаб.УдалитьВсе();      	// ... реквизитов табличной части документа,
			СпТабРекДок.УдалитьВсе();      	// ... реквизитов для суммирования итогов,
			Кол=Метаданные.Документ(ТекДок).РеквизитТабличнойЧасти();
			Для Сч=1 По Кол Цикл 
				ТекРек=Метаданные.Документ(ТекДок).РеквизитТабличнойЧасти(Сч);
				// причем только тех, у которых выставлен итог по колонке
				Если (ТекРек.Тип="Число") И (ТекРек.ИтогПоКолонке=1) Тогда
					СпТабРекДок.ДобавитьЗначение(ТекРек.Идентификатор);
				Иначе
					СпРекТаб.ДобавитьЗначение(ТекРек.Идентификатор);
				КонецЕсли;	
			КонецЦикла;
			ГлТекДок=ТекДок;               	// запомним обработанный документ в глобальной переменной
		КонецЕсли;  
		// теперь разберемся с логикой доступности
		ФлРекДок=?(СпРекДок.РазмерСписка()>0,1,0);
		Форма.СпРекДок.Доступность(ФлРекДок); Форма.ВыбРекДок.Доступность(ФлРекДок);
		ФлРекТаб=?(СпРекТаб.РазмерСписка()>0,1,0);
		Форма.СпРекТаб.Доступность(ФлРекТаб); Форма.ВыбРекТаб.Доступность(ФлРекТаб);
		Форма.ФлИтог.Доступность(1); 
		Форма.ТекстСумма.Доступность(1); 
	Иначе 
		СбрРекОтб(СпРекОбщ,ВыбРекОбщ);  
		Форма.СпРекДок.Доступность(0); Форма.ВыбРекДок.Доступность(0); 
		Форма.СпРекТаб.Доступность(0); Форма.ВыбРекТаб.Доступность(0);
		Форма.ФлИтог.Доступность(0); Форма.ТекстСумма.Доступность(0); 
	КонецЕсли;
	Возврат "";
КонецФункции   
//-----------------------------------------------------
// используется для динамического формирования запроса,
// "Запр" - это текст запроса, непустой параметр "Имя" - это
// имя поля отбора (по реквизитам шапки и общим реквизитам доков)
Процедура ВклВЗапрос(Запр,Имя="")
	КолДок=ВыбДок.РазмерСписка();
	Для Сч=1 По КолДок Цикл  
		Запр=Запр+?(Сч=1,"",РазделительСтрок)+"Документ."+Строка(ВыбДок.ПолучитьЗначение(Сч))+?(ПустаяСтрока(Имя)=1,".ТекущийДокумент","."+Имя)+?(Сч<КолДок,",",";");
	КонецЦикла;
КонецПроцедуры      
//----------------------------------------------------- 
Процедура ВклОтбор(Тело,Выб,ВыбИмя,Имя,Ном);
	Если ПустоеЗначение(Выб)=0 Тогда     	// если отбор установлен
		ИмяОтб="Отб"+Ном;
		Тело=Тело+РазделительСтрок+ИмяОтб+"=";  	// то включим его в запрос
		ВклВЗапрос(Тело,Имя);
		Если ТипЗначения(Выб)=2 Тогда    			// если строка, то условие наоборот
			Тело=Тело+РазделительСтрок+"Условие (СокрЛП("+ВыбИмя+") В "+ИмяОтб+");";
		Иначе	
			Тело=Тело+РазделительСтрок+"Условие ("+ИмяОтб+" В "+ВыбИмя+");"; 
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры 
//----------------------------------------------------- 
Функция ИмяОтбора(Сп)        
	Имя="";        
	Если Сп.РазмерСписка()>0 Тогда  	// проверим отбор по реквизитам документа
		НомОтб=Сп.ТекущаяСтрока();
		Если НомОтб>0 Тогда
			Имя=Сп.ПолучитьЗначение(НомОтб); 
		КонецЕсли;	
	КонецЕсли;  
    Возврат Имя;
КонецФункции	
//----------------------------------------------------- 
// собственно сама процедура формирования реестра документов
Процедура Выполнить() 
	ИмяВыбРекДок=ИмяОтбора(СпРекДок); 
	ИмяВыбРекТаб=ИмяОтбора(СпРекТаб); 
	Если ПустоеЗначение(ВыбРекОбщ)=0 Тогда	// и по общим реквизитам
		ИмяВыбОбщРек=СпРекОбщ.ПолучитьЗначение(СпРекОбщ.ТекущаяСтрока());
	Иначе
		ИмяВыбОбщРек="";
	КонецЕсли;	
	Если ФлИтог=1 Тогда
	   	ОбщСумма=0;                         
	   	Если СпТабРекДок.РазмерСписка()>0 Тогда  	// если итоговая колонка определена
	   		ТекСтр=СпТабРекДок.ТекущаяСтрока();     // запомним ее имя
	   		Если ТекСтр>0 Тогда
				ИмяТабРекДок=СпТабРекДок.ПолучитьЗначение(ТекСтр); 
			КонецЕсли;	
		Иначе
			ИмяТабРекДок="";
		КонецЕсли;	
	КонецЕсли;
	ТекПоз=ВыбДок.РазмерСписка();
	Пока ТекПоз>0 Цикл 	// удалим документы из списка, не содержащие отобранных реквизитов,
		Вид=Строка(ВыбДок.ПолучитьЗначение(ТекПоз)); // сделаем это перебором списка в цикле с конца
		Если ((ПустоеЗначение(ВыбРекДок)=0) И (ЕстьРеквДок(ИмяВыбРекДок,Вид)=0)) ИЛИ 
		     ((ПустоеЗначение(ВыбРекТаб)=0) И (ЕстьРеквДок(ИмяВыбРекТаб,Вид,0)=0)) ИЛИ 
		     ((ПустаяСтрока(ИмяТабРекДок)=0) И (ЕстьРеквДок(ИмяТабРекДок,Вид,0)=0)) Тогда
			ВыбДок.УдалитьЗначение(ТекПоз);
		КонецЕсли;
		ТекПоз=ТекПоз-1;
	КонецЦикла;	
	Если ВыбДок.РазмерСписка()>0 Тогда
		/// готово - теперь формируем сам запрос по установленным условиям
		СтрОбрВсе="Все"+РазделительСтрок+"Проведенные"+РазделительСтрок+"Непроведенные";
		СтрОбрНеУдал="НеПомеченныеНаУдаление"+РазделительСтрок+"ПомеченныеНаУдаление";
		Запрос=СоздатьОбъект("Запрос");
		Тело="Период с ДатаНачала по ДатаКонца;
			 |Обрабатывать "+СтрПолучитьСтроку(СтрОбрНеУдал,ОбрНеУдал)+";  
			 |ОбрабатыватьДокументы "+СтрПолучитьСтроку(СтрОбрВсе,ОбрВсе)+";
			 |Док="; 
		ВклВЗапрос(Тело);        		// здесь перечислятся все типы подходящих документов
		Тело=Тело+РазделительСтрок+"Группировка Док;"; 
		ВклОтбор(Тело,ВыбРекОбщ,"ВыбРекОбщ",ИмяВыбОбщРек,1);
		ВклОтбор(Тело,ВыбРекДок,"ВыбРекДок",ИмяВыбРекДок,2); 
		ВклОтбор(Тело,ВыбРекТаб,"ВыбРекТаб",ИмяВыбРекТаб,3);                
		
		// закомментированный ниже кусок выводит сформированное тело запроса
		// (отладочная опция)
		//ОчиститьОкноСообщений();
		//КолСтр=СтрКоличествоСтрок(Тело);
		//Для Стр=1 По КолСтр Цикл 
		//	Сообщить(СтрПолучитьСтроку(Тело,Стр));
		//КонецЦикла;
		
		// все - текст запроса готов, теперь собственно выполняем запрос 
		Если Запрос.Выполнить(Тело)>0 Тогда
	   		Таб=СоздатьОбъект("Таблица");
			ПечИмяСуммы=?(ПустоеЗначение(ИмяТабРекДок)=1,"Сумма",ИмяТабРекДок);
			ПечИмяОбщРек=?(ПустоеЗначение(ИмяВыбОбщРек)=1,"Автор",ИмяВыбОбщРек);
   			Таб.ВывестиСекцию("Отчет"); 
		   	ЧислоСтрок=0;
			Пока Запрос.Группировка(1)=1 Цикл    		// группировка по документам
				Докум=Запрос.Док; 
				ДВид=Докум.Вид();
				Если Докум.ПометкаУдаления()>0 Тогда
					ПечСтатус="х";              	// так обозначим помеченные на удаление доки
				ИначеЕсли Докум.Проведен()>0 Тогда
					ПечСтатус="+";             		// проведенные обозначим так
				Иначе
					ПечСтатус="-";              	// а так соответственно не проведенные
				КонецЕсли;
				Если ПустаяСтрока(ИмяВыбОбщРек)=1 Тогда 
					Если ЕстьРеквДок("Автор",ДВид)=1 Тогда
						ФИО=Докум.Автор.Наименование;   // оставим только фамилию с инициалами
						ПозИм=Найти(ФИО," ")+1;
						ИО=Сред(ФИО,ПозИм);
						ПозОт=Найти(ИО," ")+1;
						ПечОбщРек=Лев(ФИО,ПозИм)+"."+Сред(ИО,ПозОт,1)+"."; 
					Иначе
						ПечОбщРек="";
					КонецЕсли;	
				Иначе
					ПечОбщРек=ВыбРекОбщ;
				КонецЕсли;	
				Если ЕстьРеквДок("Клиент",ДВид)=1 Тогда
					ПечКлиент=СокрП(Докум.Клиент);
				ИначеЕсли ЕстьРеквДок("Склад",ДВид)=1 Тогда
					ПечКлиент=СокрП(Докум.Склад);
				ИначеЕсли ЕстьРеквДок("РасчетныйСчет",ДВид)=1 Тогда
					ПечКлиент=СокрП(Докум.РасчетныйСчет);
				Иначе
					ПечКлиент="";
				КонецЕсли;
				ПечСумма=?(ПустоеЗначение(ИмяТабРекДок)=1,ОбщСуммаДок(Докум),ОбщСуммаДок(Докум,ИмяТабРекДок));
	   			Если ФлИтог=1 Тогда                 // копим итог если надо
					ОбщСумма=ОбщСумма+ПечСумма;
   				КонецЕсли;
				Оживить(1); 
				Таб.ВывестиСекцию("Документ");
			КонецЦикла;
	   		Если ФлИтог=1 Тогда
				Таб.ВывестиСекцию("Итог");
   			КонецЕсли;
			Таб.Опции(0,0,3,0);
			Таб.ТолькоПросмотр(1);
			Таб.Показать("Реестр Документов","");
		Иначе	
			Ругать("Системная ошибка выполнения запроса!"); 
		КонецЕсли;
	Иначе
		Ругать("Не выбраны обрабатываемые документы!");
	КонецЕсли;	
КонецПроцедуры	
