Перем ДокПодч;
Перем ТекущТовар;
Перем ПустаяФирма;
Перем ПереченьКодовОпераций;
//------------------------------- 
 Процедура ПриОткрытии()
	Если Константа.ПартииНаСкладах<>Перечисление.Булево.Да Тогда
		Форма.ВыбСклад.Видимость(0);
		Форма.КнопкаСклад.Видимость(0); 
		Форма.ТекстСкл.Видимость(0);
		ВыбСклад=0;
	КонецЕсли;	         
	
	глПриОткрытии(Контекст,"УпрПродажиБлюд");
КонецПроцедуры  
//-------------------------------
Процедура Продажа(Режим)
	начЛог = семЗаписатьЛогНач( "Отчет", "Упр. отчет по продажам блюд", "Обработка", Шаблон("Формирование отчета ([Режим])") );
	ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,"Упр");
	Заг="";
	ПустаяФирма=глФирма;
	Если ДатаКонца>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонца=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонца; 
	КонецЕсли;
	ПереченьКодовОпераций=СоздатьОбъект("СписокЗначений");
	ПереченьКодовОпераций.ДобавитьЗначение(ПродажаБлюда);
	ПереченьКодовОпераций.ДобавитьЗначение(ПродажаТовара);
	
	//ПродажаТовара+ПроданоРеализатором+ПродажаПринятогоТовара;
	
	Запрос=СоздатьОбъект("Запрос");
	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала По ДатаКонца;
	|ВидТовара=Регистр.ПартииТоваров.Товар.ВидТовара;
	|Товар=Регистр.ПартииТоваров.Товар;
	|Фирма=Регистр.ПартииТоваров.Фирма;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент;
	|Докум=Регистр.ПартииТоваров.ТекущийДокумент;
	|Клиент=Регистр.ПартииТоваров.ТекущийДокумент.РасходнаяНакладная.Клиент,Регистр.ПартииТоваров.ТекущийДокумент.ОтчетРеализатора.Клиент,Регистр.ПартииТоваров.ТекущийДокумент.ИнвентаризацияРеализатора.Клиент;
	|ОстатокТовара=Регистр.ПартииТоваров.ОстатокТовара;
	|Оборот=Регистр.ПартииТоваров.Оборот;
	|КодОперации=Регистр.ПартииТоваров.КодОперации;";
     Если ВыбСклад.Выбран()<>0 Тогда         
		ТекстЗапроса=ТекстЗапроса+"
		|Склад=Регистр.ПартииТоваров.Склад;";
	КонецЕсли;
	Если (ПоТоварам=1)  Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Товар Без групп;
		|Группировка Клиент Без групп;";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Клиент Без групп;
		|Группировка Товар Без групп;";
	КонецЕсли;
	Если (Режим="Подробный")  Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Докум;";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|Функция РасхОстатокТовара=Расход(ОстатокТовара);
	|Функция ОборотТовара=Сумма(Оборот);
	|Условие (Фирма=ПустаяФирма);
	|Условие ((КодОперации=ПродажаБлюда) ИЛИ (КодОперации=ПродажаТовара));
	|";
	
	Если ВыбСклад.Выбран() = 0 Тогда         
		Заг=Заг+"   По всем складам"+". ";
	ИначеЕсли ВыбСклад.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие (Склад в ВыбСклад);"; 
		Заг=Заг+"   По группе складов: " + СокрП(ВыбСклад.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие (Склад=ВыбСклад);"; 
		Заг=Заг+"   По складу: " + СокрП(ВыбСклад.Наименование)+". ";
	КонецЕсли;
	
	Если ВыбТовар.Выбран()=0 Тогда
		Заг=Заг+"По всем товарам"+". ";
	ИначеЕсли ВыбТовар.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар в ВыбТовар);";
		Заг=Заг+"По товарам группы: "+СокрП(ВыбТовар.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		Заг=Заг+"По товару: " + СокрП(ВыбТовар.Наименование)+". ";
	КонецЕсли;
	
	Если ВыбКлиент.Выбран()=0 Тогда
		Заг=Заг+"По всем клиентам"+". ";
	ИначеЕсли ВыбКлиент.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Клиент в ВыбКлиент);";
		Заг=Заг+"По клиентам группы: "+СокрП(ВыбКлиент.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Клиент=ВыбКлиент);";
		Заг=Заг+"По клиенту: " + СокрП(ВыбКлиент.Наименование)+". ";
	КонецЕсли;
	//ТекстЗапроса=ТекстЗапроса+"Условие(Товар.ВидТовара=Перечисление.ВидыТоваров.Блюдо);"; 
	ТекстЗапроса=ТекстЗапроса+"Условие(ВидТовара=Перечисление.ВидыТоваров.Блюдо);";
	Таб=СоздатьОбъект("Таблица");
	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!");
		Возврат;
	КонецЕсли;
	ВалютаОтчета=?(ВыбВалюта.Выбран()=0,Константа.ВалютаВзаиморасчетов,ВыбВалюта);
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
	Оживить(4);
	ИтогоОборот=0;
	Если (ПоТоварам=1)  Тогда
		Таб.ВывестиСекцию("ШапкаТов");
		Пока Запрос.Группировка("Товар")=1 Цикл
			ТекТовар=Запрос.Товар;
			Если ТекТовар.Выбран()=0 Тогда
				Продолжить;
			КонецЕсли;
			ВУ=ТекТовар.ВалютаУчета;
			ВП=?(ВыбВалюта.Выбран()=0,ВУ,ВыбВалюта);
			ПечТовар=СокрП(ТекТовар.Наименование);
			ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
			ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВП,ДатаКонца),ВП,1);
			Таб.ВывестиСекцию("Товар");
			Оживить(1);
			ИтогоОборот=ИтогоОборот+Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВалютаОтчета,ДатаКонца);
			Пока Запрос.Группировка("Клиент") = 1 Цикл
				ПечКлиент=СокрП(Запрос.Клиент.Наименование);
				ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
				ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВП,ДатаКонца),ВП,1);
				Таб.ВывестиСекцию("РасходКлиент");
				Оживить(1);
				Если (Режим="Подробный")  Тогда
					Пока Запрос.Группировка("Докум") = 1 Цикл 
						Док=Запрос.Докум;
						Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
							Продолжить;
						КонецЕсли;
						ПечНомерДок=""+СокрП(Док)+" от "+Док.ДатаДок;
						ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
						ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВП,ДатаКонца),ВП,1);
						Таб.ВывестиСекцию("Документ");
						Оживить(1);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		Таб.ВывестиСекцию("ШапкаКлн");
		Пока Запрос.Группировка("Клиент")=1 Цикл
			ПечКлиент=СокрП(Запрос.Клиент.Наименование);
			ОборотКлиента=0;
			Таб.ВывестиСекцию("Клиент");
			Оживить(1);
			Пока Запрос.Группировка("Товар")=1 Цикл
				ТекТовар=Запрос.Товар;
				Если ТекТовар.Выбран()=0 Тогда
					Продолжить;
				КонецЕсли;
				ВУ=ТекТовар.ВалютаУчета;
				ВП=?(ВыбВалюта.Выбран()=0,ВУ,ВыбВалюта);
				ПечТовар=СокрП(ТекТовар.Наименование);
				ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
				ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВП,ДатаКонца),ВП,1);
				Таб.ВывестиСекцию("РасходТовар");
				Оживить(1);
				ОборотКлиента=ОборотКлиента+Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВалютаОтчета,ДатаКонца);
				Если (Режим="Подробный")  Тогда
					Пока Запрос.Группировка("Докум") = 1 Цикл 
						Док=Запрос.Докум;
						Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
							Продолжить;
						КонецЕсли;
						ПечНомерДок=""+СокрП(Док)+" от "+Док.ДатаДок;
						ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
						ПечОборот=ФРМ(Пересчет(Запрос.ОборотТовара,ВУ,ДатаКонца,ВП,ДатаКонца),ВП,1);
						Таб.ВывестиСекцию("Документ");
						Оживить(1);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			ИтогоОборот=ИтогоОборот+ОборотКлиента;
			ПечОборот=ФРМ(ОборотКлиента,ВалютаОтчета,1);
			Таб.ВывестиСекцию("ИтогоПоКлиенту");
			Оживить(1);
		КонецЦикла;
	КонецЕсли;
	ПечИтогоОборот=ФРМ(ИтогоОборот,ВалютаОтчета,1);
	Таб.ВывестиСекцию("Итого");
	Оживить(1);
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,4,0);
	Таб.Показать("Отчет по продажам товаров (упр. учет)","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------
Процедура ПриВыбореПоТоварам()
	Если ПоТоварам=1 Тогда
		ПоКлиентам=0;
	Иначе
		ПоКлиентам=1;
	КонецЕсли;
КонецПроцедуры
//-------------------------------
Процедура ПриВыбореПоКлиентам()
	Если ПоКлиентам=1 Тогда
		ПоТоварам=0;
	Иначе
		ПоТоварам=1;
	КонецЕсли;
КонецПроцедуры
//-------------------------------
ДатаКонца=ПолучитьДатуТА();
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;  
ДокПодч=СоздатьОбъект("Документ");
ВыбКупленный=1;
ВыбОтданный=1;
ВыбПринятый=1;
ПоТоварам=1;
ВыбВалюта=Рубли;
