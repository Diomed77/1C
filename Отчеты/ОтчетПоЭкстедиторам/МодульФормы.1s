Функция ПредставлениеУсловия()
	стр = "";
	Если выбЭкспедитор.Выбран() = 1 Тогда
		Если выбЭкспедитор.ЭтоГруппа() = 1 Тогда 
			стр = стр + Шаблон( "По группе экспедиторов [выбЭкспедитор.Код] [выбЭкспедитор]. ");
		Иначе
			стр = стр + Шаблон( "По экспедитору [выбЭкспедитор.Код] [выбЭкспедитор]. ");
		КонецЕсли;
	КонецЕсли;
	Если выбАдрес.Выбран() = 1 Тогда
		Если выбАдрес.ЭтоГруппа() = 1 Тогда 
			стр = стр + Шаблон( "По группе адресов [выбАдрес.Код] [выбАдрес]. ");
		Иначе
			стр = стр + Шаблон( "По адресу [выбАдрес.Код] [выбАдрес]. ");
		КонецЕсли;
	КонецЕсли;
	Возврат стр;
КонецФункции

Функция ТекстЗапроса()
	ТекстЗапроса = "
	|Период с ДатаС по ДатаПо;
	|Экспедитор = Документ.МаршрутныйЛист.Экспедитор;
	|Экспедитор2 = Документ.МаршрутныйЛист.Экспедитор2;
	//|КилНач = Документ.МаршрутныйЛист.КилометражНач;
	//|КилКон = Документ.МаршрутныйЛист.КилометражКон;
	|Клиент = Документ.МаршрутныйЛист.Клиент;
	|Адрес = Документ.МаршрутныйЛист.Адрес;
	|Функция Сч = Счётчик( Клиент );
	|Группировка Экспедитор без групп;
	|Группировка День;
	|Группировка Документ;
	|Группировка Клиент без групп;";
	Если выбЭкспедитор.Выбран() = 1 Тогда
		Если выбЭкспедитор.ЭтоГруппа() = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
			|Условие ( Экспедитор в выбЭкспедитор );";
		Иначе ТекстЗапроса = ТекстЗапроса + "
			|Условие ( Экспедитор = выбЭкспедитор );";
		КонецЕсли;
	КонецЕсли;
	Если выбАдрес.Выбран() = 1 Тогда
		Если выбАдрес.ЭтоГруппа() = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
			|Условие ( Адрес в выбАдрес );";
		Иначе ТекстЗапроса = ТекстЗапроса + "
			|Условие ( Адрес = выбАдрес );";
		КонецЕсли;
	КонецЕсли;
	Возврат ТекстЗапроса;
КонецФункции

Функция ПолучитьКилометраж( док )
	пер = СоздатьОбъект("Периодический");
	пер.ИспользоватьОбъект("Пробег", док.Машина);
	пер.ОбратныйПорядок();
	пер.ВыбратьЗначения(,док.ДатаДок);
	Флаг = 0;
	Пока пер.ПолучитьЗначение() = 1  Цикл
		Если Флаг = 1 Тогда
			Возврат пер.Значение;
		КонецЕсли;
		Если пер.ТекущийДокумент() = док Тогда Флаг = 1; КонецЕсли;
	КонецЦикла;
	Возврат 0;
КонецФункции

Процедура Сформировать()
	начЛог = семЗаписатьЛогНач( "Отчет", "Отчет по экспедиторам", "Обработка", "Формирование отчета" );
	Запрос = СоздатьОбъект( "Запрос" );
	Если Запрос.Выполнить( ТекстЗапроса() ) = 0 Тогда Возврат; КонецЕсли;
	//табл = СоздатьОбъект( "ТаблицаЗначений" );
	//Запрос.Выгрузить(табл,1,0);
	//табл.ВыбратьСтроку();
	
	таб = СоздатьОбъект( "Таблица" );
	таб.ИсходнаяТаблица( "Таблица" );
	пПериод = Шаблон( "За период с [ДатаС] по [ДатаПо]." );
	пФильтр = ПредставлениеУсловия();
	таб.ВывестиСекцию( "Заголовок" );		
	таб.ВывестиСекцию( "Шапка" );		
	таб.Опции( 0, 0, таб.ВысотаТаблицы(), 0 );
	Всего = 0; н = 0;
	Пока Запрос.Группировка( "Экспедитор" ) = 1 Цикл
		Пока Запрос.Группировка( "День" ) = 1 Цикл
			Пока Запрос.Группировка( "Документ" ) = 1 Цикл
				Всего = Всего + 1;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	сумЭкспАдр = 0;
	сумЭкспКил = 0;
	сумДеньАдр = 0;
	сумДеньКил = 0;
	сумДокАдр = 0;
	сумДокКил = 0;
	Пока Запрос.Группировка( "Экспедитор" ) = 1 Цикл
		пЭкспедитор = Запрос.Экспедитор;
		таб.ВывестиСекцию( "Экспедитор" );		
		облЭкспАдр = таб.Область(Шаблон("R[таб.ВысотаТаблицы()]C3"));
		облЭкспКил = таб.Область(Шаблон("R[таб.ВысотаТаблицы()]C4"));
		
		Пока Запрос.Группировка( "День" ) = 1 Цикл
			Если флПоДатам = 1 Тогда
				пДата = Запрос.День;
				таб.ВывестиСекцию( "Дата" );
				облДеньАдр = таб.Область(Шаблон("R[таб.ВысотаТаблицы()]C3"));
				облДеньКил = таб.Область(Шаблон("R[таб.ВысотаТаблицы()]C4"));
			КонецЕсли;

			Пока Запрос.Группировка( "Документ" ) = 1 Цикл Состояние(Шаблон("Обработано [н] из [Всего] документов."));
				н = н + 1;
				сумДокАдр = 0;
				сумДокКил = Запрос.Документ.КилометражКон - ПолучитьКилометраж( Запрос.Документ );
				Если флПоДокументам = 1 Тогда
					пДата = Шаблон("[Запрос.Документ.ДатаДок] [Запрос.Документ.Экспедитор2]");
					пДокумент = Шаблон("М.л. [Запрос.Документ.НомерДок]");
					таб.ВывестиСекцию( "Документ" );		
				КонецЕсли;
				облДокАдр = таб.Область(Шаблон("R[таб.ВысотаТаблицы()]C3"));
				Пока Запрос.Группировка( "Клиент" ) = 1 Цикл
					сумДокАдр = сумДокАдр + 1;
				КонецЦикла;
				облДокАдр.Текст = сумДокАдр;
				сумДеньАдр = сумДеньАдр + сумДокАдр; сумДокАдр = 0;
				сумДеньКил = сумДеньКил + сумДокКил; сумДокКил = 0;
			КонецЦикла;//Документ
			Если флПоДатам = 1 Тогда
				облДеньАдр.Текст = сумДеньАдр; 
				облДеньКил.Текст = сумДеньКил; 
			КонецЕсли;
			сумЭкспАдр = сумЭкспАдр + сумДеньАдр; сумДеньАдр = 0;
			сумЭкспКил = сумЭкспКил + сумДеньКил; сумДеньКил = 0;
		КонецЦикла;//День

		облЭкспАдр.Текст = сумЭкспАдр; сумЭкспАдр = 0;
		облЭкспКил.Текст = сумЭкспКил; сумЭкспКил = 0; 
	КонецЦикла;//Экспедитор
	таб.ТолькоПросмотр( 1 );
	таб.Показать( "Отчет по экспедиторам" );
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

Процедура ПроверкаПрав()
	спПрав = СоздатьОбъект( "СписокЗначений" );
	спПрав.ДобавитьЗначение("Администратор");
	спПрав.ДобавитьЗначение("РуководительЛогистики");
	спПрав.ДобавитьЗначение("Просмотр");
	Если спПрав.НайтиЗначение( НазваниеНабораПрав() ) = 0 Тогда
		Предупреждение( "Нет прав." );
		СтатусВозврата( 0 );
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	//ПроверкаПрав();
КонецПроцедуры