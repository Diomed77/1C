//-------------------------------
Перем ДокПодч;
Перем ТекущТовар;
Перем ПустаяФирма;
//-------------------------------
Процедура Партии(Режим)
	начЛог = семЗаписатьЛогНач( "Отчет", "Фин. отчет по реализации", "Обработка", Шаблон("Формирование отчета ([Режим])") );

	Заг="";
	Если ВыбФирма.Выбран()=1 Тогда
		Заг=Заг+"По фирме: "+СокрП(ВыбФирма.Наименование)+". ";
	Иначе
		Предупреждение("Выберите фирму!");
		Возврат;
	КонецЕсли;

    // Здесь формируется отчет, который использует регистры, критичные к 
    // последовательности проведения документов
    // поэтому сравним установленные даты периода формируемого отчета с ГП 
    ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Фин",ВыбФирма);

	// Сделаем это при помощи механизма Запросов
 	Рег1=СоздатьОбъект("Регистр.ПартииТоваров");

	Если ДатаКонец>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонец=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонец; 
	КонецЕсли;
                                                     
	Пустой="";
	
	Запрос=СоздатьОбъект("Запрос");
	
	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала По ДатаКонец;
	|Товар=Регистр.ПартииТоваров.Товар;
	|Фирма=Регистр.ПартииТоваров.Фирма;
	|Статус=Регистр.ПартииТоваров.Статус;
	|Контрагент=Регистр.ПартииТоваров.Контрагент;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент;
	|Докум=Регистр.ПартииТоваров.ТекущийДокумент;
	|ОстатокТовара=Регистр.ПартииТоваров.ОстатокТовара;
	|Стоимость=Регистр.ПартииТоваров.Стоимость;
	|НДС=Регистр.ПартииТоваров.НДС;
	|ПродСтоимость=Регистр.ПартииТоваров.ПродСтоимость;
	|Оборот=Регистр.ПартииТоваров.Оборот;
	|КодОперации=Регистр.ПартииТоваров.КодОперации;
	|Группировка Контрагент;
	|Группировка Статус;
	|Группировка Товар Без групп;";
    Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка ПрихДокумент;";
	КонецЕсли;
	Если (Режим="Подробный")  Тогда
		ТекстЗапроса=ТекстЗапроса+"
		|Группировка Докум;";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|Функция НачОстатокТовара=НачОст(ОстатокТовара);
	|Функция КонОстатокТовара=КонОст(ОстатокТовара);
	|Функция ПрихОстатокТовара=Приход(ОстатокТовара);
	|Функция РасхОстатокТовара=Расход(ОстатокТовара) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхОстатокТовараВозвр=Расход(ОстатокТовара) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачСтоимость=НачОст(Стоимость);
	|Функция КонСтоимость=КонОст(Стоимость);
	|Функция ПрихСтоимость=Приход(Стоимость);
	|Функция РасхСтоимость=Расход(Стоимость) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхСтоимостьВозвр=Расход(Стоимость) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачНДС=НачОст(НДС);
	|Функция КонНДС=КонОст(НДС);
	|Функция ПрихНДС=Приход(НДС);
	|Функция РасхНДС=Расход(НДС) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхНДСВозвр=Расход(НДС) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачПродСтоимость=НачОст(ПродСтоимость);
	|Функция КонПродСтоимость=КонОст(ПродСтоимость);
	|Функция ПрихПродСтоимость=Приход(ПродСтоимость);
	|Функция РасхПродСтоимость=Расход(ПродСтоимость) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхПродСтоимостьВозвр=Расход(ПродСтоимость) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция ОборотТовара=Сумма(Оборот);         
	|Условие ((Статус=Отданный) Или (Статус=Принятый));  
	|Условие (Фирма=ВыбФирма);
	|";
	
	Если ВыбТовар.Выбран()=0 Тогда
		Заг=Заг+"По всем товарам"+". ";
	ИначеЕсли ВыбТовар.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар.ПринадлежитГруппе(ВыбТовар)=1);";
		Заг=Заг+"По товарам группы: "+СокрП(ВыбТовар.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		Заг=Заг+"По товару: " + СокрП(ВыбТовар.Наименование)+". ";
	КонецЕсли;

	Если ВыбКлиент.Выбран()=0 Тогда
		Заг=Заг+"По всем контрагентам"+". ";
	ИначеЕсли ВыбКлиент.ЭтоГруппа()=1 Тогда
		ТекстЗапроса = ТекстЗапроса+"Условие (Контрагент.ПринадлежитГруппе(ВыбКлиент)=1);";
		Заг=Заг+"По контрагентам группы "+ВыбКлиент.Наименование+". ";
	Иначе
		ТекстЗапроса = ТекстЗапроса+"Условие (Контрагент=ВыбКлиент);";
		Заг=Заг+"По контрагенту: "+СокрП(ВыбКлиент.Наименование)+". ";
	КонецЕсли;
	
	Если (ВыбОтданный=0) И (ВыбПринятый=0) Тогда
		Заг=Заг+"По всем статусам партий"+". ";
	Иначе
		Заг=Заг+"По статусам партий: ";
		Если ВыбОтданный=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Отданный);";
		Иначе
			Заг=Заг+"Отданный"+", ";
		КонецЕсли;
		Если ВыбПринятый=0 Тогда
			ТекстЗапроса=ТекстЗапроса+"Условие(Статус<>Принятый);";
		Иначе
			Заг=Заг+"Принятый"+". ";
		КонецЕсли;
	КонецЕсли;

	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	
	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!");
		Возврат;
	КонецЕсли;

	// теперь в запросе собраны все Документы по партиям 
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
    Если Режим="Краткий" Тогда
		Таб.ВывестиСекцию("ШапкаТов");
	Иначе
		Таб.ВывестиСекцию("ШапкаДок");
	КонецЕсли;
	Оживить(4);

	Пока Запрос.Группировка("Контрагент") = 1 Цикл
		Пока Запрос.Группировка("Статус") = 1 Цикл
			Номенклатура=0;
			Пока Запрос.Группировка("Товар") = 1 Цикл
		        Номенклатура=Номенклатура+1;
			КонецЦикла;
			Досье=СокрП(Запрос.Контрагент.Наименование)+"; ";
			Если Запрос.Статус=Отданный Тогда
			    Досье=Досье+"Отданных на реализацию товаров числится "+Номенклатура+" видов. ";
				ЗагСтатуса="Отдано на реализацию";
			Иначе
			    Досье=Досье+"Принятых на реализацию товаров числится "+Номенклатура+" видов. ";
				ЗагСтатуса="Принято на реализацию";
			КонецЕсли;
	 		Таб.ВывестиСекцию("Статус");
			Оживить(1);
			Таб.ВывестиСекцию("Клиент");
			Оживить(1);
			ИтогоНач=0;
			ИтогоПрих=0;
			ИтогоВозвр=0;
			ИтогоПрод=0;
			ИтогоКон=0;
			Пока Запрос.Группировка("Товар") = 1 Цикл
				ТекТовар=Запрос.Товар;
			    Если ТекТовар.Выбран()=0 Тогда
					Продолжить;
				КонецЕсли;
				ВУ=Рубли;
				ВП=Рубли;
				ВИ=Рубли;
		        
				ПечТовар=СокрП(ТекТовар.Наименование);
				ПечНачальныйОстатокТовара=Запрос.НачОстатокТовара;
				ПечТекущийОстатокТовара=Запрос.КонОстатокТовара;
				ПечПриходОстатокТовара=Запрос.ПрихОстатокТовара;
				ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
				ПечРасходОстатокТовараВозвр=Запрос.РасхОстатокТовараВозвр;
				
				Если Запрос.Статус=Отданный Тогда
					НачальнаяСтоимость=Запрос.НачПродСтоимость;
					КонечнаяСтоимость=Запрос.КонПродСтоимость;
					ПриходСтоимость=Запрос.ПрихПродСтоимость;
					РасходСтоимость=Запрос.РасхПродСтоимость;
					РасходСтоимостьВозвр=Запрос.РасхПродСтоимостьВозвр;
				Иначе
					НачальнаяСтоимость=Запрос.НачСтоимость+Запрос.НачНДС;
					КонечнаяСтоимость=Запрос.КонСтоимость+Запрос.КонНДС;
					ПриходСтоимость=Запрос.ПрихСтоимость+Запрос.ПрихНДС;
					РасходСтоимость=Запрос.РасхСтоимость+Запрос.РасхНДС;
					РасходСтоимостьВозвр=Запрос.РасхСтоимостьВозвр+Запрос.РасхНДСВозвр;
				КонецЕсли;
							
				ПечНачальнаяСтоимость=ФРМ(НачальнаяСтоимость,ВП,1);
				ПечКонечнаяСтоимость=ФРМ(КонечнаяСтоимость,ВП,1);
				ПечПриходСтоимость=ФРМ(ПриходСтоимость,ВП,1);
				ПечРасходСтоимость=ФРМ(РасходСтоимость,ВП,1);
				ПечРасходСтоимостьВозвр=ФРМ(РасходСтоимостьВозвр,ВП,1);
				
				ИтогоНач=ИтогоНач+НачальнаяСтоимость;
				ИтогоПрих=ИтогоПрих+ПриходСтоимость;
				ИтогоВозвр=ИтогоВозвр+РасходСтоимостьВозвр;
				ИтогоПрод=ИтогоПрод+РасходСтоимость;
				ИтогоКон=ИтогоКон+КонечнаяСтоимость;

				Если НЕ(Режим="Краткий") Тогда
					Таб.ВывестиСекцию("ШапкаТов");
				КонецЕсли;
				Таб.ВывестиСекцию("Товар");
				Оживить(2);
		
		        // теперь еще раз пройдемся по ПрихДокумент и пропишем все основательно 
		        Если (Режим="Подробный") ИЛИ (Режим="Развернутый") Тогда
					Пока Запрос.Группировка("ПрихДокумент") = 1 Цикл 
			
						Док=Запрос.ПрихДокумент;
						ПечНачальныйОстатокДокум=Запрос.НачОстатокТовара;
						ПечТекущийОстатокДокум=Запрос.КонОстатокТовара;
						ПечПриходОстатокДокум=Запрос.ПрихОстатокТовара;
						ПечРасходОстатокДокум=Запрос.РасхОстатокТовара;
						ПечРасходОстатокДокумВозвр=Запрос.РасхОстатокТовараВозвр;
						ДатаКурса=?(Док.Выбран()=1,ДатаКурсаДок(Док),ДатаКонец);
						Если Запрос.Статус=Отданный Тогда
							СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,Запрос.ПрихПродСтоимость/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,Запрос.РасхПродСтоимость/(Запрос.РасхОстатокТовара+Запрос.РасхОстатокТовараВозвр),0));
						Иначе
							СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,(Запрос.ПрихСтоимость+Запрос.ПрихНДС)/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,(Запрос.РасхСтоимость+Запрос.РасхНДС)/(Запрос.РасхОстатокТовара+Запрос.РасхОстатокТовараВозвр),0));
						КонецЕсли;
						ПечСебестПоДок=ФРМ(Пересчет(СебестПоДок,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
						ПечНомерДок=Док.НомерДок+"; "+СокрП(Запрос.Контрагент);
						
						Таб.ВывестиСекцию("Партия");
						Оживить(1); 
						
						БалансТовара= Запрос.НачОстатокТовара;
						
						Если (Режим="Подробный")  Тогда
				 			Пока Запрос.Группировка("Докум") = 1 Цикл 
		      					Док=Запрос.Докум;
								ВидДок=Док.Вид();
								Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
									Продолжить;
								КонецЕсли;
								
								ДатаКурса=ДатаКурсаДок(Док);
								БалансТовара=БалансТовара+Запрос.ПрихОстатокТовара-Запрос.РасхОстатокТовара-Запрос.РасхОстатокТовараВозвр;
								ПечНомерДок=СокрП(Док.НомерДок)+"; "+КлиентДок(Док);
								Приращение=Запрос.ПрихОстатокТовара+Запрос.РасхОстатокТовара+Запрос.РасхОстатокТовараВозвр;
								ПечПриращение=Приращение;
								Если (Запрос.ПрихОстатокТовара<>0) Тогда
		                          	Таб.ВывестиСекцию("Приход");
								ИначеЕсли (Запрос.РасхОстатокТовара<>0)  Тогда
		                          	Таб.ВывестиСекцию("Расход");
								ИначеЕсли (Запрос.РасхОстатокТовараВозвр<>0) Тогда
		                          	Таб.ВывестиСекцию("Возврат");
								КонецЕсли;
								Оживить(1);
								
							КонецЦикла;
								
						КонецЕсли;
						
					КонецЦикла;
			    КонецЕсли;
			КонецЦикла;
			
			ПечИтогоНач=ФРМ(ИтогоНач,ВИ,1);
			ПечИтогоПрих=ФРМ(ИтогоПрих,ВИ,1);
			ПечИтогоВозвр=ФРМ(ИтогоВозвр,ВИ,1);
			ПечИтогоПрод=ФРМ(ИтогоПрод,ВИ,1);
			ПечИтогоКон=ФРМ(ИтогоКон,ВИ,1);
			
			Если Запрос.Статус=Отданный Тогда
				ЗагИтого="Итого по отданным на реализацию:";
			Иначе
				ЗагИтого="Итого по принятым на реализацию:";
			КонецЕсли;
			
          	Таб.ВывестиСекцию("Итоги");

		КонецЦикла;
	КонецЦикла;
	
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,4,0);
	Таб.Показать("Фин. учет реализации","");
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

//-------------------------------
Процедура Продажи()
	Заг="";
	Если ВыбФирма.Выбран()=1 Тогда
		Заг=Заг+"По фирме: "+СокрП(ВыбФирма.Наименование)+". ";
	Иначе
		Предупреждение("Выберите фирму!");
		Возврат;
	КонецЕсли;

    // Здесь формируется отчет, который использует регистры, критичные к 
    // последовательности проведения документов
    // поэтому сравним установленные даты периода формируемого отчета с ГП 
    ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонец,"Фин",ВыбФирма);

	// Сделаем это при помощи механизма Запросов
 	Рег1=СоздатьОбъект("Регистр.ПартииТоваров");

	Если ДатаКонец>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонец=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонец; 
	КонецЕсли;

	Запрос=СоздатьОбъект("Запрос");
	
	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала По ДатаКонец;
	|Товар=Регистр.ПартииТоваров.Товар;
	|Фирма=Регистр.ПартииТоваров.Фирма;
	|Статус=Регистр.ПартииТоваров.Статус;
	|Контрагент=Регистр.ПартииТоваров.Контрагент;
	|ПрихДокумент=Регистр.ПартииТоваров.ПрихДокумент;
	|Докум=Регистр.ПартииТоваров.ТекущийДокумент;
	|ОстатокТовара=Регистр.ПартииТоваров.ОстатокТовара;
	|Стоимость=Регистр.ПартииТоваров.Стоимость;
	|НДС=Регистр.ПартииТоваров.НДС;
	|ПродСтоимость=Регистр.ПартииТоваров.ПродСтоимость;
	|Оборот=Регистр.ПартииТоваров.Оборот;
	|КодОперации=Регистр.ПартииТоваров.КодОперации;
	|Группировка Контрагент Без групп;
	|Группировка Товар Без групп;
	|Группировка ПрихДокумент;
	|Группировка Докум;
	|Функция НачОстатокТовара=НачОст(ОстатокТовара);
	|Функция КонОстатокТовара=КонОст(ОстатокТовара);
	|Функция ПрихОстатокТовара=Приход(ОстатокТовара);
	|Функция РасхОстатокТовара=Расход(ОстатокТовара) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхОстатокТовараВозвр=Расход(ОстатокТовара) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачСтоимость=НачОст(Стоимость);
	|Функция КонСтоимость=КонОст(Стоимость);
	|Функция ПрихСтоимость=Приход(Стоимость);
	|Функция РасхСтоимость=Расход(Стоимость) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхСтоимостьВозвр=Расход(Стоимость) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачНДС=НачОст(НДС);
	|Функция КонНДС=КонОст(НДС);
	|Функция ПрихНДС=Приход(НДС);
	|Функция РасхНДС=Расход(НДС) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхНДСВозвр=Расход(НДС) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция НачПродСтоимость=НачОст(ПродСтоимость);
	|Функция КонПродСтоимость=КонОст(ПродСтоимость);
	|Функция ПрихПродСтоимость=Приход(ПродСтоимость);
	|Функция РасхПродСтоимость=Расход(ПродСтоимость) Когда ((КодОперации=ПроданоРеализатором) ИЛИ (КодОперации=ПродажаПринятогоТовара));
	|Функция РасхПродСтоимостьВозвр=Расход(ПродСтоимость) Когда ((КодОперации=ВозвратОтРеализатора) ИЛИ (КодОперации=ВозвратПоставщикуПринятогоТовара));
	|Функция ОборотТовара=Сумма(Оборот);
	|Условие (Статус=Принятый);
	|Условие (Фирма=ВыбФирма);
	|";
	
	Если ВыбТовар.Выбран()=0 Тогда
		Заг=Заг+"По всем товарам"+". ";
	ИначеЕсли ВыбТовар.ЭтоГруппа()=1 Тогда
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар.ПринадлежитГруппе(ВыбТовар)=1);";
		Заг=Заг+"По товарам группы: "+СокрП(ВыбТовар.Наименование)+". ";
	Иначе
		ТекстЗапроса=ТекстЗапроса+"Условие(Товар=ВыбТовар);";
		Заг=Заг+"По товару: " + СокрП(ВыбТовар.Наименование)+". ";
	КонецЕсли;

	Если ВыбКлиент.Выбран()=0 Тогда
		Заг=Заг+"По всем контрагентам"+". ";
	ИначеЕсли ВыбКлиент.ЭтоГруппа()=1 Тогда
		ТекстЗапроса = ТекстЗапроса+"Условие (Контрагент.ПринадлежитГруппе(ВыбКлиент)=1);";
		Заг=Заг+"По контрагентам группы "+ВыбКлиент.Наименование+". ";
	Иначе
		ТекстЗапроса = ТекстЗапроса+"Условие (Контрагент=ВыбКлиент);";
		Заг=Заг+"По контрагенту: "+СокрП(ВыбКлиент.Наименование)+". ";
	КонецЕсли;
	
	Заг=Заг+"По статусам партий: Принятый. ";

	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица1");
	
	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Партиям не выполнился!!!");
		Возврат;
	КонецЕсли;

	// теперь в запросе собраны все Документы по партиям 
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
	Таб.ВывестиСекцию("ШапкаДок");
	Оживить(4);

	Пока Запрос.Группировка("Контрагент") = 1 Цикл
		Номенклатура=0;
		Пока Запрос.Группировка("Товар") = 1 Цикл
			Номенклатура=Номенклатура+1;
		КонецЦикла;
		Досье=СокрП(Запрос.Контрагент.Наименование)+"; ";
		Досье=Досье+"Принятых от контрагента на реализацию товаров числится "+Номенклатура+" видов. ";
		Оживить(1);
		Таб.ВывестиСекцию("Клиент");
		Оживить(1);
		ИтогоНач=0;
		ИтогоПрих=0;
		ИтогоВозвр=0;
		ИтогоПрод=0;
		ИтогоКон=0;
		Пока Запрос.Группировка("Товар") = 1 Цикл
			ТекТовар=Запрос.Товар;
			Если ТекТовар.Выбран()=0 Тогда
				Продолжить;
			КонецЕсли;
			ВУ=Рубли;
			ВП=Рубли;
			ВИ=Рубли;
			
			ПечТовар=СокрП(ТекТовар.Наименование);
			ПечНачальныйОстатокТовара=Запрос.НачОстатокТовара;
			ПечТекущийОстатокТовара=Запрос.КонОстатокТовара;
			ПечПриходОстатокТовара=Запрос.ПрихОстатокТовара;
			ПечРасходОстатокТовара=Запрос.РасхОстатокТовара;
			ПечРасходОстатокТовараВозвр=Запрос.РасхОстатокТовараВозвр;
			
			НачальнаяСтоимость=Запрос.НачСтоимость+Запрос.НачНДС;
			КонечнаяСтоимость=Запрос.КонСтоимость+Запрос.КонНДС;
			ПриходСтоимость=Запрос.ПрихСтоимость+Запрос.ПрихНДС;
			РасходСтоимость=Запрос.РасхСтоимость+Запрос.РасхНДС;
			РасходСтоимостьВозвр=Запрос.РасхСтоимостьВозвр+Запрос.РасхНДСВозвр;
			
			ПечНачальнаяСтоимость=ФРМ(НачальнаяСтоимость,ВП,1);
			ПечКонечнаяСтоимость=ФРМ(КонечнаяСтоимость,ВП,1);
			ПечПриходСтоимость=ФРМ(ПриходСтоимость,ВП,1);
			ПечРасходСтоимость=ФРМ(РасходСтоимость,ВП,1);
			ПечРасходСтоимостьВозвр=ФРМ(РасходСтоимостьВозвр,ВП,1);
			
			ИтогоНач=ИтогоНач+НачальнаяСтоимость;
			ИтогоПрих=ИтогоПрих+ПриходСтоимость;
			ИтогоВозвр=ИтогоВозвр+РасходСтоимостьВозвр;
			ИтогоПрод=ИтогоПрод+РасходСтоимость;
			ИтогоКон=ИтогоКон+КонечнаяСтоимость;
			
			Таб.ВывестиСекцию("ШапкаТов");
			Таб.ВывестиСекцию("Товар");
			Оживить(2);
			ДокПодч=СоздатьОбъект("Документ");
			
			// теперь еще раз пройдемся по ПрихДокумент и пропишем все основательно 
			Пока Запрос.Группировка("ПрихДокумент") = 1 Цикл 
				
				Док=Запрос.ПрихДокумент;
				Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
					Продолжить;
				КонецЕсли;
				ПечНачальныйОстатокДокум=Запрос.НачОстатокТовара;
				ПечТекущийОстатокДокум=Запрос.КонОстатокТовара;
				ПечПриходОстатокДокум=Запрос.ПрихОстатокТовара;
				ПечРасходОстатокДокум=Запрос.РасхОстатокТовара;
				ПечРасходОстатокДокумВозвр=Запрос.РасхОстатокТовараВозвр;
				ДатаКурса=?(Док.Выбран()=1,ДатаКурсаДок(Док),ДатаКонец);
				СебестПоДок=?(Запрос.ПрихОстатокТовара<>0,(Запрос.ПрихСтоимость+Запрос.ПрихНДС)/Запрос.ПрихОстатокТовара,?(Запрос.РасхОстатокТовара<>0,(Запрос.РасхСтоимость+Запрос.РасхНДС)/(Запрос.РасхОстатокТовара+Запрос.РасхОстатокТовараВозвр),0));
				ПечСебестПоДок=ФРМ(Пересчет(СебестПоДок,ВУ,ДатаКурса,ВП,ДатаКурса),ВП,1);
				ПечНомерДок=Док.НомерДок;
				
				Таб.ВывестиСекцию("Партия");
				Оживить(1); 
				
				БалансТовара= Запрос.НачОстатокТовара;
				
				Пока Запрос.Группировка("Докум") = 1 Цикл 
					Док=Запрос.Докум;
					ВидДок=Док.Вид();
					Если Док.Выбран()=0 Тогда // пропускаем непонятные документы
						Продолжить;
					КонецЕсли;
					БалансТовара=БалансТовара+Запрос.ПрихОстатокТовара-Запрос.РасхОстатокТовара-Запрос.РасхОстатокТовараВозвр;
					Если Док=Запрос.ПрихДокумент Тогда // пропускаем документ партии
						Продолжить;
					КонецЕсли;
					
					ДатаКурса=ДатаКурсаДок(Док);
					Если ВидДок="РасходнаяНакладная" Тогда
						ДокСФ="Нет Регистрации Счета_фактуры!";
						ДокПодч.ВыбратьПодчиненныеДокументы(Док.ДатаДок-7,ТекущаяДата(),Док); 
						Пока   ДокПодч.ПолучитьДокумент()=1 Цикл
							Если ДокПодч.Вид()="РегистрацияСчета_фактуры" Тогда
								Если ДокПодч.Проведен()=0 Тогда
									Продолжить;
								КонецЕсли;
								Если ДокПодч.Клиент<>Запрос.ПрихДокумент.Клиент Тогда
								// находим РегСчФ именно для данного поставщика
									Продолжить;
								КонецЕсли;
								ДокСФ=ДокПодч.ТекущийДокумент();
								Прервать;
							КонецЕсли;
						КонецЦикла;
						Если ДокСФ="Нет Регистрации Счета_фактуры!" Тогда
							ПечНомерДок=ДокСФ;
						Иначе
							ПечНомерДок=СокрП(ДокСФ.НомерСчетаФактуры)+" от "+ДокСФ.ДатаДок+"; ("+СокрП(ДокСФ.НомерДок)+")";
						КонецЕсли;
					Иначе
						ПечНомерДок=СокрП(Док.НомерДок);
					КонецЕсли;
					Приращение=Запрос.ПрихОстатокТовара+Запрос.РасхОстатокТовара+Запрос.РасхОстатокТовараВозвр;
					ПечПриращение=Приращение;
					Если (Запрос.ПрихОстатокТовара<>0) Тогда
						Таб.ВывестиСекцию("Приход");
					ИначеЕсли (Запрос.РасхОстатокТовара<>0)  Тогда
						Таб.ВывестиСекцию("Расход");
					ИначеЕсли (Запрос.РасхОстатокТовараВозвр<>0) Тогда
						Таб.ВывестиСекцию("Возврат");
					КонецЕсли;
					Оживить(1);
					
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
		
		ПечИтогоНач=ФРМ(ИтогоНач,ВИ,1);
		ПечИтогоПрих=ФРМ(ИтогоПрих,ВИ,1);
		ПечИтогоВозвр=ФРМ(ИтогоВозвр,ВИ,1);
		ПечИтогоПрод=ФРМ(ИтогоПрод,ВИ,1);
		ПечИтогоКон=ФРМ(ИтогоКон,ВИ,1);
		
		ЗагИтого="Итого по принятым на реализацию:";
		
		Таб.ВывестиСекцию("Итоги");
		
	КонецЦикла;
	
	//Вызов выходного отчета в окно просмотра и редактирования.
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,4,0);
	Таб.Показать("Фин. отчет о продажах товаров","");

КонецПроцедуры
//-------------------------------
ДатаКонец=ПолучитьДатуТА();
ДатаНачала=Константа.ОсновнаяДатаНачалаОтчета;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.98';
Конецесли;  
ДокПодч=СоздатьОбъект("Документ"); 
ВыбФирма=Константа.ОсновнаяФирма;
ВыбОтданный=1;
ВыбПринятый=1;

