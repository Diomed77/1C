Функция УправлениеДиалогом()
	Если ( флСвободные = 0 ) и ( флЗанятые = 0 ) и ( флЗаблокированные = 0 ) Тогда
		Если Форма.АктивныйЭлемент() = "флСвободные" Тогда
			флЗанятые = 1;
		ИначеЕсли Форма.АктивныйЭлемент() = "флЗанятые" Тогда
			флЗаблокированные = 1;
		ИначеЕсли Форма.АктивныйЭлемент() = "флЗаблокированные" Тогда
			флСвободные = 1;
		Иначе
			флСвободные = 1;
		КонецЕсли;
	КонецЕсли;
КонецФункции
	
Функция ПолучитьНазваниеУровня( пРодитель )
	Если ПустоеЗначение( пРодитель ) = 1 Тогда Возврат "Склад";
	ИначеЕсли пРодитель.Уровень() = 1 Тогда Возврат "Ряд";
	ИначеЕсли пРодитель.Уровень() = 2 Тогда Возврат "Секция";
	ИначеЕсли пРодитель.Уровень() = 3 Тогда Возврат "Ячейка";
	Иначе Возврат "";
	КонецЕсли;
КонецФункции

Функция ПолучитьПолныйКод( пРодитель )
	Если ПустоеЗначение( пРодитель ) = 1 Тогда 
		Возврат "";
	ИначеЕсли пРодитель.Уровень() = 1 Тогда 
		Возврат Шаблон( "[пРодитель.Код]-" );
	ИначеЕсли пРодитель.Уровень() = 2 Тогда 
		Возврат Шаблон( "[пРодитель.Родитель.Код]-[пРодитель.Код]-" );
	ИначеЕсли пРодитель.Уровень() = 3 Тогда 
		Возврат Шаблон( "[пРодитель.Родитель.Родитель.Код]-[пРодитель.Родитель.Код]-[пРодитель.Код]-" );
	Иначе 
		Возврат "";
	КонецЕсли;
КонецФункции

Процедура Сформировать()
	начЛог = семЗаписатьЛогНач( "Отчет", "Список складских адресов", "Обработка", "Формирование отчета" );
	спр = СоздатьОбъект( "Справочник.СкладскиеАдреса" );
	Если выбАдрес.Выбран() = 1 Тогда
		спр.ИспользоватьРодителя( выбАдрес );
	КонецЕсли;
	таб = СоздатьОбъект( "Таблица" );
	таб.ИсходнаяТаблица( "Таблица" );
	пДата = Шаблон("Дата формирования: [ТекущаяДата()]");
	пФильтр = "Фильтр: "+?(выбАдрес.Выбран() = 1,выбАдрес,"по всем");
	пФильтр = пФильтр + ?( флСвободные = 1, " Свободные.", "" );
	пФильтр = пФильтр + ?( флЗанятые = 1, " Занятые.", "" );
	пФильтр = пФильтр + ?( флЗаблокированные = 1, " Заблокированные.", "" );
	таб.ВывестиСекцию( "Заголовок" );		
	таб.Опции( 0, 0, таб.ВысотаТаблицы(), 0 );
	
	н = 0;
	спр.ВыбратьЭлементы();
	Пока спр.ПолучитьЭлемент() = 1 Цикл н = н + 1; Состояние( Шаблон( "Обработано [н] элемент(ов)." ) );
		Если спр.ПометкаУдаления() = 1 Тогда
			Если флЗаблокированные = 0 Тогда Продолжить; КонецЕсли;
		Иначе
			Если спр.Товар.Выбран() = 1 Тогда 
				Если флЗанятые = 0 Тогда Продолжить; КонецЕсли;
			Иначе
				Если флСвободные = 0 Тогда Продолжить; КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		пКод = Шаблон( "[ПолучитьПолныйКод( спр.Родитель )][спр.Код]" );
		Если спр.Товар.Выбран() = 1 Тогда
			пТовар = Шаблон( "[спр.Товар.Код] [спр.Товар.Наименование] [спр.Товар.КодПоставщика]" );
			пЕд = спр.Товар.БазоваяЕдиницаИзмерения;
		Иначе
			пТовар = "";
			пЕд = "";
		КонецЕсли;
		Ячейка = спр.ТекущийЭлемент();
		таб.ВывестиСекцию( ПолучитьНазваниеУровня( спр.Родитель )+?(спр.ПометкаУдаления()=1,"Уд","") );		
	КонецЦикла;
	
	таб.ТолькоПросмотр( 1 );
	таб.Показать( "" ); 
	семЗаписатьЛогКон( начЛог );	
КонецПроцедуры

Процедура ПроверкаПрав()
	спПрав = СоздатьОбъект( "СписокЗначений" );
	спПрав.ДобавитьЗначение("Администратор");
	спПрав.ДобавитьЗначение("РуководительЛогистики");
	Если спПрав.НайтиЗначение( НазваниеНабораПрав() ) = 0 Тогда
		Предупреждение( "Нет прав." );
		СтатусВозврата( 0 );
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	//ПроверкаПрав();
	УправлениеДиалогом();
КонецПроцедуры
