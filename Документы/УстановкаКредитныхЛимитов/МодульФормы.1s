Процедура ПриОткрытии()
	ПроверкаРазрешенияРедактирования(Контекст);
КонецПроцедуры

Процедура ВводНового()
	ЗаполнитьШапку(Контекст);
	ПозНомера=Найти(НомерДок,"-");
	НомерДокНомер=СокрЛП(Сред(НомерДок,ПозНомера+1));
	Если ПозНомера>0 Тогда
		НомерДок=Лев(НомерДок,ПозНомера)+Прав(НомерДокНомер,5);
	КонецЕсли;
КонецПроцедуры

Процедура Заполнить()
	Если КоличествоСтрок() <> 0 Тогда
		Если Вопрос("Существующие строки будут удалены! Продолжить?","Да+Нет")="Да" Тогда
			УдалитьСтроки();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если (НачалоПериода=Дата(0)) или (КонецПериода=Дата(0)) Тогда
		Предупреждение("Не указан период проверки!");
		Возврат;
	КонецЕсли;
	Если СреднемесячныйОборот=0 Тогда
		Предупреждение("Не указан среднемесячный оборот для отбора!");
		Возврат;
	КонецЕсли;
	
	RS = СоздатьОбъект("ODBCRecordset");
	
	RS.УстБД1С();         
	
	Запрос=СоздатьОбъект("Запрос");  
	ТекстЗапроса="SELECT QRY.Клиент [Клиент $Справочник.Контрагенты]
	|, $ПоследнееЗначение.Контрагенты.Глубина(QRY.Клиент, :КонДата)  Глубина
	|, QRY.СуммаДолгРасход 
	|, QRY.СуммаДолгРасход/:КолВоМес Среднее
	| FROM
	|(
	|SELECT ВзаиморасчетыПокупателейОбороты.Клиент Клиент 
	|	, ВзаиморасчетыПокупателейОбороты.ВидДокумента ВидДокумента
	|	, Sum(ВзаиморасчетыПокупателейОбороты.ДолгРасход) СуммаДолгРасход
	|FROM $РегистрОбороты.ВзаиморасчетыПокупателей(:НачДата,
	|		:КонДата~,
	|		Документ,,,
	|		Клиент,) AS ВзаиморасчетыПокупателейОбороты
	|WHERE (ВзаиморасчетыПокупателейОбороты.ВидДокумента=$ВидДокумента.ДвиженияДенежныхСредств)
	|GROUP BY ВзаиморасчетыПокупателейОбороты.Клиент
	|	, ВзаиморасчетыПокупателейОбороты.ВидДокумента
	|) as QRY
	|INNER JOIN $Справочник.Контрагенты Клиенты (NOLOCK) ON Клиенты.ID = QRY.Клиент
	|WHERE (QRY.СуммаДолгРасход/:КолВоМес >= :Порог)
	|ORDER BY Клиенты.DESCR ";
	
	КолВоМес=(ДатаГод(КонецПериода)-ДатаГод(НачалоПериода))*12+ДатаМесяц(КонецПериода)-ДатаМесяц(НачалоПериода)+1;
	RS.УстановитьТекстовыйПараметр("КолВоМес", КолВоМес);  
	RS.УстановитьТекстовыйПараметр("Порог", СреднемесячныйОборот);  
	RS.УстановитьТекстовыйПараметр("НачДата", НачалоПериода);  
	RS.УстановитьТекстовыйПараметр("КонДата", КонецПериода);  
	ТП=СоздатьОбъект("ТаблицаЗначений");
	RS.ВыполнитьИнструкцию(ТекстЗапроса,ТП,1);  
	
	ТП.ВыбратьСтроки();
	Пока ТП.ПолучитьСтроку() = 1 Цикл
		НоваяСтрока();
		Клиент=ТП.Клиент;
		Лимит=ТП.Глубина*ТП.Среднее/30;
		Оборот=ТП.Среднее;
	КонецЦикла;
КонецПроцедуры
