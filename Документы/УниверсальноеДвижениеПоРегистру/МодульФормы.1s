//___________________________________________________________________________________________
Процедура ПриВыбореРегистра()
	// Спрячем все реквизиты, измерения, ресурсы, чтобы нарисовать их заново
	Для Инд = 1 По 6 Цикл
		Элемент = Форма.ПолучитьАтрибут("Реквизит"+Инд);
		Элемент.Видимость(0);
		Элемент.Заголовок("");
	КонецЦикла;	
	Для Инд = 1 По 10 Цикл
		Элемент = Форма.ПолучитьАтрибут("Измерение"+Инд);
		Элемент.Видимость(0);
		Элемент.Заголовок("");
	КонецЦикла;	
	Для Инд = 1 По 5 Цикл
		Элемент = Форма.ПолучитьАтрибут("Ресурс"+Инд);
		Элемент.Видимость(0);
		Элемент.Заголовок("");
	КонецЦикла;	
	
	ВыбранныйРегистр = СписокРегистров.ПолучитьЗначение(НомерРегистра);
	Рег = Регистр.ПолучитьАтрибут(ВыбранныйРегистр);
	Мет = Метаданные.Регистр(ВыбранныйРегистр);			
	
	// ------ ИЗМЕРЕНИЯ РЕГИСТРА --------
	Для Инд=1 По Мет.Измерение() Цикл
		НазначитьТип("Измерение"+Инд,Мет.Измерение(Инд));
		Элемент = Форма.ПолучитьАтрибут("Измерение"+Инд);
		
		Если ПустоеЗначение(Мет.Измерение(Инд).Представление()) = 0 Тогда
			Элемент.Заголовок(Мет.Измерение(Инд).Представление());
		Иначе
			Элемент.Заголовок(Мет.Измерение(Инд).Идентификатор);
		КонецЕсли;
		Элемент.Видимость(1);
	КонецЦикла;
	
	// ------ РЕСУРСЫ РЕГИСТРА --------
	Для Инд=1 По Мет.Ресурс() Цикл
		Реквизит = Мет.Ресурс(Инд).Идентификатор;
		
		НазначитьТип("Ресурс"+Инд,Мет.Ресурс(Инд),Мет.Ресурс(Инд).Длина,Мет.Ресурс(Инд).Точность );
        Элемент = Форма.ПолучитьАтрибут("Ресурс"+Инд);
		Элемент.Заголовок(Мет.Ресурс(Инд).Идентификатор);
		Элемент.Видимость(1);
	КонецЦикла;
	
	//// ------ РЕКВИЗИТЫ РЕГИСТРА --------
	Для Инд=1 По Мет.Реквизит() Цикл
		НазначитьТип("Реквизит"+Инд,Мет.Реквизит(Инд));
		Элемент = Форма.ПолучитьАтрибут("Реквизит"+Инд);
		
		Если ПустоеЗначение(Мет.Реквизит(Инд).Представление()) = 0 Тогда
			Элемент.Заголовок(Мет.Реквизит(Инд).Представление());
		Иначе
			Элемент.Заголовок(Мет.Реквизит(Инд).Идентификатор);
		КонецЕсли;
		Элемент.Видимость(1);
	КонецЦикла;
КонецПроцедуры //ПриВыбореРегистра 
//___________________________________________________________________________________________
Процедура ПриВыбореРегистраИнтер()
    ВидРег = СписокРегистров.ПолучитьЗначение(СписокРегистров.ТекущаяСтрока()); 
	// Зададим текущее значение списка соответствующему регистру  
	Если НомерРегистра <> СписокРегистров.ТекущаяСтрока() Тогда
		Если Вопрос ("Перед изменением регистра необходимо очистить табличную часть."+РазделительСтрок+"Очистить табличную часть?","Да+Нет",60)="Да" Тогда
			УдалитьСтроки();    
		Иначе
		КонецЕсли;	
		НомерРегистра = Число(СписокРегистров.ТекущаяСтрока());
		ПриВыбореРегистра();    
	КонецЕсли;
КонецПроцедуры //ПриВыбореРегистраИнтер
//___________________________________________________________________________________________
Процедура ПриРедактированииНовойСтроки()
	ПриВыбореРегистра(); 
	АктивизироватьОбъект("Движение");
	Спис = СоздатьОбъект("СписокЗначений");
	Спис.ДобавитьЗначение("+","Приход");
	Спис.ДобавитьЗначение("-","Расход"); 
	Зн = "+";
	Спис.ВыбратьЗначение(Зн,,1,,2);
	Движение = Зн;
КонецПроцедуры //ПриРедактированииНовойСтроки
//___________________________________________________________________________________________
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтрандОбр)
	Если ИдентЭлемДиалога = "Движение" Тогда
	    Спис = СоздатьОбъект("СписокЗначений");
		Спис.ДобавитьЗначение("+","Приход");
		Спис.ДобавитьЗначение("-","Расход"); 
		Зн = "+";
		Спис.ВыбратьЗначение(Зн,,1,,2);
		Движение = Зн;
		ФлагСтандОбр = 0;
	КонецЕсли;	
КонецПроцедуры //ПриНачалеВыбораЗначения
//___________________________________________________________________________________________
Процедура ЗаполнитьСписокРегистров()
	Для Инд = 1 ПО Метаданные.Регистр() Цикл
	    Название = Метаданные.Регистр(Инд).Идентификатор;
		Представление = Метаданные.Регистр(Инд).Представление(); 
		СписокРегистров.ДобавитьЗначение(Название,Представление);
	КонецЦикла;	
КонецПроцедуры //ЗаполнитьСписокРегистров

//************************************** "Служебные" процедуры ******************************
// Очистим табличную часть
Процедура Очистить()
	Если Вопрос ("Очистить табличную часть?","Да+Нет",60)="Да" Тогда
	    УдалитьСтроки();
	КонецЕсли;	
КонецПроцедуры //Очистить

// Свернутьт табличную часть
Процедура Свернуть()
	Перем С,н;
	Если Вопрос ("Свернуть табличную часть?","Да+Нет",60)="Да" Тогда
		Спис = СоздатьОбъект("СписокЗначений");
		ВыбранныйРегистр = СписокРегистров.ПолучитьЗначение(НомерРегистра);
		Мет = Метаданные.Регистр(ВыбранныйРегистр);			
		// ------ ИЗМЕРЕНИЯ РЕГИСТРА --------
		Для Инд=1 По Мет.Измерение() Цикл
			Спис.ДобавитьЗначение("Измерение"+СокрЛП(Инд),Мет.Измерение(Инд).Идентификатор);
		КонецЦикла;
		
		//// ------ РЕКВИЗИТЫ РЕГИСТРА --------
		Для Инд=1 По Мет.Реквизит() Цикл
			Спис.ДобавитьЗначение("Реквизит"+СокрЛП(Инд),Мет.Реквизит(Инд).Идентификатор);
		КонецЦикла;

		Если Спис.ОтметитьЗначения(,"Какие реквизиты учитывать?",,)=1 тогда
			Стр = "";
			Для н =1 по Спис.РазмерСписка() Цикл
				Если Спис.Пометка(н,)=1 тогда
					Стр = Стр + Спис.ПолучитьЗначение(н,С)+",";
				КонецЕсли;
			КонецЦикла;                                        
			Стр = Лев(Стр,СтрДлина(Стр)-1);
			
			// Получим строку с ресурсами
			СтрР = "";
			//// ------ РЕКВИЗИТЫ РЕГИСТРА --------
			Для Инд=1 По Мет.Ресурс() Цикл
				СтрР = СтрР + "Ресурс"+СокрЛП(Инд)+",";
			КонецЦикла;
			СтрР = Лев(СтрР,СтрДлина(СтрР)-1);

			// Приступим к свертыванию
			ТЗ = СоздатьОбъект("ТаблицаЗначений");
			ВыгрузитьТабличнуюЧасть(ТЗ);
			//Свертываем
			ТЗ.Свернуть(Стр,СтрР); 
			УдалитьСтроки();
			ЗагрузитьТабличнуюЧасть(ТЗ);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры //Свернуть
                             
Процедура Установить()
	
КонецПроцедуры // Установить

// По кнопке "Действие"
Процедура Действие()
	Зн=0;
	Спис = СоздатьОбъект("СписокЗначений");
	Спис.ДобавитьЗначение(1,"Очистить");
	Спис.ДобавитьЗначение(2,"Свернуть");
	Спис.ДобавитьЗначение(3,"Внешняя обработка");
	
	Если Спис.ВыбратьЗначение(Зн,,1,,1)=1 тогда
		Если Зн = 1 тогда
			Очистить();
		ИначеЕсли Зн = 2 тогда
			Свернуть();
		ИначеЕсли Зн = 3 тогда 
			Конт = ВзятьКонтекст(Контекст);
			ОткрытьФорму("Отчет.ВнешняяОбработка",Конт,КаталогИБ()+"/ExtForms/ОбработкаУниДвижения.ert");
		КонецЕсли;                            
	КонецЕсли;
КонецПроцедуры

//************************************** Предопределенные процедуры *************************
Процедура ВводНового()
	НомерРегистра = 1;
КонецПроцедуры
//___________________________________________________________________________________________
Процедура ПриОткрытии()
    ЗаполнитьСписокРегистров();
	
	СписокРегистров.ТекущаяСтрока(НомерРегистра);
	
	ПриВыбореРегистра();
	
	ПриЗаписиПерепроводить(1);
КонецПроцедуры //ПриОткрытии
//___________________________________________________________________________________________
Процедура ПриЗакрытии()

КонецПроцедуры //ПриЗакрытии	
//*******************************************************************************************

