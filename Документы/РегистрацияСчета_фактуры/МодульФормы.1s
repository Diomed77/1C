//------------------------
Процедура ПриОткрытии()
	ПроверкаРазрешенияРедактирования(Контекст);
	УстановкаРеквизитаТип(Контекст);
	Тип.ДобавитьЗначение("Упр.");
	Тип.ДобавитьЗначение("Общ.");
	Тип.ДобавитьЗначение("Фин.");
	Тип.ТекущаяСтрока(ТипУчета+1);
	Если Форма.ТолькоПросмотр()=1 Тогда
	    Форма.КнФирма.Доступность(0);
	    Форма.ОК.Доступность(0);
	    Форма.КнопкаПоУмолчанию("Закрыть");
	Иначе
	    Форма.КнопкаПоУмолчанию("ОК");
	КонецЕсли;
КонецПроцедуры
//-----------------------------------------------
Процедура ВводНового();
	Если Фирма.Выбран() = 1 Тогда	//копирование документа
		Автор = Пользователь;  
		Возврат;	    
	КонецЕсли;
	ЗаполнитьШапку(Контекст);
	Клиент=Константа.ОсновнойПоставщик;
	Валюта=Константа.БазоваяВалюта; 
КонецПроцедуры
//-----------------------------------------------
Процедура ВводНаОсновании(ДокОснование)
	Если ДокОснование.Проведен()=0  Тогда
		Предупреждение("Регистрацию Счета-фактуры нельзя вводить на основании не проведенного документа!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
    ВидОсн=ДокОснование.Вид();
	Если 	(ВидОсн="ПриходнаяНакладная")
		ИЛИ (ВидОсн="ВводОстатковКредита")
		ИЛИ (ВидОсн="РасходнаяНакладная") Тогда
	Иначе
		Предупреждение("Регистрацию Счета-фактуры нельзя вводить на основании выбранного вида документа!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	ДокПодч=СоздатьОбъект("Документ");
	ДокПодч.ВыбратьПодчиненныеДокументы(ДокОснование.ДатаДок-7,ТекущаяДата(),ДокОснование.ТекущийДокумент()); 
	Пока  ДокПодч.ПолучитьДокумент()=1 Цикл                                                        
		Если ДокПодч.Вид()="РегистрацияСчета_фактуры" Тогда
			Если ДокПодч.ПометкаУдаления()=1 Тогда
			    Продолжить
			КонецЕсли;
			Если ДокПодч.ТекущийДокумент()<>ТекущийДокумент() Тогда
				Если 	(ДокОснование.Вид()="ПриходнаяНакладная")  
					ИЛИ (ДокОснование.Вид()="ВводОстатковКредита")  
						Тогда
					Предупреждение("У докум.основания уже есть одна "+ДокПодч+", нельзя выписывать вторую!");
					СтатусВозврата(0);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьШапкуНаОсн(Контекст,ДокОснование);
	ДатаДок=ДокОснование.ДатаДок;
 	Валюта=Константа.БазоваяВалюта; 
	Сумма=0;
	СуммаБезНДС20=0;
	СуммаБезНДС10=0;
	НДС20=0;
	НДС10=0;
	СуммаСовсемБезНДС=0;

	Клиент=ДокОснование.Клиент;
	Основание=ДокОснование.Вид()+" № "+ ДокОснование.НомерДок+" от "+ДокОснование.ДатаДок;
	НомерСчетаФактуры="??????";
	ДокументОснование=ДокОснование;
	ДатаСчетаФактуры=ДокОснование.ДатаДок; 
	ДатаПриходаСчетаФактуры=ДокОснование.ДатаДок; 
	ДатаОплатыСчетаФактуры=ДокОснование.ДатаДок; 
	ДатаПриходаТовара=ДокОснование.ДатаДок; 
	ПромВал=ДокОснование.Валюта;
	Дата_Курса=ДокОснование.Дата_Курса;
	Курс=КурсДляВалюты(Валюта,Дата_Курса);
	Курс_Прежн=Курс;

	Если (ВидОсн="ПриходнаяНакладная") Тогда
		ДокОснование.ВыбратьСтроки();
		Пока ДокОснование.ПолучитьСтроку()=1 Цикл
			Сумма=Сумма+Пересчет(ДокОснование.Сумма,ПромВал,ДокОснование.Курс,Рубли,1);
			Если ДокОснование.Товар.СтавкаНДС=Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС Тогда 
				 НДС20=НДС20+Пересчет(ДокОснование.НДС,ПромВал,ДокОснование.Курс,Рубли,1);
				 СуммаБезНДС20=СуммаБезНДС20+Пересчет(ДокОснование.Сумма-ДокОснование.НДС,ПромВал,ДокОснование.Курс,Рубли,1);
			ИначеЕсли  ДокОснование.Товар.СтавкаНДС=Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС Тогда
				 НДС10=НДС10+Пересчет(ДокОснование.НДС,ПромВал,ДокОснование.Курс,Рубли,1);
				 СуммаБезНДС10=СуммаБезНДС10+Пересчет(ДокОснование.Сумма-ДокОснование.НДС,ПромВал,ДокОснование.Курс,Рубли,1);
			ИначеЕсли  ДокОснование.Товар.СтавкаНДС=Перечисление.ЗначенияНДС.НДС0 Тогда
				 СуммаСовсемБезНДС=СуммаСовсемБезНДС+Пересчет(ДокОснование.Сумма-ДокОснование.НДС,ПромВал,ДокОснование.Курс,Рубли,1);
			Иначе
				// Здесь ничего не делаем 
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли (ВидОсн="ВводОстатковКредита") Тогда
		Если ДокОснование.Сумма>=0 Тогда
			Предупреждение("Данным документом введен долг контрагента, поэтому Регистрации Счета-фактуры не требуется!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		Сумма=Пересчет(-ДокОснование.Сумма,ПромВал,ДокОснование.Курс,Рубли,1);
		НДС20=0;
		СуммаБезНДС20=0;
		НДС10=0;
		СуммаБезНДС10=0;
		СуммаСовсемБезНДС=0;
		Если ДокОснование.СтавкаНДС=Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС Тогда 
			НДС20=Сумма/6;
			СуммаБезНДС20=Сумма-НДС20;
		ИначеЕсли  ДокОснование.СтавкаНДС=Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС Тогда
			 НДС10=Сумма/11;
			 СуммаБезНДС10=Сумма-НДС10;
		Иначе
			 СуммаСовсемБезНДС=Сумма;
		КонецЕсли;
	ИначеЕсли (ВидОсн="РасходнаяНакладная")  Тогда
		СписокКлиентов=СоздатьОбъект("СписокЗначений");
		СписокСумма=СоздатьОбъект("СписокЗначений");
		СписокСуммаБезНДС10=СоздатьОбъект("СписокЗначений");
		СписокСуммаБезНДС20=СоздатьОбъект("СписокЗначений");
		РегПТ=СоздатьОбъект("Регистр.ПартииТоваров");
		РегПТ.ВыбратьДвиженияДокумента(ДокОснование);
		Пока РегПТ.ПолучитьДвижение()=1 Цикл
			Если РегПТ.Фирма.Выбран()=0 Тогда
				// выбираем только Фин. движения
			    Продолжить;
			КонецЕсли;
			Если РегПТ.КодОперации<>ПродажаПринятогоТовара Тогда
				// выбираем только ПродажаПринятогоТовара
			    Продолжить;
			КонецЕсли;
			ПромСумма=РегПТ.Стоимость+РегПТ.НДС;
			ПромСуммаБезНДС20=0;
			ПромНДС20=0;
			ПромНДС10=0;
			ПромСуммаБезНДС10=0;
			ПромСуммаСовсемБезНДС=0;
			Если РегПТ.Товар.СтавкаНДС=Перечисление.ЗначенияНДС.ОсновнаяСтавкаНДС Тогда
				ПромСуммаБезНДС20=РегПТ.Стоимость;
				ПромНДС20=РегПТ.НДС;
			ИначеЕсли РегПТ.Товар.СтавкаНДС=Перечисление.ЗначенияНДС.ЛьготнаяСтавкаНДС Тогда
				ПромСуммаБезНДС10=РегПТ.Стоимость;
				ПромНДС10=РегПТ.НДС;
			Иначе
				ПромСуммаСовсемБезНДС=РегПТ.Стоимость;
			КонецЕсли;
			Поз=СписокКлиентов.НайтиЗначение(РегПТ.Контрагент);
			ПромСтрока="";
			Если Поз=0 Тогда
				СписокКлиентов.ДобавитьЗначение(РегПТ.Контрагент,РегПТ.Контрагент.Наименование);
				СписокСумма.ДобавитьЗначение(ПромСумма,Строка(ПромСуммаСовсемБезНДС));
				СписокСуммаБезНДС20.ДобавитьЗначение(ПромСуммаБезНДС20,Строка(ПромНДС20));
				СписокСуммаБезНДС10.ДобавитьЗначение(ПромСуммаБезНДС10,Строка(ПромНДС10));
			Иначе
				ПромСумма=ПромСумма+СписокСумма.ПолучитьЗначение(Поз,ПромСтрока);
				ПромСуммаСовсемБезНДС=ПромСуммаСовсемБезНДС+Число(ПромСтрока);
				ПромСуммаБезНДС20=ПромСуммаБезНДС20+СписокСуммаБезНДС20.ПолучитьЗначение(Поз,ПромСтрока);
				ПромНДС20=ПромНДС20+Число(ПромСтрока);
				ПромСуммаБезНДС10=СписокСуммаБезНДС10.ПолучитьЗначение(Поз,ПромСтрока);
				ПромНДС10=ПромНДС10+Число(ПромСтрока);
				СписокСумма.УстановитьЗначение(Поз,ПромСумма,Строка(ПромСуммаСовсемБезНДС),1);
				СписокСуммаБезНДС20.УстановитьЗначение(Поз,ПромСуммаБезНДС20,Строка(ПромНДС20),1);
				СписокСуммаБезНДС10.УстановитьЗначение(Поз,ПромСуммаБезНДС10,Строка(ПромНДС10),1);
			КонецЕсли;
		КонецЦикла;
		РазмСписка=СписокКлиентов.РазмерСписка();
		Если РазмСписка=0 Тогда
			Предупреждение("Данным документом не продавались комиссионные товары, поэтому Регистрации Счета-фактуры не требуется!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		Если РазмСписка=1 Тогда
			ВыбрПоставщик=СписокКлиентов.ПолучитьЗначение(1);
			Поз=1;
		Иначе
			ВыбрПоставщик="";
			Поз=0;
			Если 1<>СписокКлиентов.ВыбратьЗначение(ВыбрПоставщик,"Выберите поставщика!",Поз) Тогда
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ДокПодч=СоздатьОбъект("Документ");
		ДокПодч.ВыбратьПодчиненныеДокументы(ДокОснование.ДатаДок-7,ТекущаяДата(),ДокОснование.ТекущийДокумент()); 
		Пока  ДокПодч.ПолучитьДокумент()=1 Цикл                                                        
			Если ДокПодч.Вид()="РегистрацияСчета_фактуры" Тогда
				Если ДокПодч.ПометкаУдаления()=1 Тогда
				    Продолжить
				КонецЕсли;
				Если ДокПодч.ТекущийДокумент()<>ТекущийДокумент() Тогда
					Если (ДокОснование.Вид()="РасходнаяНакладная") Тогда
						Если ДокПодч.Клиент=ВыбрПоставщик Тогда
							Предупреждение("У докум.основания уже есть одна "+ДокПодч+" по контрагенту "+ВыбрПоставщик+", нельзя выписывать вторую!");
							СтатусВозврата(0);
							Возврат;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Клиент=СписокКлиентов.ПолучитьЗначение(Поз);
		Сумма=СписокСумма.ПолучитьЗначение(Поз,ПромСтрока);
		СуммаСовсемБезНДС=Число(ПромСтрока);
		СуммаБезНДС20=СписокСуммаБезНДС20.ПолучитьЗначение(Поз,ПромСтрока);
		НДС20=Число(ПромСтрока);
		СуммаБезНДС10=СписокСуммаБезНДС10.ПолучитьЗначение(Поз,ПромСтрока);
		НДС10=Число(ПромСтрока);
	Иначе
		Предупреждение("Регистрацию Счета-фактуры нельзя вводить на основании выбранного вида документа!");
		СтатусВозврата(0);
	КонецЕсли; 
КонецПроцедуры
//-------------------------
Процедура Переоформить()
	Если (ДокументОснование.Выбран()=1) Тогда
		ВводНаОсновании(ДокументОснование);
		Форма.Обновить();
	Иначе
		Предупреждение("Регистрацию Счет-фактуру нельзя переоформлять. Нет документа основания!");
		Возврат;
	КонецЕсли;
КонецПроцедуры
//-----------------------------------------------
//Начало текста, вставленного Инсталлятором МОД
Процедура ПриЗаписи()
	//Внимание: данная процедура при обновлении Менеджера будет перезаписана заново
	//без сохранения текущего кода !!!
	//Рекомендуем собственные алгоритмы записывать в старую процедуру с префиксом стар_
	ПриИзмененииОбъекта(Контекст, ТекущийДокумент(), ДатаДок);
	Если семМожноЗаписать( Контекст ) = 0 Тогда Возврат; КонецЕсли;
КонецПроцедуры
//Конец текста, вставленного Инсталлятором МОД

