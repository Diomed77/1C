Перем ТаблСоот;

Процедура ПроверитьДатуИтогов()
	Перем ДатаТА;
	ДатаТА = ПолучитьДатуТА();
	Если ПустоеЗначение(ДатаИтогов)=1 Тогда
		Если ПустоеЗначение(ДатаДок)=0 Тогда
			ДатаИтогов = ДатаДок;
		Иначе
			ДатаИтогов = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	Если ДатаИтогов>ДатаТА Тогда
		ДатаИтогов = ДатаТА-1;
	КонецЕсли;
КонецПроцедуры

Процедура ПриИзмененииТочкиИтогов()
	Если ТочкаИтогов=2 Тогда
		Форма.ДатаИтогов.Доступность(1);
	Иначе
		Форма.ДатаИтогов.Доступность(0);
	КонецЕсли;
КонецПроцедуры

Функция ДоступностьРеквизитов()
	Если Выбран()=0 Тогда
		//так как документ не записан, то фильтр только на конец даты
		ТочкаИтогов=2;
		ПроверитьДатуИтогов();
		Форма.ТочкаИтогов.Доступность(0);
	Иначе
		//ТочкаИтогов=1;
		Форма.ТочкаИтогов.Доступность(1);
	КонецЕсли;
	ПриИзмененииТочкиИтогов();
КонецФункции

Функция ТекущийРегистр()
	Перем Рез,ТекСтр;
	Рез = "";
	Если ВидРегистра.РазмерСписка()>0 Тогда
		ТекСтр = ВидРегистра.ТекущаяСтрока();
		Если ТекСтр>0 Тогда
			Рез = ВидРегистра.ПолучитьЗначение(ТекСтр);
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции

Функция ПолучитьТипЗначения(Идентификатор,Вид="Измерение")
	Перем ВыбРег,ТекТип,ТекВид,ТекЗнач;
	ВыбРег = ТекущийРегистр();
	Если Вид="Измерение" Тогда
		ТекЗнач = Метаданные.Регистр(ВыбРег).Измерение(Идентификатор);
	Иначе
		Возврат "Строка";
	КонецЕсли;
	ТекТип = ТекЗнач.Тип;
	ТекВид = ТекЗнач.Вид;
	Если ПустоеЗначение(ТекВид)=0 Тогда
		ТекТип=ТекТип+"."+ТекВид;
	КонецЕсли;
	Возврат ТекТип;
КонецФункции

Функция ЭтоАгрегатныйОбъект(Объект)
	Перем ТипОбъекта;
	ТипОбъекта  = ТипЗначенияСтр(Объект);
	Если Найти("Число,Строка,Дата",ТипОбъекта)>0 Тогда
		Возврат 0;
	Иначе
		Возврат 1;
	КонецЕсли;
КонецФункции

Процедура УстановитьПустоеЗначениеТЗ(ТЗ,РеквизитДокумента=0,ТекКол="")
	Перем ТекСтр,ТекРекв,ТекЗнач,ТипРекв,ТекТип;
	Если ТекКол="" Тогда
		ТекКол = ТЗ.ТекущаяКолонка();
	КонецЕсли;
	ТекСтр = ТЗ.ТекущаяСтрока();
	Если ТекСтр>0 Тогда
		Если ТекКол="Значение" Тогда
			ТекРекв = ТЗ.ПолучитьЗначение(ТекСтр,1);
			ТекЗнач = ТЗ.ПолучитьЗначение(ТекСтр,ТекКол);
			ТипРекв = ТипЗначенияСтр(ТекЗнач);
			ТекТип =  ТипРекв;
			
			Попытка 
				ТекТип =  ТекТип+?(ПустоеЗначение(ТекЗнач.Вид())=0,"."+ТекЗнач.Вид(),"");
			Исключение
			КонецПопытки;
			
			Попытка 
				ТекЗнач = ПолучитьПустоеЗначение(ТекТип);
				ТЗ.УстановитьЗначение(ТекСтр,"Значение",ТекЗнач);
				Если ТЗ.ПолучитьПараметрыКолонки(1) = "Измерение" Тогда
					ТЗ.УстановитьЗначение(ТекСтр,"Условие","");
				КонецЕсли;
				Если РеквизитДокумента=1 Тогда
					УстановитьАтрибут(ТекРекв,ТекЗнач);
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ЭтоИзмерение(НазвРег,НазвАтрибута)
	Перем Рез,Изм;
	Рез=0;
	Попытка
		Изм = Метаданные.Регистр(НазвРег).Измерение(НазвАтрибута);
		Рез = Изм.Выбран();
	Исключение
	КонецПопытки;
	Возврат Рез;
КонецФункции

Функция УдовлетворяетУсловиямФильтра(СравниваемоеЗначение,Условие,ЗначФильтра)
	Перем Рез;
	Рез=0;
	Если Условие="=" Тогда
		Если (СравниваемоеЗначение=ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	ИначеЕсли Условие="<>" Тогда
		Если НЕ(СравниваемоеЗначение=ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	ИначеЕсли Условие=">" Тогда
		Если (СравниваемоеЗначение>ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	ИначеЕсли Условие=">=" Тогда
		Если (СравниваемоеЗначение>=ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	ИначеЕсли Условие="<" Тогда
		Если (СравниваемоеЗначение<ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	ИначеЕсли Условие="<=" Тогда
		Если (СравниваемоеЗначение<=ЗначФильтра) Тогда
			Рез=1;
		КонецЕсли;
	Иначе
		Рез=1;
	КонецЕсли;
	Возврат Рез;
КонецФункции

Процедура Заполнить(Операция="*",ВыбЗнак="-")
	Перем ВыбРег,ТекРег,Рег,ТекЗнач,ТекУсл;
	Перем НазвИзм,ЗначИзм,ИтогРасчета,Кол,Стр,Ном;
	Перем МД,ТекМД,ТекТип,ПроходитФильтр;
	Перем НазвРекв;
	
	Если Операция<>"+" Тогда
		УдалитьСтроки();
	КонецЕсли;
	Если Операция="-" Тогда
		Возврат;
	КонецЕсли;
	
	ВыбРег=ТекущийРегистр();
	Если ПустоеЗначение(ВыбРег)=1 Тогда
		Сообщить("Не выбран регистр!");
		Возврат;
	КонецЕсли;
	ТекРег="Регистр"+?(ПустоеЗначение(ВыбРег)=0,"."+ВыбРег,"");
	Рег = СоздатьОбъект(ТекРег);
	Рег.ВременныйРасчет();
	
	//установим фильтр на итоги измерений по условию "="
	Фильтр.ВыбратьСтроки();
	Пока Фильтр.ПолучитьСтроку()=1 Цикл
		НазвИзм = Фильтр.Измерение;
		ТекЗнач = Фильтр.Значение;
		ТекУсл  = Фильтр.Условие;
		Если ПустоеЗначение(ТекУсл)=1 Тогда
			Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(Фильтр.ТипРекв)=0 Тогда
			//фильтр не измерений пропускаем
			Продолжить;
		КонецЕсли;
		Если ТекУсл="=" Тогда
			Если ПустоеЗначение(ТекЗнач)=1 Тогда
				ТекЗнач = ПолучитьПустоеЗначение(ПолучитьТипЗначения(НазвИзм));
			КонецЕсли;
			Если Пустоезначение(ТекЗнач)=1 Тогда
				Рег.УстановитьЗначениеФильтра(НазвИзм,ТекЗнач,1);
			Иначе
				Рег.УстановитьЗначениеФильтра(НазвИзм,ТекЗнач,2);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//опеределим точку итогов и рассчитаем на нее
	Если ТочкаИтогов=2 Тогда //на дату
		ПроверитьДатуИтогов();
		РассчитатьРегистрыПо(ДатаИтогов);
	ИначеЕсли ТочкаИтогов=1 Тогда //на текущий документ
		Если Выбран()=1 Тогда
			РассчитатьРегистрыНа(ТекущийДокумент());
		Иначе
			Сообщить("Текущий документ не записан! Невозможно рассчитать итоги по незаписанному документу...");
			Возврат;
		КонецЕсли;
	Иначе
		Сообщить("Ошибка выбора точки итогов!");
		Возврат;
	КонецЕсли;
	
	ИтогРасчета = СоздатьОбъект("ТаблицаЗначений");
	Рег.ВыгрузитьИтоги(ИтогРасчета,1);
	
	ИтогРасчета.ВыбратьСтроки();
	Пока ИтогРасчета.ПолучитьСтроку()=1 Цикл

		//фильтр
		ПроходитФильтр = 1;
		Для Кол=1 по ИтогРасчета.КоличествоКолонок() Цикл
			НазвИзм = ИтогРасчета.ПолучитьПараметрыКолонки(Кол);
			ЗначИзм = ИтогРасчета.ПолучитьЗначение(ИтогРасчета.НомерСтроки,Кол);
			Стр=0;
			Если Фильтр.НайтиЗначение(НазвИзм,Стр,"Измерение")=1 Тогда
				Фильтр.ПолучитьстрокуПоНомеру(Стр);
				Если УдовлетворяетУсловиямФильтра(ЗначИзм,Фильтр.Условие,Фильтр.Значение)=0 Тогда
					ПроходитФильтр = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ПроходитФильтр = 0 Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока();
		Знак = ВыбЗнак;

		//цикл по измерениям и ресурсам
		Для Кол=1 по ИтогРасчета.КоличествоКолонок() Цикл
			НазвИзм = ИтогРасчета.ПолучитьПараметрыКолонки(Кол);
			ЗначИзм = ИтогРасчета.ПолучитьЗначение(ИтогРасчета.НомерСтроки,Кол);
			Стр=0;
			Если ТаблСоот.НайтиЗначение(НазвИзм,Стр,"ЗагТЧ")=1 Тогда
				НазвРекв=ТаблСоот.ПолучитьЗначение(Стр,"РеквТЧ");
				МД 		= Метаданные.Регистр(ВыбРег).Измерение(НазвИзм);
				ТекМД 	= "";
				Если МД.Выбран()=1 Тогда
					ТекМД = МД;
				Иначе
					МД 	= Метаданные.Регистр(ВыбРег).Ресурс(НазвИзм);
					Если МД.Выбран()=1 Тогда
						ТекМД = МД;
					Иначе
						ТекМД = Метаданные.Регистр(ВыбРег).Реквизит(НазвИзм);
					КонецЕсли;
				КонецЕсли;
				Если ПустоеЗначение(ТекМД)=1 Тогда
					Продолжить;
				КонецЕсли;
				ТекТип = ТекМД.Тип+?(ПустоеЗначение(ТекМД.Вид)=0,"."+ТекМД.Вид,"");
				НазначитьТип(НазвРекв,ТекТип,ТекМД.Длина,ТекМД.Точность);
				УстановитьАтрибут(НазвРекв,ЗначИзм);
			КонецЕсли;				
		КонецЦикла;
		
		//цикл по реквизитам
		Для Ном=1 по Метаданные.Регистр(ВыбРег).Реквизит() Цикл
			ТекМД = Метаданные.Регистр(ВыбРег).Реквизит(Ном);
			НазвРекв = "Реквизит"+Ном;
			ТекТип = ТекМД.Тип+?(ПустоеЗначение(ТекМД.Вид)=0,"."+ТекМД.Вид,"");
			НазначитьТип(НазвРекв,ТекТип,ТекМД.Длина,ТекМД.Точность);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыборЗначенияТЗ(ТЗ)
	Перем ТекСтр,ТекКол,ТекРекв,ТекЗнач,Рекв,ТекТип;
	
	ТекСтр = ТЗ.ТекущаяСтрока();
	ТекКол = ТЗ.ТекущаяКолонка();
	Если (ТекСтр>0)И(ПустоеЗначение(ТекКол)=0) Тогда
		ТекРекв = ТЗ.ПолучитьЗначение(ТекСтр,"Реквизит");
		ТекЗнач = ТЗ.ПолучитьЗначение(ТекСтр,"Значение");
		Если ТекКол = "Значение" Тогда
			Рекв = Метаданные.ОбщийРеквизитДокумента(ТекРекв);
			ТекТип = Рекв.Тип+?(ПустоеЗначение(Рекв.Вид)=0,"."+Рекв.Вид,"");
			Если ВвестиЗначение(ТекЗнач,"Введите значение реквизита",ТекТип,Рекв.Длина,Рекв.Точность)=1 Тогда
				ТЗ.УстановитьЗначение(ТекСтр,"Значение",ТекЗнач);
				УстановитьАтрибут(ТекРекв,ТекЗнач);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ВыборЗначенияФильтра()
	Перем ВидРег,ТекИзм,ТекЗнач,ТекТип,ТекМета;
	Перем ТекСтрФ,ТекКолФ,ТекТипФ,СписокУсловий,ТекУсл;
	
	ТекСтрФ = Фильтр.ТекущаяСтрока();
	ТекКолФ = Фильтр.ТекущаяКолонка();
	Если ТекСтрФ>0 Тогда
		ВидРег 	= ВидРегистра.ПолучитьЗначение(ВидРегистра.ТекущаяСтрока());
		ТекИзм 	= Фильтр.ПолучитьЗначение(ТекСтрФ,"Измерение");
		ТекЗнач = Фильтр.ПолучитьЗначение(ТекСтрФ,"Значение");
		ТекТипФ = Фильтр.ПолучитьЗначение(ТекСтрФ,"ТипРекв");
		Если ТекТипФ=0 Тогда
			ТекМета = Метаданные.Регистр(ВидРег).Измерение(ТекИзм);
		ИначеЕсли ТекТипФ=1 Тогда
			ТекМета = Метаданные.Регистр(ВидРег).Ресурс(ТекИзм);
		КонецЕсли;
		ТекТип 	= ТекМета.Тип+?(ПустоеЗначение(ТекМета.Вид)=0,"."+ТекМета.Вид,"");
		Если ТекКолФ="Значение" Тогда
			Если ВвестиЗначение(ТекЗнач,"Введите значение фильтра",ТекТип,ТекМета.Длина,ТекМета.Точность)=1 Тогда
				Фильтр.УстановитьЗначение(ТекСтрФ,"Условие","=");
				Фильтр.УстановитьЗначение(ТекСтрФ,"Значение",ТекЗнач);
			КонецЕсли;
		
		ИначеЕсли ТекКолФ="Условие" Тогда
			
			ТекУсл = Фильтр.ПолучитьЗначение(ТекСтрФ,"Условие");
			
			СписокУсловий = СоздатьОбъект("СписокЗначений");
			СписокУсловий.ДобавитьЗначение("","<без фильтра>");
			СписокУсловий.ДобавитьЗначение("=","равно");
			СписокУсловий.ДобавитьЗначение("<>","не равно");
			
			Если Найти(".Число.Дата.","."+ТекТип+".")>0 Тогда
				СписокУсловий.ДобавитьЗначение(">","больше");
				СписокУсловий.ДобавитьЗначение("<","меньше");
				СписокУсловий.ДобавитьЗначение(">=","больше или равно");
				СписокУсловий.ДобавитьЗначение("<=","меньше или равно");
			КонецЕсли;
			
			Если СписокУсловий.ВыбратьЗначение(ТекУсл,,,60,2)=1 Тогда
				Фильтр.УстановитьЗначение(ТекСтрФ,"Условие",ТекУсл);
				Если ТекУсл="" Тогда
					УстановитьПустоеЗначениеТЗ(Фильтр,,"Значение");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьТипыИЗаголовки(ИнициализацияФильтра=1)
	Перем ТекСтр,ВыбРег,ТекТип;
	Перем РеквИ,ТекИзм,ТекАтрибутИ;
	Перем РеквР,ТекРес,ТекАтрибутР;
	Перем РеквРк,ТекРек,ТекАтрибутРк;
	
	ТаблСоот.УдалитьСтроки();
	
	Если ИнициализацияФильтра=1 Тогда
		Фильтр.УдалитьСтроки();
	КонецЕсли;
	
	ТекСтр = ВидРегистра.ТекущаяСтрока();
	Если ТекСтр>0 Тогда
		ВыбРег = СокрП(ВидРегистра.ПолучитьЗначение(Текстр));
		Если СокрП(НазваниеРегистра)<>ВыбРег Тогда
			НазваниеРегистра=ВыбРег;
		КонецЕсли;
		Для Ном=1 по 10 Цикл
			
			//измерения
			РеквИ = "Измерение"+Ном;
			ТекИзм = Метаданные.Регистр(ВыбРег).Измерение(Ном);
			ТекАтрибутИ = Форма.ПолучитьАтрибут(РеквИ);
			Если ТекИзм.Выбран()=1 Тогда
				ТекАтрибутИ.Видимость(1);
				ТекАтрибутИ.Заголовок(ТекИзм.Идентификатор);
				ТекТип = ТекИзм.Тип+?(ПустоеЗначение(ТекИзм.Вид)=0,"."+ТекИзм.Вид,"");
				НазначитьТип(РеквИ,ТекТип,ТекИзм.Длина,ТекИзм.Точность);
				
				Если ИнициализацияФильтра=1 Тогда
					Фильтр.НоваяСтрока();
					Фильтр.Ном			= Ном;
					Фильтр.ТипРекв 		= 0;
					Фильтр.Измерение 	= ТекИзм.Идентификатор;
					Фильтр.Значение	 	= ПолучитьПустоеЗначение(ТекТип);
				КонецЕсли;
				
			Иначе
				ТекАтрибутИ.Видимость(0);
				ТекАтрибутИ.Заголовок("");
			КонецЕсли;
			ТаблСоот.НоваяСтрока();
			ТаблСоот.РеквТЧ = РеквИ;
			ТаблСоот.ЗагТЧ	= ТекАтрибутИ.Заголовок();
            
			//ресурсы
			РеквР = "Ресурс"+Ном;
			ТекРес = Метаданные.Регистр(ВыбРег).Ресурс(Ном);
			ТекАтрибутР = Форма.ПолучитьАтрибут(РеквР);
			Если ТекРес.Выбран()=1 Тогда
				ТекАтрибутР.Видимость(1);
				ТекАтрибутР.Заголовок(ТекРес.Идентификатор);
				ТекТип = ТекРес.Тип+?(ПустоеЗначение(ТекРес.Вид)=0,"."+ТекРес.Вид,"");
				НазначитьТип(РеквР,ТекТип,ТекРес.Длина,ТекРес.Точность);
				
				Если ИнициализацияФильтра=1 Тогда
					Фильтр.НоваяСтрока();
					Фильтр.Ном			= Ном;
					Фильтр.ТипРекв 		= 1;
					Фильтр.Измерение 	= ТекРес.Идентификатор;
					Фильтр.Значение		= ПолучитьПустоеЗначение(ТекТип);
				КонецЕсли;
				
			Иначе
				ТекАтрибутР.Видимость(0);
				ТекАтрибутР.Заголовок("");
			КонецЕсли;
			ТаблСоот.НоваяСтрока();
			ТаблСоот.РеквТЧ = РеквР;
			ТаблСоот.ЗагТЧ	= ТекАтрибутР.Заголовок();

			//реквизиты
			РеквРк = "Реквизит"+Ном;
			ТекРек = Метаданные.Регистр(ВыбРег).Реквизит(Ном);
			ТекАтрибутРк = Форма.ПолучитьАтрибут(РеквРк);
			Если ТекРек.Выбран()=1 Тогда
				ТекАтрибутРк.Видимость(1);
				ТекАтрибутРк.Заголовок(ТекРек.Идентификатор);
				ТекТип = ТекРек.Тип+?(ПустоеЗначение(ТекРек.Вид)=0,"."+ТекРек.Вид,"");
				НазначитьТип(РеквРк,ТекТип,ТекРек.Длина,ТекРек.Точность);
			Иначе
				ТекАтрибутРк.Видимость(0);
				ТекАтрибутРк.Заголовок("");
			КонецЕсли;
			ТаблСоот.НоваяСтрока();
			ТаблСоот.РеквТЧ = РеквРк;
			ТаблСоот.ЗагТЧ	= ТекАтрибутРк.Заголовок();
		КонецЦикла;
	КонецЕсли;
	
	Фильтр.Сортировать("ТипРекв,Ном");
КонецПроцедуры

Процедура ПриВыбореРегистра(ПриОткрытии=0)
	//Фильтр.УдалитьСтроки();
	Если ПриОткрытии=0 тогда
		УдалитьСтроки();
	КонецЕсли;
	УстановитьТипыИЗаголовки();
КонецПроцедуры
    
Процедура ПриНачалеРедактированияСтроки()
	Перем ТекКол,ЗначКол;
	ТекКол = Форма.АктивныйЭлемент();
	Если ТекКол="Знак" Тогда
		ЗначКол = ПолучитьАтрибут(ТекКол);
		Если ЗначКол="+" Тогда
			ЗначКол = "-";
		Иначе
			ЗначКол = "+";
		КонецЕсли;
		УстановитьАтрибут(ТекКол,ЗначКол);
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ПриРедактированииНовойСтроки()
	Если ПустоеЗначение(Знак)=1 Тогда
		Знак="+";
	КонецЕсли;
	УстановитьТипыИЗаголовки(0);
КонецПроцедуры

Процедура ЗаполнитьВидыРегистров()
	Перем КолРег,Ном,ТекРег;
	ВидРегистра.УдалитьВсе();
	КолРег = Метаданные.Регистр();
	Для Ном = 1 по КолРег Цикл
		ТекРег = Метаданные.Регистр(Ном).Идентификатор;
		ВидРегистра.ДобавитьЗначение(ТекРег);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьСписокОбщихРеквизитов()
	Перем КолРекв,Ном,Рекв,НазвРекв;
	ОбщиеРеквизиты.УдалитьСтроки();
	КолРекв = Метаданные.ОбщийРеквизитДокумента();
	Для Ном=1 по КолРекв Цикл
		Рекв = Метаданные.ОбщийРеквизитДокумента(Ном);
		НазвРекв = Рекв.Идентификатор;
		Если ПустоеЗначение(НазвРекв)=0 Тогда
			ОбщиеРеквизиты.НоваяСтрока();
			ОбщиеРеквизиты.Реквизит=НазвРекв;
			ОбщиеРеквизиты.Значение=ПолучитьАтрибут(НазвРекв);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОтобратьРегистрыПоДвижениямДокумента(ДокументДвижения,СписокРегистров)
	Перем Ном,КолРег;
	Если ПустоеЗначение(ДокументДвижения)=1 Тогда
		СписокРегистров.УдалитьВсе();
	КонецЕсли;
	КолРег = СписокРегистров.РазмерСписка();
	Для Ном=-КолРег по -1 Цикл
		ТекРег = СписокРегистров.ПолучитьЗначение(-Ном);
		Рег = СоздатьОбъект("Регистр."+ТекРег);
		Если Рег.ВыбратьДвиженияДокумента(ДокументДвижения)=0 Тогда
			СписокРегистров.УдалитьЗначение(-Ном);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ТипРеквизитаРегистра(НаимРег,ТипРекв,НазвРекв,Длина=0,Точность=0)
	Перем Рез,МетаРег,МетаРекв;
	Рез = "";
	МетаРег 	= Метаданные.Регистр(НаимРег);
	Если МетаРег.Выбран()=1 Тогда
		Если ТипРекв="Измерение" Тогда
			МетаРекв 	= МетаРег.Измерение(НазвРекв);
		ИначеЕсли ТипРекв="Ресурс" Тогда
			МетаРекв 	= МетаРег.Ресурс(НазвРекв);
		ИначеЕсли ТипРекв="Реквизит" Тогда
			МетаРекв 	= МетаРег.Реквизит(НазвРекв);
		Иначе
			Возврат Рез;
		КонецЕсли;
		Если МетаРекв.Выбран()=1 Тогда
			Рез = МетаРекв.Тип;
			Если ПустоеЗначение(МетаРекв.Вид)=0 Тогда
				Рез=Рез+"."+МетаРекв.Вид;
			КонецЕсли;
			Длина = МетаРекв.Длина;
			Точность = МетаРекв.Точность;
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции
	
Процедура ВводНаОсновании(ДокОсн)
	Перем КолРег,ТекРег,Ном,КолСтр,МетаРег;
	Перем ТекРекв,ТекЗнач;
	Перем НазвРекв,ТипРекв,ДлинаРекв,ТочностьРекв;
	
	ТекРег = "";
	Если ПустоеЗначение(ДокОсн)=1 Тогда
		Сообщить("Не выбран документ основание!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Создать сторно по движениям документа "+ДокОсн+" ?",4)<>6 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	УстановитьТипыИЗаголовки(1);
	ЗаполнитьВидыРегистров();
	Если ВидРегистра.РазмерСписка()>0 Тогда
		СписокРегистров = СоздатьОбъект("СписокЗначений");
		ВидРегистра.Выгрузить(СписокРегистров);
		ОтобратьРегистрыПоДвижениямДокумента(ДокОсн,СписокРегистров);
		КолРег = СписокРегистров.РазмерСписка();
		Если КолРег=1 Тогда
			ТекРег = СписокРегистров.ПолучитьЗначение(1);
		ИначеЕсли КолРег>1 Тогда
			СписокРегистров.ВыбратьЗначение(ТекРег,"Выберите регистр движений",,,0);
		Иначе
			Сообщить("Нет движений у документа основания!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		Если ПустоеЗначение(ТекРег)=1 Тогда
			Сообщить("Не выбран регистр двжиений у документа основания!");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		ВидРегистра.ТекущаяСтрока(ВидРегистра.НайтиЗначение(ТекРег));
		
		НазваниеРегистра = ТекРег;
		ДокументОснование = ДокОсн;
		УстановитьТипыИЗаголовки(1);
		
		//общие реквизиты
		КолСтр = Метаданные.ОбщийРеквизитДокумента();
		Для Ном=1 по КолСтр Цикл
			ТекРекв = Метаданные.ОбщийРеквизитДокумента(Ном).Идентификатор;
			ТекЗнач = ДокОсн.ПолучитьАтрибут(ТекРекв);
			УстановитьАтрибут(ТекРекв,ТекЗнач);
		КонецЦикла;
		
		//строки
		Рег = СоздатьОбъект("Регистр."+ТекРег);
		Если Рег.ВыбратьДвиженияДокумента(ДокументОснование)=1 Тогда
			
			МетаРег = Метаданные.Регистр(ТекРег);
			
			Пока Рег.ПолучитьДвижение()=1 Цикл
				НоваяСтрока();
				Если Рег.Приход=1 Тогда
					Знак="-";
				Иначе
					Знак="+";
				КонецЕсли;
				
				//измерения
				КолСтр = МетаРег.Измерение();
				Для Ном=1 по КолСтр Цикл
					ТекРекв = МетаРег.Измерение(Ном).Идентификатор;
					ТекЗнач = Рег.ПолучитьАтрибут(ТекРекв);
					Стр = 0;
					Если ТаблСоот.НайтиЗначение(ТекРекв,Стр,"ЗагТЧ")=1 Тогда
						НазвРекв=ТаблСоот.ПолучитьЗначение(Стр,"РеквТЧ");
						
						ДлинаРекв=0;
						ТочностьРекв=0;
						ТипРекв = ТипРеквизитаРегистра(ТекРег,"Измерение",ТекРекв,ДлинаРекв,ТочностьРекв);
						Если ПустоеЗначение(ТипРекв)=0 Тогда
							НазначитьТип(НазвРекв,ТипРекв,ДлинаРекв,ТочностьРекв);
						КонецЕсли;
						
						УстановитьАтрибут(НазвРекв,ТекЗнач);
					КонецЕсли;
				КонецЦикла;
				
				//ресурсы
				КолСтр = МетаРег.Ресурс();
				Для Ном=1 по КолСтр Цикл
					ТекРекв = МетаРег.Ресурс(Ном).Идентификатор;
					ТекЗнач = Рег.ПолучитьАтрибут(ТекРекв);
					Стр = 0;
					Если ТаблСоот.НайтиЗначение(ТекРекв,Стр,"ЗагТЧ")=1 Тогда
						НазвРекв=ТаблСоот.ПолучитьЗначение(Стр,"РеквТЧ");
						
						ДлинаРекв=0;
						ТочностьРекв=0;
						ТипРекв = ТипРеквизитаРегистра(ТекРег,"Ресурс",ТекРекв,ДлинаРекв,ТочностьРекв);
						Если ПустоеЗначение(ТипРекв)=0 Тогда
							НазначитьТип(НазвРекв,ТипРекв,ДлинаРекв,ТочностьРекв);
						КонецЕсли;
						
						УстановитьАтрибут(НазвРекв,ТекЗнач);
					КонецЕсли;
				КонецЦикла;
				
				//реквизиты
				КолСтр = МетаРег.Реквизит();
				Для Ном=1 по КолСтр Цикл
					ТекРекв = МетаРег.Реквизит(Ном).Идентификатор;
					ТекЗнач = Рег.ПолучитьАтрибут(ТекРекв);
					Стр = 0;
					Если ТаблСоот.НайтиЗначение(ТекРекв,Стр,"ЗагТЧ")=1 Тогда
						НазвРекв=ТаблСоот.ПолучитьЗначение(Стр,"РеквТЧ");
						
						ДлинаРекв=0;
						ТочностьРекв=0;
						ТипРекв = ТипРеквизитаРегистра(ТекРег,"Реквизит",ТекРекв,ДлинаРекв,ТочностьРекв);
						Если ПустоеЗначение(ТипРекв)=0 Тогда
							НазначитьТип(НазвРекв,ТипРекв,ДлинаРекв,ТочностьРекв);
						КонецЕсли;
						
						УстановитьАтрибут(НазвРекв,ТекЗнач);
					КонецЕсли;
				КонецЦикла;
			
			КонецЦикла;
		КонецЕсли;
	Иначе
		Сообщить("Нет регистров!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Перем ТекПоз;
	
	ПриЗаписиПерепроводить(1);
	
	//заполняем список общих реквизитов
	ЗаполнитьСписокОбщихРеквизитов();
	
	//заполняем возможные виды регистров
	ЗаполнитьВидыРегистров();
	Если ПустоеЗначение(НазваниеРегистра)=0 Тогда
		ТекПоз = ВидРегистра.НайтиЗначение(СокрП(НазваниеРегистра));
		Если ТекПоз>0 Тогда
			ВидРегистра.ТекущаяСтрока(ТекПоз);
		КонецЕсли;
	КонецЕсли;
	Если ВидРегистра.РазмерСписка()>0 Тогда
		Если ВидРегистра.ТекущаяСтрока()=0 Тогда
			ВидРегистра.ТекущаяСтрока(1);
		КонецЕсли;
	Иначе
		Сообщить("В данной конфигурации нет ни одного регистра!");
	КонецЕсли;
    ПриВыбореРегистра(1);
	
	//заполняем точку итогов
	ТочкаИтогов=1;
	ДоступностьРеквизитов();
	
КонецПроцедуры

Процедура Печать()
	Перем Ном,КолРекв,Рекв,НазвРекв,ТекРекв,ЗначРекв;
	Перем КолИзм,КолРес,КолРек,ТаблЧасть,СписокИтогов;
	Перем Таб,ПечСтрока,ПечИтог;
	
	Таб = СоздатьОбъект("Таблица");
	
	ПечРегистр = СокрП(НазваниеРегистра);
	
	//печать шапки документа
	Таб.ВывестиСекцию("ШапкаДок|Общая");
	
	//печать общих реквизитов
	КолРекв = Метаданные.ОбщийРеквизитДокумента();
	Для Ном=1 по КолРекв Цикл
		Рекв = Метаданные.ОбщийРеквизитДокумента(Ном);
		НазвРекв = Рекв.Идентификатор;
		ПечСтрока = НазвРекв+": "+ПолучитьАтрибут(НазвРекв);
		Таб.ВывестиСекцию("ОбщРекв|Общая");
	КонецЦикла;
	
	//печать шапки табличной части
	Таб.ВывестиСекцию("ШапкаТЧ|Общая");
	
	КолИзм = Метаданные.Регистр(ПечРегистр).Измерение();
	Для Ном=1 по КолИзм Цикл
		ПечРекв = Метаданные.Регистр(ПечРегистр).Измерение(Ном).Идентификатор;
		Таб.ПрисоединитьСекцию("ШапкаТЧ|Измерение");
	КонецЦикла;

	КолРес = Метаданные.Регистр(ПечРегистр).Ресурс();
	Для Ном=1 по КолРес Цикл
		ПечРекв = Метаданные.Регистр(ПечРегистр).Ресурс(Ном).Идентификатор;
		Таб.ПрисоединитьСекцию("ШапкаТЧ|Ресурс");
	КонецЦикла;
	
	КолРек = Метаданные.Регистр(ПечРегистр).Реквизит();
	Для Ном=1 по КолРек Цикл
		ПечРекв = Метаданные.Регистр(ПечРегистр).Реквизит(Ном).Идентификатор;
		Таб.ПрисоединитьСекцию("ШапкаТЧ|реквизит");
	КонецЦикла;

	//печать табличной части
	ТаблЧасть = СоздатьОбъект("ТаблицаЗначений");
	СписокИтогов = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблЧасть);
	
	ТаблЧасть.ВыбратьСтроки();
	Пока ТаблЧасть.ПолучитьСтроку()=1 Цикл
		
		Таб.ВывестиСекцию("Строка|Общая");
		
		Для Ном=1 по КолИзм Цикл
			ТекРекв = "Измерение"+Ном;
			ЗначРекв= ТаблЧасть.ПолучитьЗначение(ТаблЧасть.НомерСтроки,ТекРекв);
			Таб.ПрисоединитьСекцию("Строка|Измерение");
		КонецЦикла;
	
		Для Ном=1 по КолРес Цикл
			ТекРекв = "Ресурс"+Ном;
			ЗначРекв= ТаблЧасть.ПолучитьЗначение(ТаблЧасть.НомерСтроки,ТекРекв);
			СписокИтогов.Установить(ТекРекв,СписокИтогов.Получить(ТекРекв)+Число(ЗначРекв));
			Таб.ПрисоединитьСекцию("Строка|Ресурс");
		КонецЦикла;
		
		Для Ном=1 по КолРек Цикл
			ТекРекв = "Реквизит"+Ном;
			ЗначРекв= ТаблЧасть.ПолучитьЗначение(ТаблЧасть.НомерСтроки,ТекРекв);
			Таб.ПрисоединитьСекцию("Строка|Ресурс");
		КонецЦикла;
		
	КонецЦикла;
	
	//печать итогов табличной части
	Таб.ВывестиСекцию("Итоги|Общая");
	
	Для Ном=1 по КолИзм Цикл
		Таб.ПрисоединитьСекцию("Итоги|Измерение");
	КонецЦикла;

	Для Ном=1 по КолРес Цикл
		ТекРекв = "Ресурс"+Ном;
		ПечИтог = СписокИтогов.Получить(ТекРекв);
		Таб.ПрисоединитьСекцию("Итоги|Ресурс");
	КонецЦикла;
	
	Для Ном=1 по КолРек Цикл
		Таб.ПрисоединитьСекцию("Итоги|Реквизит");
	КонецЦикла;

	Таб.Показать("Коррекция по регистру "+ПечРегистр+" №"+НомерДок+" от "+ДатаДок);
КонецПроцедуры

ТаблСоот = СоздатьОбъект("ТаблицаЗначений");
ТаблСоот.НоваяКолонка("РеквТЧ");
ТаблСоот.НоваяКолонка("ЗагТЧ");

Фильтр.НоваяКолонка("Ном" ,"Число",,,,2);
Фильтр.НоваяКолонка("ТипРекв","Число",1,,"Тип",3);
Фильтр.НоваяКолонка("Измерение",,,,"Реквизит",15);
Фильтр.НоваяКолонка("Условие"  ,,,,"усл",3);
Фильтр.НоваяКолонка("Значение" ,"Неопределенный",,,,15);
Фильтр.ВидимостьКолонки("Ном",0);

ОбщиеРеквизиты.НоваяКолонка("Реквизит",,,,,15);
ОбщиеРеквизиты.НоваяКолонка("Значение",,,,,15);