// ********************
//
Процедура стар_ОбработкаПроведения()
	
	
	Если семМожноПровести( Контекст ) = 0 Тогда Возврат; КонецЕсли;
	
	Регистры=СоздатьОбъект("Регистры");    
	Рег=Регистры.СостояниеДолгов;
	РегВз=Регистры.ВзаиморасчетыПокупателей;
	Регистры.Актуальность(1);
	СписокКлиентов=СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокКлиентов,"Клиент");
	Рег.УстановитьЗначениеФильтра("Клиент",СписокКлиентов,2);
	РегВз.УстановитьЗначениеФильтра("Клиент",СписокКлиентов,2);
	Если ИтогиАктуальны()=0 Тогда
		Рег.ВременныйРасчет(1);
		РегВз.ВременныйРасчет(1);
		Регистры.РассчитатьРегистрыНа(ТекущийДокумент());
	КонецЕсли;
	Спр=СоздатьОбъект("Справочник.Контрагенты");

	тКлиенты = СоздатьОбъект("ТаблицаЗначений");
	тКлиенты.НоваяКолонка("Клиент");
	тКлиенты.НоваяКолонка("Статус");
	
	ТабДолгов=СоздатьОбъект("ТаблицаЗначений");
    ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		// спишем то что есть на этот документ
		Состояние(Шаблон("Проведение: [НомерСтроки]/[КоличествоСтрок()]"));
		Рег.УстановитьФильтр(Клиент,,ПоДокументу);
		Рег.ВыгрузитьИтоги(ТабДолгов,1,1);
		НадоСписать=Сумма;
		Списали=0;
		ТабДолгов.Сортировать("ПоДокументу");
		ТабДолгов.ВыбратьСтроки();
		Пока ТабДолгов.ПолучитьСтроку() = 1 Цикл
			Если НадоСписать <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			Рез=ДатаДок-ПоДокументу.ДатаОплаты;
			Если Рез<0 Тогда
				Продолжить;
			КонецЕсли;
			
			Регистр.СостояниеДолгов.Клиент=Клиент;
			Регистр.СостояниеДолгов.ПоДокументу=ПоДокументу;
			Регистр.СостояниеДолгов.СпособВоздействия=ТабДолгов.СпособВоздействия;
			Регистр.СостояниеДолгов.Инкассо=ТабДолгов.Инкассо;
			Регистр.СостояниеДолгов.Долг = Мин(НадоСписать,ТабДолгов.Долг);
			Списали=Списали+Мин(НадоСписать,ТабДолгов.Долг);
			Регистр.СостояниеДолгов.ДвижениеРасходВыполнить();
			НадоСписать=НадоСписать-Мин(НадоСписать,ТабДолгов.Долг);
		КонецЦикла;
		// выставим по новому
		МожноВыставлять=Мин(Сумма,РегВз.СводныйОстаток(,Клиент,,,ПоДокументу,"Долг"));
		Если МожноВыставлять <> Сумма Тогда
			Если МожноВыставлять=0 Тогда
				Сообщить("По документу "+ПоДокументу+" невозможно зарегистрировать просроченный долг! Долг оплачен или списан!");
			Иначе
				Сообщить("По документу "+ПоДокументу+" можно зарегистрировать просроченный долг на сумму "+МожноВыставлять);
			КонецЕсли;
		КонецЕсли;
		
		Если МожноВыставлять <> 0 Тогда

			Регистр.СостояниеДолгов.Клиент=Клиент;
			Регистр.СостояниеДолгов.ПоДокументу=ПоДокументу;
			Регистр.СостояниеДолгов.СпособВоздействия=Статус;
			Регистр.СостояниеДолгов.Долг = Сумма;
			Если (Статус=Перечисление.СтатусыПросроченногоДолга.ИнкассоПодано) Тогда
				Регистр.СостояниеДолгов.Инкассо=Инкассо;
			КонецЕсли;
			Регистр.СостояниеДолгов.ДвижениеПриходВыполнить();
			тКлиенты.НоваяСтрока();
			тКлиенты.Клиент=Клиент;
			тКлиенты.Статус=Статус;
			
		КонецЕсли;
	КонецЦикла;
	
	тКлиенты.Свернуть("Клиент,Статус","");
	тКлиенты.ВыбратьСтроки();
	Пока тКлиенты.ПолучитьСтроку() = 1 Цикл
		Спр.НайтиЭлемент(тКлиенты.Клиент);

		Если тКлиенты.Статус = Перечисление.СтатусыПросроченногоДолга.Предупреждение1 Тогда
			УстановитьРеквизитСправочника(Спр.ТекущийЭлемент(),"КомментарийДляКредитаАгенту",Строка(ДатаДок)+" выслано 1-е предупреждение",ТекущийДокумент());
		ИначеЕсли тКлиенты.Статус = Перечисление.СтатусыПросроченногоДолга.Предупреждение2 Тогда
			УстановитьРеквизитСправочника(Спр.ТекущийЭлемент(),"КомментарийДляКредитаАгенту",Строка(ДатаДок)+" выслано 2-е предупреждение",ТекущийДокумент());
		ИначеЕсли тКлиенты.Статус = Перечисление.СтатусыПросроченногоДолга.Извещение Тогда
			УстановитьРеквизитСправочника(Спр.ТекущийЭлемент(),"КомментарийДляКредитаАгенту",Строка(ДатаДок)+" выслано извещение о штрафных санкциях",ТекущийДокумент());
		ИначеЕсли тКлиенты.Статус = Перечисление.СтатусыПросроченногоДолга.ИнкассоПодано Тогда
			УстановитьРеквизитСправочника(Спр.ТекущийЭлемент(),"КомментарийДляКредитаАгенту",Строка(ДатаДок)+" подано заявление в Инкассо",ТекущийДокумент());
		ИначеЕсли тКлиенты.Статус = Перечисление.СтатусыПросроченногоДолга.ИнкассоНеплатежеспособность Тогда
			УстановитьРеквизитСправочника(Спр.ТекущийЭлемент(),"КомментарийДляКредитаАгенту",Строка(ДатаДок)+" выслана кредиторская заявка",ТекущийДокумент());
			
		КонецЕсли;

		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения()
	//Внимание: данная процедура при обновлении Менеджера будет перезаписана заново
	//без сохранения текущего кода !!!
	//Рекомендуем собственные алгоритмы записывать в старую процедуру с префиксом стар_
	Если ПроведениеМОД(Контекст)=1 Тогда
	    Возврат;
	КонецЕсли;
	стар_ОбработкаПроведения();
КонецПроцедуры
