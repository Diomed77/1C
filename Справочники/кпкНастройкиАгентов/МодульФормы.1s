Перем ТекВидДок; //текущий вид документа в закладке "Документы"
                      
//******************************************************************************
//вызывается при нажатии на кнопку "Папка обмена по умолчанию" на закладке "Обмен"
Процедура кнПапкаОбменаПоУмолчВыбор() 
    ПапкаОбмена = "\Program Files\agentp2\exchange\";
КонецПроцедуры     

//******************************************************************************
Процедура списСпособОбменаВыбор()
	
	ТекСтр = списСпособОбмена.ТекущаяСтрока();
	Если ПустоеЗначение(ТекСтр) = 1 Тогда
		ТекСтр = 1;
		списСпособОбмена.ТекущаяСтрока(ТекСтр);
	КонецЕсли;
	
	СпособОбмена = списСпособОбмена.ПолучитьЗначение(ТекСтр);	
	Если СпособОбмена = 2 Тогда //выбран обмен через "Агент+ Сервер"
		ИспАрхивы = 1; //"Агент Плюс СОД" поддерживает обмен только в архивах "ZIP"
		Форма.ИспАрхивы.Доступность(0); 
	Иначе                               
		Форма.ИспАрхивы.Доступность(1); 
	КонецЕсли;
	
	//видимость реквизитов обмена включается только если в диалоге ткуущая закладка "Обмен"
	ТекЗакладка = Форма.Закладки.ТекущаяСтрока();
	Если ПустоеЗначение(ТекЗакладка) = 1 Тогда
	    Возврат;
	Иначе
		ТекЗначЗакл = Форма.Закладки.ПолучитьЗначение(ТекЗакладка);
		Если ТекЗначЗакл <> "Обмен" Тогда
		    Возврат;
		КонецЕсли;
	КонецЕсли;
	     
	Если СпособОбмена = 0 Тогда      // Прямой способ
		Слои = "Обмен, ОбменаНет";
	ИначеЕсли СпособОбмена = 2 Тогда //Агент СОД
		Слои = "Обмен, ОбменСОД";
	Иначе
		Слои = "Обмен, ОбменаНет";		
	КонецЕсли;
	
	Форма.ИспользоватьСлой(Слои + ",Общие", 2);
	
КонецПроцедуры  

//******************************************************************************
Процедура списВидДокВыбор()
	ТекВидДок = списВидДок.ПолучитьЗначение(списВидДок.ТекущаяСтрока());
	НазвСлоя = "Док" + ТекВидДок;
	Форма.ИспользоватьСлой("Документы,Общие,"+НазвСлоя, 2);
КонецПроцедуры //списВидДокВыбор()    

//******************************************************************************
Процедура флПриИзмененииКолКода()
	               
	Форма.НадписьКоды.Видимость(ОтображатьКодТовара); 
	Форма.СписКодыТоваров.Видимость(ОтображатьКодТовара);	
	
КонецПроцедуры                 

//******************************************************************************
Процедура флПриИзмененииРаботаСМаршрутами()
	
	Форма.ПриоритетПоМаршрутам.Видимость(РаботаСМаршрутами);	
	
КонецПроцедуры    

//******************************************************************************
Процедура флПриИзмененииМнУчет()
	               
	Форма.СкрыватьСписокСкладов.Видимость(?(ОтображатьКодТовара=1, 0, 1));
	
КонецПроцедуры                                                           

//******************************************************************************
Процедура ПрочитатьНастройкуЖурнала()  
	               
	СписЖрнЗакладки.УдалитьВсе();
	
	СписЖрнЗакладки.ДобавитьЗначение("Заказы",		 "Заказы");
	СписЖрнЗакладки.ДобавитьЗначение("Касса",		 "Касса");		
	СписЖрнЗакладки.ДобавитьЗначение("Долги",		 "Долги");		 	
	СписЖрнЗакладки.ДобавитьЗначение("Мерчендайзинг","Мерчендайзинг");		
	
	Если ПустаяСтрока(НастройкаЖурнала) = 0 Тогда  		
		Стр = СокрЛП(НастройкаЖурнала) + ",";		
		Поз = Найти(Стр, ",");           
		
		Пока Поз > 0 Цикл  			
			СтрДок = Сред(Стр, 1, Поз-1); 
			ПозСписка = СписЖрнЗакладки.НайтиЗначение(СтрДок);
			Если ПозСписка > 0 Тогда
				СписЖрнЗакладки.Пометка(ПозСписка, 1);			    
			КонецЕсли;			
			Стр = Сред(Стр, Поз + 1); 			
			Поз = Найти(Стр, ",");  						
		КонецЦикла;  		
	КонецЕсли;
		
КонецПроцедуры   

//******************************************************************************
Процедура СохранитьНастройкуЖурнала()   
	
	Стр = ""; 
	РазмСпис = СписЖрнЗакладки.РазмерСписка();
	
	Если РазмСпис = 0 Тогда
		НастройкаЖурнала = "";
	КонецЕсли;
	
	Для Поз = 1 По РазмСпис Цикл 		
		ТекЖурнал = СписЖрнЗакладки.ПолучитьЗначение(Поз,);
		Если СписЖрнЗакладки.Пометка(Поз) = 1 Тогда
			Стр = Стр + Строка(ТекЖурнал) + ",";				    
		КонецЕсли;		
	КонецЦикла;                                
	
	Стр = Лев(Стр, СтрДлина(Стр) - 1); // Убрали последнюю запятую
	
	НастройкаЖурнала = Стр;

КонецПроцедуры

//******************************************************************************
Процедура ЖурналВыбратьЗакладки(Режим)
	Для Поз = 1  По СписЖрнЗакладки.РазмерСписка() Цикл
		СписЖрнЗакладки.Пометка(Поз,Режим);
	КонецЦикла;
КонецПроцедуры

//******************************************************************************
Процедура ЖурналСдвинутьЗакладку(Сдвиг)
	РазмСпис = СписЖрнЗакладки.РазмерСписка();
	ТекСтр 	 = СписЖрнЗакладки.ТекущаяСтрока();
	Если ТекСтр = 0 Тогда
	    Возврат;
	КонецЕсли;
	НоваяПоз = ТекСтр + Сдвиг;
	Если (НоваяПоз < 1) Или (НоваяПоз > РазмСпис) Тогда
		Возврат;
	КонецЕсли;
	СписЖрнЗакладки.СдвинутьЗначение(Сдвиг, ТекСтр);
	СписЖрнЗакладки.ТекущаяСтрока(НоваяПоз);
КонецПроцедуры    

//******************************************************************************
Процедура кнЖурналПорядокПоУмолчВыбор()
	//ОбновитьВыборЗакладок();
КонецПроцедуры

//******************************************************************************
Процедура ПриВыбореЗакладки(НомЗак, ЗначЗак)
	
	Если НомЗак = 0 Тогда
	    НомЗак = Форма.Закладки.НайтиЗначение(ЗначЗак);
		Если НомЗак = 0 Тогда
		    НомЗак = 1;
			ЗначЗак = Форма.Закладки.ПолучитьЗначение(НомЗак);
		КонецЕсли;
		Форма.Закладки.ТекущаяСтрока(НомЗак);
	КонецЕсли;

	Если ЗначЗак = "Основные" Тогда			
		Форма.ИспользоватьСлой(ЗначЗак + ",Общие", 2);
		Форма.СкрыватьСписокСкладов.Видимость(?(ИспользоватьМногоскладскойУчет=1, 0, 1));
	ИначеЕсли ЗначЗак = "Справочники" Тогда			
		Форма.ИспользоватьСлой(ЗначЗак + ",Общие", 2);
		флПриИзмененииКолКода();                                        		
	ИначеЕсли ЗначЗак = "Документы" Тогда
		НазвСлоя = "Док" + ТекВидДок;
		Форма.ИспользоватьСлой(ЗначЗак + ",Общие," + НазвСлоя, 2);
		флПриИзмененииРаботаСМаршрутами();
	Иначе
		Форма.ИспользоватьСлой(ЗначЗак + ",Общие", 2);					
	КонецЕсли; 
	
КонецПроцедуры // ПриВыбореЗакладки()   

//******************************************************************************
Процедура ПриОткрытии()
	     
	Если ПустоеЗначение(Форма.Параметр) = 1 Тогда
		Предупреждение("Редактирование элементов справочника производится через форму справочника ""Агенты""!");		
		СтатусВозврата(0);	                                                                             	    
	КонецЕсли;          
	
	списВидДок.ДобавитьЗначение("Заказ"); 
	списВидДок.ДобавитьЗначение("Касса");
	списВидДок.ТекущаяСтрока(1);
	
	ТекВидДок = списВидДок.ПолучитьЗначение(списВидДок.ТекущаяСтрока()); //текущий вид документа в закладке "Документы"
	
	Форма.ИспользоватьЗакладки(1);	
	Форма.Закладки.ДобавитьЗначение("Обмен",	  "Обмен данными");	
	Форма.Закладки.ДобавитьЗначение("Основные",	  "Основные"); 
	Форма.Закладки.ДобавитьЗначение("Журнал",     "Журнал");
	Форма.Закладки.ДобавитьЗначение("Справочники","Справочники");
	Форма.Закладки.ДобавитьЗначение("Документы",  "Документы");      
	
	списСпособОбмена.ДобавитьЗначение(1, "Прямое подключение");
	списСпособОбмена.ДобавитьЗначение(2, "Агент Плюс СОД");
	списСпособОбмена.ТекущаяСтрока(1);
	
	СписКодыТоваров.ДобавитьЗначение(1,"Отображать код");
	СписКодыТоваров.ДобавитьЗначение(2,"Отображать артикул");	
	СписКодыТоваров.ТекущаяСтрока(1); 
	
	ПрочитатьНастройкуЖурнала();   
	
	Поз = СписКодыТоваров.НайтиЗначение(РежимОтображенияКода);
	Если Поз = 0 Тогда
	    Поз = 1;
	КонецЕсли;
	СписКодыТоваров.ТекущаяСтрока(Поз);
	
	ПриВыбореЗакладки(0, "Общие");	
	
	Поз = списСпособОбмена.НайтиЗначение(СпособОбмена);
	Если Поз <> 0 Тогда
	    списСпособОбмена.ТекущаяСтрока(Поз);
		списСпособОбменаВыбор();
	КонецЕсли;	
	
КонецПроцедуры //ПриОткрытии    

//******************************************************************************
Процедура ПриЗаписи()  
	
	Цифры = "1234567890";   
	
	Ошибка = 0;
	
	Для Поз = 1 По СтрДлина(Порт) Цикл		
		лСимв = Сред(Порт, Поз, 1);
	    Если Найти(Цифры, Нрег(лСимв)) = 0 Тогда
	    	Ошибка = 1;
			Прервать;
	    КонецЕсли;
	КонецЦикла;
	
	Если Ошибка = 1 Тогда
	    Сообщить("В качестве порта для службы ""Агент Плюс СОД"" следует указывать цифры!", "!");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;    
	
	РежимОтображенияКода = СписКодыТоваров.ПолучитьЗначение(СписКодыТоваров.ТекущаяСтрока());      
	
	СохранитьНастройкуЖурнала();
	
	Наименование = "Основная"; 
	
КонецПроцедуры  
