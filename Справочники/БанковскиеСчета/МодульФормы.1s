// Сведения о банковском счете фирмы заносятся в реквизиты справочника
// оканчивающиеся на "...Банка".
// В свою очередь, банк фирмы может являться клиентом Банка Росси напрямую
// или быть клиентом третьего, расчетного банка. Реквизиты третьего банка, 
// если такой существует, заносятся в реквизиты справочника с окончанием 
// "...БанкаРасчетного".
// Кроме этого, признаком наличия третьего банка является установка реквизита
// ПрямыеРасчеты в 0.
// Реквизиты справочника используются следующим образом (на примере документа,
// к которому неравнодушен любой бухгалтер, - платежного поручения):
// - если признак ПрямыеРасчеты установлен в 1, в поле "Банк плательщика" платежного
//   поручения заносится содержимое реквизитов, оканчивающихся на "...Банка",
//   независимо от наличия информации в реквизитах "...БанкаРасчетного";
// - если признак ПрямыеРасчеты установлен в 0, в поле "Банк плательщика" платежного
//   поручения заносится содержимое реквизитов, оканчивающихся на "...БанкаРасчетного".
//   Сведения о банке фирмы записываются вместе с наименованием фирмы в поле
//   "Плательщик" платежного поручения.
// В других документах банковские реквизиты фирмы отображаются аналогичным образом.

Перем ИнформационнаяСтрока;
Перем ВводНового;
Перем ВнКонтекст;
Перем Записали;

//***** 
//
// Возвращаемое значение:
//  0 - если все нормально
//  не 0 - если счет указан неверно.
//
// Функция проверяет правильность указания номера расчетного счета
// по алгоритму проверки правильности расчета контрольного ключа.
// 
Функция ПроверкаСчета( Номер )
	Перем Поз; // счетчик цикла

	// Весовые коэффициенты
	Вес="71371371371371371371371";
		
	КонтрСумма=0;
	
	Для Поз=1 По 23 Цикл
		Произв=Число(Сред(Номер, Поз, 1))*Число(Сред(Вес, Поз, 1));
		МладшийРазряд=Число(Прав(Строка(Произв),1));
		КонтрСумма=КонтрСумма+МладшийРазряд;
	КонецЦикла;
	
	Проверка=КонтрСумма/10-Цел(КонтрСумма/10);
	
	Возврат Проверка;
	
КонецФункции

//*****
//
// Описание:
//  Процедура предназначена для проверки правильности указания номеров расчетного
// и корреспондентских счетов, так как вероятность ошибки при вводе 20-разрядного
// номера велика, и такая проверка будет удобным дополнительным сервисом.
//  Правильность указания банковских реквизитов проверяется по следующим критериям:
//  - длина номера счета должна быть 20 символов;
//  - длина БИК должна быть 9 символов;
//  - БИК должен быть числом (проверять номер счета на число нельзя, так как в случае
//    использования клиринговой валюты в 6-м разряде номера может стоять символ
//    (хотя это редкий случай));
//  - номера счетов и БИК совместно проверяются по алгоритму проверки правильности 
//    расчета контрольного ключа.
//  Если какое-либо из этих условий не выполняется, выдается сообщение в окно сообщений,
// но информация о расчетном счете все равно записывается в справочник.
Процедура КонтрольСчета()
	ПроверятьКлючРасчетногоСчета=Да;
	ПроверятьКлючКоррСчета=Да;
	
    // Проверяем номер счета и реквизиты банка организации
	
	// Длина номера счета должна быть равна 20
	Если ПустаяСтрока( Номер ) = 0 Тогда
		Если СтрДлина( СокрЛП( Номер ) ) <> 20 Тогда
		    Сообщить( "Возможно, номер расчетного счета указан неверно.", "!" );
		    ПроверятьКлючРасчетногоСчета=Нет;
		КонецЕсли;
	Иначе
		ПроверятьКлючРасчетногоСчета=Нет;
	КонецЕсли;
	
	// Длина номера корр. счета должна быть равна 20
	Если ПустаяСтрока( КоррСчетБанка ) = 0 Тогда
		Если СтрДлина( СокрЛП( КоррСчетБанка ) ) <> 20 Тогда
		    Сообщить( "Возможно, номер корсчета банка указан неверно.", "!" );
		    ПроверятьКлючКоррСчета=Нет;
		КонецЕсли;
	Иначе
		ПроверятьКлючКоррСчета=Нет;
	КонецЕсли;
	
	// Если указан БИК, начинаем проверять его
	Если ПустаяСтрока( БИКБанка ) = 0 Тогда
		ВыдатьСообщение=Нет;
		
		// Длина БИК должна быть равна 9
		Если СтрДлина( СокрЛП( БИКБанка ) ) <> 9 Тогда
			ВыдатьСообщение=Да;
		КонецЕсли;
	
		// БИК должен быть числом
		Если Число( БИКБанка ) = 0 Тогда
			ВыдатьСообщение=Да;
		КонецЕсли;
		
		Если ВыдатьСообщение=Да Тогда
	    	Сообщить( "Возможно, БИК указан неверно.", "!" );
	    	ПроверятьКлючРасчетногоСчета=Нет;
	    	ПроверятьКлючКоррСчета=Нет;
		КонецЕсли;
	Иначе
		ПроверятьКлючРасчетногоСчета=Нет;
		ПроверятьКлючКоррСчета=Нет;
	КонецЕсли;
	
	
	Если ПроверятьКлючРасчетногоСчета=Да Тогда
		
		// Для проверки к номеру счета слева добавляются 3 последние цифры БИК
		Если ПроверкаСчета( Прав( БИКБанка, 3 )+Номер ) <> 0 Тогда
		    Сообщить( "Возможно, номер расчетного счета или БИК указаны неверно.", "!" );
		КонецЕсли;
	    
	КонецЕсли;
	
	Если ПроверятьКлючКоррСчета=Да Тогда
		
		// Для проверки к номеру корреспондентского счета слева добавляются 0
		// и 5-й и 6-й разряды БИК
		Если ПроверкаСчета( "0"+ Сред( БИКБанка, 5, 2 ) + КоррСчетБанка ) <> 0 Тогда
		    Сообщить( "Возможно, номер корсчета или БИК указан неверно.", "!" );
		КонецЕсли;

	КонецЕсли;
	
	Если ПрямыеРасчеты=0 Тогда
		ПроверятьКлючКоррСчетаБанкаРасчетного=Да;

		// Длина номера корр. счета расчетного банка должна быть равна 20
		Если ПустаяСтрока( КоррСчетБанкаРасчетного ) = 0 Тогда
			Если СтрДлина( КоррСчетБанкаРасчетного ) < 20 Тогда
			    Сообщить( "Возможно, номер корсчета расчетного банка указан неверно.", "!" );
			    ПроверятьКлючКоррСчетаБанкаРасчетного=Нет;
			КонецЕсли;
		КонецЕсли;

		
		// Если указан БИК расчетного банка, начинаем проверять его
		Если ПустаяСтрока( БИКБанкаРасчетного ) = 0 Тогда
			ВыдатьСообщение=Нет;
			
			Если СтрДлина( БИКБанкаРасчетного ) <> 9 Тогда
				ВыдатьСообщение=Да;
			КонецЕсли;
		
			// Номер корр. счета должен быть числом
			Если Число( БИКБанкаРасчетного ) = 0 Тогда
   				ВыдатьСообщение=Да;
			КонецЕсли;
   			
			Если ВыдатьСообщение=Да Тогда
                Сообщить( "Возможно, БИК расчетного банка указан неверно.", "!" );
			    ПроверятьКлючКоррСчетаБанкаРасчетного=Нет;                
			КонецЕсли;
		Иначе
		   	ПроверятьКлючКоррСчетаБанкаРасчетного=Нет;                
		КонецЕсли;
		
		Если ПроверятьКлючКоррСчетаБанкаРасчетного=Да Тогда
			
			// Для проверки к номеру корреспондентского счета слева добавляются 0
			// и 5-й и 6-й разряды БИК
			Если ПроверкаСчета( "0"+ Сред( БИКБанкаРасчетного, 5, 2 ) + БИКБанкаРасчетного ) <> 0 Тогда
			    Сообщить( "Возможно, номер корсчета расчетного банка указан неверно.", "!" );
			КонецЕсли;
	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // КонтрольСчета

//*****
Процедура ВключитьРеквизитыРасчетногоБанка( ФлагДоступности )
	
	Форма.Текст_НаименованиеРасчетногоБанка.Доступность( ФлагДоступности );
	Форма.БанкРасчетный.Доступность( ФлагДоступности );
	Форма.Текст_ГородБанкаРасчетного.Доступность( ФлагДоступности );
	Форма.ГородБанкаРасчетного.Доступность( ФлагДоступности );
	Форма.Текст_КоррСчетБанкаРасчетного.Доступность( ФлагДоступности );
	Форма.КоррСчетБанкаРасчетного.Доступность( ФлагДоступности );
	Форма.Текст_БИКБанкаРасчетного.Доступность( ФлагДоступности );
	Форма.БИКБанкаРасчетного.Доступность( ФлагДоступности );

КонецПроцедуры // ВключитьРеквизитыРасчетногоБанка

//*****
//
// Вызывается из:
//  реквизит ДатаЗакрытияСчета
//

Процедура ПриУстановкеДатыЗакрытияСчета()
	ИнформационнаяСтрока=?(Число(ДатаЗакрытияСчета) > 0,"Счет закрыт","")
КонецПроцедуры

//*****
//
// Вызывается из:
//  реквизит диалога ФлагРасч ("Прямые расчеты")
//
Процедура УстановкаРасчетов();
	ПрямыеРасчеты=ФлагРасч;
	ВключитьРеквизитыРасчетногоБанка( ?( ПрямыеРасчеты=0, 1, 0 ) );
КонецПроцедуры


//*****
Процедура ПриВыбореТипаСчета()
	Если ТипСчета=Перечисление.ТипыБанковскихСчетов.Валютный Тогда
	    Форма.Текст_НомерИДатаРазрешения.Доступность( 1 );
	    Форма.НомерИДатаРазрешения.Доступность( 1 );
	Иначе
	    Форма.Текст_НомерИДатаРазрешения.Доступность( 0 );
	    Форма.НомерИДатаРазрешения.Доступность( 0 );
	КонецЕсли;
КонецПроцедуры

//*****
Процедура ПриВыбореЗакладки(Ном, Значен );
	НомерЗакладки=Значен;
	Если Значен=1 Тогда
		Форма.ИспользоватьСлой("Основной,Счет",2);
	ИначеЕсли Значен=2 Тогда
		Форма.ИспользоватьСлой("Основной,Доп",2);
	КонецЕсли;
КонецПроцедуры 

//*****
Процедура ВводНового();
	Если ПустаяСтрока(Наименование) = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	ТипСчета=Перечисление.ТипыБанковскихСчетов.Рублевый;
	ВидСчета="Расчетный (текущий)";
	ДатаОткрытияСчета=РабочаяДата();
	Наименование="Основной";
    ПрямыеРасчеты=1;
    
    ВводНового=1;
    
КонецПроцедуры // ВводНового

//*****
Процедура ПриОткрытии();
	Если глФлагРасшифровки=1 Тогда
	    ВнКонтекст=глРасшифровка;
	КонецЕсли;

	Форма.КнопкаПоУмолчанию("ОК");
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение(1,"Счет");
	Форма.Закладки.ДобавитьЗначение(2,"Дополнительные");
    
	ФлагРасч=ПрямыеРасчеты;
	ВключитьРеквизитыРасчетногоБанка( ?( ПрямыеРасчеты=0, 1, 0 ) );
	
	Если ТипСчета=Перечисление.ТипыБанковскихСчетов.Валютный Тогда
	    Форма.Текст_НомерИДатаРазрешения.Доступность( 1 );
	    Форма.НомерИДатаРазрешения.Доступность( 1 );
	КонецЕсли;

	Форма.ИспользоватьСлой("Основной,Счет",2);
	ИнформационнаяСтрока=?(Число( ДатаЗакрытияСчета )>0,"Счет закрыт","")
	
КонецПроцедуры

//*****
//Начало текста, вставленного Инсталлятором МОД
Процедура ПриЗаписи() Далее
//Конец текста, вставленного Инсталлятором МОД
Процедура стар_ПриЗаписи()
//Закомментировано Инсталлятором МОД:Процедура ПриЗаписи()
	ПрямыеРасчет=ФлагРасч;
	Записали=1;
КонецПроцедуры
//Начало текста, вставленного Инсталлятором МОД
Процедура ПриЗаписи()
	//Внимание: данная процедура при обновлении Менеджера будет перезаписана заново
	//без сохранения текущего кода !!!
	//Рекомендуем собственные алгоритмы записывать в старую процедуру с префиксом стар_
	ПриИзмененииОбъекта(Контекст, ТекущийЭлемент(), РабочаяДата());
	стар_ПриЗаписи();
КонецПроцедуры
//Конец текста, вставленного Инсталлятором МОД


//*****
Процедура ПриЗакрытии()
	Если ТипЗначенияСтр(ВнКонтекст)="ГрупповойКонтекст" Тогда
		Если Записали = 1 Тогда
			Если ВводНового=1 Тогда
				ВнКонтекст.СписокСчетов.ДобавитьЗначение(ТекущийЭлемент(),СокрП(ВидСчета)+" счет №"+СокрЛП(Номер)+" в "+СокрЛП(Банк));
				ВнКонтекст.СписокСчетов.ТекущаяСтрока(ВнКонтекст.СписокСчетов.РазмерСписка());
				ВнКонтекст.Форма.КнИзменить.Доступность(1);
				ВнКонтекст.Форма.КнУдалить.Доступность(1);
			Иначе                                  
				Поз=ВнКонтекст.СписокСчетов.НайтиЗначение(ТекущийЭлемент());
				ВнКонтекст.СписокСчетов.УдалитьЗначение(Поз);
				ВнКонтекст.СписокСчетов.ВставитьЗначение(Поз,ТекущийЭлемент(),СокрП(ВидСчета)+" счет №"+СокрЛП(Номер)+" в "+СокрЛП(Банк));
				ВнКонтекст.СписокСчетов.ТекущаяСтрока(Поз);
			КонецЕсли;
			ВнКонтекст.Форма.Обновить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
