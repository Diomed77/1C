Перем ИзмененыСклады;                                                           

Процедура ПриОткрытии()
	//Заполняем списки согласно значению реквизита "Склады"
	СписокСкладов.УдалитьВсе();
	СписокСвободныхСкладов.УдалитьВсе();
	Спр=СоздатьОбъект("Справочник.Склады");
	Стр=СокрЛП(Склады)+",";
	Если Стр<>"," Тогда
		Пока Стр<>"" Цикл
			Поз  = Найти(Стр,",");
			Код1 = Лев(Стр,Поз-1);
			Стр  = Прав(Стр,СтрДлина(Стр)-Поз);
			Спр.НайтиПоКоду(Код1);
			Если Спр.Выбран()=0 Тогда
				Сообщить("Не найден склад с кодом "+Строка(Код1)+" !");
			Иначе
				СписокСкладов.ДобавитьЗначение(
				Спр.Код,Строка(Спр.Код)+". "+СокрП(Спр.Наименование));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//формируем второй список
	СписокСвободныхСкладов.УдалитьВсе();
	Спр.ПорядокКодов();
	Спр.ВыбратьЭлементы();
	Пока Спр.ПолучитьЭлемент()=1  Цикл
		Если Спр.ПометкаУдаления()=1 Тогда
			Продолжить;
		КонецЕсли;
		Если СписокСкладов.НайтиЗначение(Спр.Код)=0
		Тогда
			СписокСвободныхСкладов.ДобавитьЗначение(
			Спр.Код,Строка(Спр.Код)+". "+СокрП(Спр.Наименование));
		КонецЕсли;
	КонецЦикла;
	СписокСкладов.ТекущаяСтрока(1);
	СписокСвободныхСкладов.ТекущаяСтрока(1);        
КонецПроцедуры //ПриОткрытии()

Процедура ДобавитьСкл()
	Если СписокСвободныхСкладов.РазмерСписка()=0 Тогда
		Возврат;
	КонецЕсли;

	Поз=СписокСвободныхСкладов.ТекущаяСтрока();
	Если Поз<1 Тогда
	    Поз=1;
	КонецЕсли;
	Стр="";
	Выб=СписокСвободныхСкладов.ПолучитьЗначение(Поз,Стр);
    СписокСкладов.ДобавитьЗначение(Выб,Стр);
    СписокСвободныхСкладов.УдалитьЗначение(Поз);
    Пром=СписокСвободныхСкладов.РазмерСписка();
    Если Пром>=Поз  Тогда
        СписокСвободныхСкладов.ТекущаяСтрока(Поз);
    ИначеЕсли Пром<>0 Тогда
    	СписокСвободныхСкладов.ТекущаяСтрока(Пром);
    КонецЕсли;
    Поз=СписокСкладов.ТекущаяСтрока();
    Если Поз<1 Тогда
    	Поз=1;
    КонецЕсли;
    СписокСкладов.ТекущаяСтрока(Поз);
    ИзмененыСклады = 1;
КонецПроцедуры //ДобавитьСкл()

Процедура УдалитьСкл()	
	Если СписокСкладов.РазмерСписка() = 0 Тогда
		Возврат;
	КонецЕсли;
	Поз = СписокСкладов.ТекущаяСтрока();
	Если Поз<1 Тогда
	    Поз = 1;
	КонецЕсли;
	Стр = "";
	Выб = СписокСкладов.ПолучитьЗначение(Поз,Стр);
    СписокСвободныхСкладов.ДобавитьЗначение(Выб,Стр);
    СписокСвободныхСкладов.Сортировать();
    СписокСкладов.УдалитьЗначение(Поз);

    Пром = СписокСкладов.РазмерСписка();
    Если Пром>=Поз  Тогда
        СписокСкладов.ТекущаяСтрока(Поз);
    ИначеЕсли Пром<>0 Тогда
    	СписокСкладов.ТекущаяСтрока(Пром);
	КонецЕсли;       
	
	ИзмененыСклады = 1;  	
КонецПроцедуры //УдалитьСкл()

Процедура ПриЗаписи()     	
	Если ИзмененыСклады = 1 Тогда
		
		СправочникСкладов = СоздатьОбъект("Справочник.Склады");
		
		Стр=""; 
		РазмСпис = СписокСкладов.РазмерСписка();
		
		Если РазмСпис=0 Тогда
		    Склад=0;
		КонецЕсли;
		
		Для Поз = 1 По РазмСпис Цикл
			КодСкл = СписокСкладов.ПолучитьЗначение(Поз,);
			Стр    = Стр + Строка(КодСкл) + ?(Поз<РазмСпис,",","");
			Если Поз = 1 Тогда
			    СправочникСкладов.НайтиПоКоду(КодСкл);
			    Склад = СправочникСкладов.ТекущийЭлемент();
			КонецЕсли;
		КонецЦикла;
		Склады = Стр;
	КонецЕсли;        	
КонецПроцедуры //ПриЗаписи()          


