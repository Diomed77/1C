Перем КаталогКартинок;
Перем ПрежнийЭл;

//*****************************************************************************
Процедура ВыбДокВыбор()

	Если ВыбДок.Выбран() = 1 Тогда
		УстановитьОтбор("Док", ВыбДок);
	Иначе
		УстановитьОтбор("Док", 0);
	КонецЕсли;
	
КонецПроцедуры

//*****************************************************************************
Процедура кнПросмотрВыбор()
	
	ТекЭл = ТекущийЭлемент();

	Если ТекЭл.Выбран() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ПутьИФайл = "";
	Если ПустоеЗначение(ТекЭл.ФайлЦБД) = 0 Тогда
		ПутьИФайл = КаталогКартинок + СокрЛП(ТекЭл.ФайлЦБД);
		Если ФС.СуществуетФайл(ПутьИФайл) = 0 Тогда
			ПутьИФайл = "";
		КонецЕсли;
	КонецЕсли;                          
	
	Если ПустаяСтрока(ПутьИФайл) = 1 Тогда
		Предупреждение("Файл фотографии не существует!", 60);
		Возврат;
	КонецЕсли;
	
	Попытка		
		ЗапуститьПриложение(ПутьИФайл); 		
	Исключение
		Предупреждение("Не удалось запустить приложение для просмотра фотографии!", 60);		
	КонецПопытки;
	
КонецПроцедуры    

//*****************************************************************************
Процедура кнСохранитьНажатие()
	
	ТекЭл = ТекущийЭлемент();
	
	Если ТекЭл.Выбран() = 1 Тогда
		
		Если ПустоеЗначение(ТекЭл.ФайлЦБД) = 0 Тогда
			
			ПутьИФайл = КаталогКартинок + СокрЛП(ТекЭл.ФайлЦБД);
			    
			Если (ПустаяСтрока(ПутьИФайл) = 1) или (ФС.СуществуетФайл(ПутьИФайл) = 0) Тогда
				Предупреждение("Файл фотографии не существует!", 60);
				Возврат;
			КонецЕсли;
			
			ВыбКаталог = ВосстановитьЗначение("КаталогСохраненияФото");
			
			ВыбИмяФайла = ТекЭл.ФайлЦБД;
			
			Результат = ФС.ВыбратьФайл(1, ВыбИмяФайла, ВыбКаталог, "Сохранение фотографии", "Все файлы изображений (*.*) |*.*");
			
			Если Результат = 1 Тогда
				
				СохранитьЗначение("КаталогСохраненияФото", ВыбКаталог);   
				
				ИмяСохрФайла = ВыбКаталог + ВыбИмяФайла;
				
				Если ФС.СуществуетФайл(ИмяСохрФайла) = 1 Тогда
					Предупреждение("Файл с таким именем уже существует. Задайте другое имя файла!", 60);					
					Возврат;
				КонецЕсли;
				
				Попытка
					ФС.КопироватьФайл(ПутьИФайл, ИмяСохрФайла, 0);
				Исключение   
					Предупреждение("Не удалось сохранить файл по указанному пути!", 60);
				КонецПопытки;
				
			КонецЕсли;			
			
		КонецЕсли;      
		
	КонецЕсли;    
	
КонецПроцедуры 

//*****************************************************************************
Процедура кнПапкаФотоНажатие()
	                    
	ВыбКаталог = ВосстановитьЗначение("КаталогСохраненияФото");
	           
	Если ПустоеЗначение(ВыбКаталог) = 1 Тогда
		Результат = ФС.ВыбратьКаталог(ВыбКаталог, "Каталог сохранения фотографий", 60);
		Если Результат = 1 Тогда
			СохранитьЗначение("КаталогСохраненияФото", ВыбКаталог);
			ЗапуститьПриложение(ВыбКаталог);
		КонецЕсли;		
	Иначе
		ЗапуститьПриложение(ВыбКаталог);
	КонецЕсли;
	
КонецПроцедуры 

//*****************************************************************************
Функция ПриОбновленииФормы()
	
	ТекЭл = ТекущийЭлемент();
	
	Если ТекЭл = ПрежнийЭл Тогда
		Возврат "";
	КонецЕсли;	
	
	ПрежнийЭл = ТекЭл;
	ПутьИФайл = "";
	
	Если ТекЭл.Выбран() = 1 Тогда
		Если ПустоеЗначение(ТекЭл.ФайлЦБД) = 0 Тогда
			ПутьИФайл = КаталогКартинок + СокрЛП(ТекЭл.ФайлЦБД);
		КонецЕсли;
	КонецЕсли;  	
	
	глАПЗагрузитьКартинку(Фото, ПутьИФайл);
	                                          	
	Возврат "";
	
КонецФункции

//*****************************************************************************
Процедура ПриОткрытии()
	
	КаталогКартинок = ВернутьКаталогФото();
   
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		ВыбДок = Форма.Параметр;
		ВыбДокВыбор();
	КонецЕсли;  
	
КонецПроцедуры                       