Перем Таб;
Перем ВидДок;
 //-----------------------------------------------
Функция ВсегоСумма()
	Если  ТекущийДокумент.Выбран()>0  Тогда 
		Если ТекущийДокумент.Вид()="РегистрацияСчета_фактуры" Тогда
			Возврат СокрЛ(Формат(ТекущийДокумент.Сумма,"Ч15.2")); 
		ИначеЕсли ТекущийДокумент.Вид()="ЗаписьКнигиПокупок" Тогда
			Возврат ФРМ(ТекущийДокумент.Всего,ТекущийДокумент.Валюта,0);
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции      
//------------------------
Процедура Печать2()
	Если ВыбФирма.Выбран()=0 Тогда
		Предупреждение("Выберите фирму для формирования Книги покупок!");
	Конецесли;
    // Здесь формируется отчет, который использует регистры, критичные к 
    // последовательности проведения документов
    // поэтому сравним установленные даты периода формируемого отчета с ГП 
    ПроверкаАктуальностиОтчета(ДатаНачала,ДатаКонца,"Фин",ВыбФирма);

    // Сделаем это при помощи механизма Запросов
	Запрос=СоздатьОбъект("Запрос");

	Если ДатаКонца>=ПолучитьДатуТА() Тогда
		ДатаКон=0; 
		ДатаКонца=ПолучитьДатуТА(); 
	Иначе
		ДатаКон=ДатаКонца; 
	КонецЕсли;

	Заг="";
	ТекстЗапроса="
	|ПЕРИОД С ДатаНачала";

	Если Не(ДатаКон=0) Тогда
		ТекстЗапроса=ТекстЗапроса+" По ДатаКон;";
	Иначе
		ТекстЗапроса=ТекстЗапроса+";";
	КонецЕсли;

	ТекстЗапроса=ТекстЗапроса+"
	|Фирма=Регистр.ВзаиморасчетыПоставщиков.Фирма;
	|КредДокумент=Регистр.ВзаиморасчетыПоставщиков.КредДокумент;
	|Докум=Регистр.ВзаиморасчетыПоставщиков.ТекущийДокумент;
	|Долг=Регистр.ВзаиморасчетыПоставщиков.Долг;
	|КодОперации=Регистр.ВзаиморасчетыПоставщиков.КодОперации;
	|Условие(Фирма=ВыбФирма);
	|Группировка КредДокумент;
	|Группировка Докум;
	|Условие (КодОперации<>ВозвратОплаты);
	|Условие (КодОперации<>ВозвратПоставщикуНеоплаченногоТовара);
	|Функция КонДолг= КонОст(Долг);
	|Функция ПрихДолг= Приход(Долг);
	|Функция РасхДолг= Расход(Долг);
	|";  
	Флаг=Запрос.Выполнить(ТекстЗапроса);
	Если Флаг=0 тогда
		Предупреждение("Запрос по Регистру ВзаиморасчетыПоставщиков не выполнился!");
		Возврат;
	КонецЕсли;
	// теперь в запросе собраны все Документы по ВзаиморасчетамПоставщиков

	//  Создание Таблицы для выходного отчета
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("КнигаПокупок");
	ДокПодч=СоздатьОбъект("Документ");

	ИтогоДолгКлиента=0;
	ИтогоНашДолг=0;
	ЧислоСтрок=0;
	Таб.ВывестиСекцию("Отчет");
	Оживить(11);
	ВВ=Рубли;

	ИтогВсего=0;
	ИтогСуммаБезНДС20=0;
	ИтогСуммаБезНДС10=0;
	ИтогНДС20=0;
	ИтогНДС10=0;
	ИтогСуммаСовсемБезНДС=0;
	ПорНомер=0;
    // теперь пройдемся по КредДокумент и пропишем все основательно 
	Пока Запрос.Группировка("КредДокумент") = 1 Цикл 
		ДокКред=Запрос.КредДокумент;
		// пропускаем пустые
		Если ДокКред.Выбран()=0 Тогда
			Продолжить;
		КонецЕсли;

		ВидКредДок=ДокКред.Вид();

		ДокСФ="";
		ВыбКлиент=ДокКред.Клиент;

		Если (ВидКредДок="ВводОстатковКредита") Тогда
			КредЗнакВводОстатковКредита=ЗнакУчета(ДокКред);
		Иначе
			КредЗнакВводОстатковКредита=0;
		КонецЕсли;
				
		Если 	(ВидКредДок="ПриходнаяНакладная") 
			ИЛИ (ВидКредДок="РасходнаяНакладная") 
			ИЛИ ((ВидКредДок="ВводОстатковКредита") И (КредЗнакВводОстатковКредита=-1)) Тогда
		// Для ПрихНакладных должна существовать РегистрацияСчета_фактуры
			ДокПодч.ВыбратьПодчиненныеДокументы(ДокКред.ДатаДок-7,ДатаКонца,ДокКред); 
			Пока   ДокПодч.ПолучитьДокумент()=1 Цикл
				Если ДокПодч.Вид()="РегистрацияСчета_фактуры" Тогда
					Если ДокПодч.Проведен()=0 Тогда
						Продолжить;
					КонецЕсли;
					ДокСФ=ДокПодч.ТекущийДокумент();
				    Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ДокСФ="" Тогда
				Сообщить("На приходный документ "+ДокКред+"; не зарегистрирована РегистрацияСчета_фактуры!");
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		// Итак здесь мы имеем документ прихода товара
		// Далее надо зарегистрировать в Книге покупок
		// проплаты по этому ДокКред

  //------------
		Пока Запрос.Группировка("Докум") = 1 Цикл 
			
			ДокОплаты="Нет документа оплаты";
			ДокОтгрузки="Нет документа отгрузки";
		
			Док=Запрос.Докум;
			ВидДок=Док.Вид(); 
			Если Док.Выбран()=0 Тогда
			// непонятно что пропускаем
				Продолжить;
			КонецЕсли;

			// Сам ДокКред мы пропускаем
			Если Док=ДокКред Тогда
				Продолжить;
			КонецЕсли;
			
			Если (ВидКредДок="ВводОстатковКредита") Тогда
				ЗнакВводОстатковКредита=ЗнакУчета(ДокКред);
			Иначе
				ЗнакВводОстатковКредита=0;
			КонецЕсли;

			Если НЕ((ВидКредДок="ПриходнаяНакладная") 
				ИЛИ (ВидКредДок="РасходнаяНакладная") 
				ИЛИ ((ВидКредДок="ВводОстатковКредита") И (КредЗнакВводОстатковКредита=-1))) Тогда
			// значит мы имеем авансовую проплату, которая погашается ПрихНакладными
				Если (ВидДок="ПриходнаяНакладная") 
					ИЛИ (ВидДок="РасходнаяНакладная") 
					ИЛИ ((ВидДок="ВводОстатковКредита") И (ЗнакВводОстатковКредита=-1)) Тогда
				// Для ПрихНакладных должна существовать Счет-Фактура
					ДокПодч.ВыбратьПодчиненныеДокументы(Док.ДатаДок-7,ДатаКонца,Док); 
					Пока   ДокПодч.ПолучитьДокумент()=1 Цикл
						Если ДокПодч.Проведен()=0 Тогда
							Продолжить;
						КонецЕсли;
						Если ДокПодч.Вид()="РегистрацияСчета_фактуры" Тогда
							ДокСФ=ДокПодч.ТекущийДокумент();
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ДокСФ="" Тогда
						Сообщить("На приходный документ "+Док+"; не зарегистрирована РегистрацияСчета_фактуры!");
						Продолжить;
					КонецЕсли;
				Иначе
					Предупреждение("Погашение аванса произошло неверным видом документа "+ВидДок+" "+Док.НомерДок);
					Возврат;
				КонецЕсли;
				ДокОплаты=ДокКред;
				ДокОтгрузки=Док;
			Иначе
				ДокОплаты=Док;
				ДокОтгрузки=ДокКред;
			КонецЕсли;

			СуммаВсего=Запрос.ПрихДолг+Запрос.РасхДолг; 
			КоэффПересч=СуммаВсего/Пересчет(ДокСФ.Сумма,ДокСФ.Валюта,ДокСФ.ДатаДок,Рубли,1);

 			НомерДата=""+Строка(ДокСФ.ДатаСчетаФактуры)+" № "+СокрП(ДокСФ.НомерСчетаФактуры); 
			Если КоэффПересч<1.0 Тогда
				Если 	(ВидКредДок="ПриходнаяНакладная") 
					ИЛИ (ВидКредДок="РасходнаяНакладная") 
					ИЛИ ((ВидКредДок="ВводОстатковКредита") И (КредЗнакВводОстатковКредита=-1)) Тогда
					// Здесь отображается Сч-ф по самому кредитному документу (ПрихНакладной),
					// но отображаем суммы только фактически оплаченные документом погашения
		 			НомерДата=НомерДата+"; частичн.опл."; 
				Иначе 
					// значит мы имеем авансовую проплату, которая погашается ПрихНакладными
					// Здесь отображается Сч-ф по документу погашения (ПрихНакладной),
					// но отображаем суммы только фактически оприходованные по этому авансу 
					// документом погашения (т.е. ПрихНакладной)
		 			НомерДата=НомерДата+"; частичн.погашен."; 
				КонецЕсли;
			КонецЕсли;
			ПрСуммаБезНДС20=КоэффПересч*ДокСФ.СуммаБезНДС20;
			ПрНДС20=КоэффПересч*ДокСФ.НДС20;
			ПрСуммаБезНДС10=КоэффПересч*ДокСФ.СуммаБезНДС10;
			ПрНДС10=КоэффПересч*ДокСФ.НДС10;
			ПрСуммаСовсемБезНДС=КоэффПересч*ДокСФ.СуммаСовсемБезНДС;

			ПечСуммаВсего=ФРМ(СуммаВсего,ВВ,0);
			ПечСуммаБезНДС20=ФРМ(ПрСуммаБезНДС20,ВВ,0);
			ПечНДС20=ФРМ(ПрНДС20,ВВ,0);
			ПечСуммаБезНДС10=ФРМ(ПрСуммаБезНДС10,ВВ,0);
			ПечНДС10=ФРМ(ПрНДС10,ВВ,0);
			ПечСуммаСовсемБезНДС=ФРМ(ПрСуммаСовсемБезНДС,ВВ,0);
	
			ИтогВсего=ИтогВсего+СуммаВсего;
			ИтогСуммаБезНДС20=ИтогСуммаБезНДС20+ПрСуммаБезНДС20;
			ИтогСуммаБезНДС10=ИтогСуммаБезНДС10+ПрСуммаБезНДС10;
			ИтогНДС20=ИтогНДС20+ПрНДС20;
			ИтогНДС10=ИтогНДС10+ПрНДС10;
			ИтогСуммаСовсемБезНДС=ИтогСуммаСовсемБезНДС+ПрСуммаСовсемБезНДС;
			
		    ДатаПриходаСчетаФактуры=ДокСФ.ДатаПриходаСчетаФактуры;
		    ДатаОплаты=Док.ДатаДок;
		    ДатаПриходаТовара=ДокСФ.ДатаПриходаТовара;
		    ВыбКлиент=Док.Клиент;
	
			Расшифровка=СоздатьОбъект("СписокЗначений");
			Расшифровка.ДобавитьЗначение(ДокСФ,"Счет-Фактура");
			Расшифровка.ДобавитьЗначение(ДокОтгрузки,"документ отгрузки");
			Расшифровка.ДобавитьЗначение(ДокОплаты,"документ оплаты");
			ПорНомер=ПорНомер+1;
			Таб.ВывестиСекцию("Строка");
			Оживить(1);
		КонецЦикла; 
	КонецЦикла;

	Таб.ВывестиСекцию("Всего");
	Фир=Константа.ОсновнаяФирма;
	Фир.ИспользоватьДату(РабочаяДата());
	ГлБух=Фир.ГлБухгалтер;
	Таб.ВывестиСекцию("Подвал");
	Оживить(2);

	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Книга покупок","");

КонецПроцедуры
//-----------------------------------------------
Процедура ОбработкаЯчейкиТаблицы(Расшифровка,СтандартнаяОбработка,Таблица) Экспорт
	Перем Отчет;
	Перем Позиция;
	Перем КонтФормы;
	
	Если ТипЗначенияСтр(Расшифровка)<>"СписокЗначений" Тогда
		СтандартнаяОбработка = 1;
		Возврат;
	КонецЕсли;
	
	Если Расшифровка.ВыбратьЗначение(Отчет,"Выберите документ",Позиция,,1)>0 Тогда
		Если ТипЗначенияСтр(Отчет)="Документ" Тогда 
			ОткрытьФорму(Отчет,КонтФормы,1);
		Иначе
			Расшифровка=Отчет;
			СтандартнаяОбработка = 1;
		КонецЕсли;
	Иначе
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры                  
//-----------------------------------------------
ДатаКонца=ПолучитьДатуТА();
ДатаНачала=ДатаКонца-30;
Если Число(ДатаНачала)=0 Тогда
	ДатаНачала='01.01.96';
Конецесли;  
ВыбФирма=Константа.ОсновнаяФирма; 
