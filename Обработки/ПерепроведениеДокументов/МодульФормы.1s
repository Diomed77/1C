Перем	ДокПодч;
Перем	СписокВидовДок;
Перем	СписокДок;
Перем	Начало;
Перем	ТочкаНачала;
Перем	ЧислоСообщений;
//---------------------
Процедура ВывСообщить(Стр)
	ЧислоСообщений=ЧислоСообщений+1;
	Если ЧислоСообщений>100 Тогда
		ОчиститьОкноСообщений(); 
		ЧислоСообщений=1;
	КонецЕсли;
	Сообщить(Стр);
КонецПроцедуры
//-----------------------------------------------
Функция Проводка()
	Если СписокДок.РазмерСписка()=0 Тогда
		Возврат 0;
	КонецЕсли;
	
	НачатьТранзакцию();
	ВывСообщить("Удаляется проведение!");
	Док=СоздатьОбъект("Документ");
	Для Ном=1 По СписокДок.РазмерСписка() Цикл
		Докум=СписокДок.ПолучитьЗначение(Ном);
		Док.НайтиДокумент(Докум);
		Док.СделатьНеПроведенным();
	КонецЦикла;
	
	РОО=глРазрОтрицОстатки;
	РПР=Константа.РазрешитьПродаватьРезерв;
	РПК=Константа.РазрешитьПревышениеКредита;
	
	глРазрОтрицОстатки=Да;
	Константа.РазрешитьПродаватьРезерв=Да;
	Константа.РазрешитьПревышениеКредита=Да;
	
	Док=СоздатьОбъект("Документ");
	Для Ном=1 По СписокДок.РазмерСписка() Цикл
		Докум=СписокДок.ПолучитьЗначение(Ном);
		Док.НайтиДокумент(Докум);
		ВывСообщить("Перепроводится: "+Докум+" от "+Докум.ДатаДок);
		Если Док.Провести(0,ТочкаНачала)=0 Тогда
			глРазрОтрицОстатки=РОО;
			Константа.РазрешитьПродаватьРезерв=РПР;
			Константа.РазрешитьПревышениеКредита=РПК;
			ОтменитьТранзакцию();
			ВывСообщить("Процесс перепроведения ОТМЕНЕН!");
			СписокДок.УдалитьВсе();
			Возврат 1;
	 	КонецЕсли;
	КонецЦикла;
	глРазрОтрицОстатки=РОО;
	Константа.РазрешитьПродаватьРезерв=РПР;
	Константа.РазрешитьПревышениеКредита=РПК;
	ЗафиксироватьТранзакцию();
	 
	СписокДок.УдалитьВсе();
	Возврат 0;
КонецФункции      
//-----------------------------------------------
Процедура ДобавитьВид(СписокВидов)
	Для Н=1 По СписокВидов.РазмерСписка() Цикл
		ВидДок=СписокВидов.ПолучитьЗначение(Н);
		Если СписокВидовДок.НайтиЗначение(ВидДок)=0 Тогда
			СписокВидовДок.ДобавитьЗначение(ВидДок);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры      
//-----------------------------------------------
Процедура УстановитьТН(ГП)
	Сравн=СравнитьДокументы(ГП,ТочкаНачала);
	ТочкаНачала=?(Сравн<0,ГП,ТочкаНачала);
КонецПроцедуры      
//-----------------------------------------------
Процедура Перепровести()
	Если Вопрос("Вы действительно хотите перепровести документы?"
				+?(МонопольныйРежим()=0,РазделительСтрок+"Эту процедуру лучше выполнять в монопольном режиме.",""),1)=2 Тогда
		Возврат;
	КонецЕсли;
	
	Начало=ПреобразоватьДатуВпозицию(?(Число(Константа.ДатаПоследнегоЗавершенияПериода)=0,'30.11.97',Константа.ДатаПоследнегоЗавершенияПериода));
	ПозицияТА=Число(Строка(Число(ПолучитьДатуТА()))+СтрЗаменить(ПолучитьВремяТА(),":",""));
	ТочкаНачала=ПозицияТА;
	Выб=0;
	
	ГП=?(ВыбранаГП(Константа.ГраницаПоследовательности_1)=0,Начало,Константа.ГраницаПоследовательности_1);
	Если СравнитьТН(ГП,ПозицияТА,"Последов_1")>0 Тогда
		Выб=1;
		УстановитьТН(ГП);
	КонецЕсли;
	
	Если Выб=0 Тогда
		Сообщить("Все документы проведены последовательно. Перепроведения документов не требуется.");
		Возврат;
	КонецЕсли;
	
	СписокВидовДок=СоздатьОбъект("СписокЗначений");
	ДобавитьВид(ВидыДокументовПоследов());
	
	ДатаНачала=ДостатьДатуПозиции(ТочкаНачала);
	Если ДатаНачала>ПолучитьДатуТА() Тогда
		Возврат;
	КонецЕсли;
	
	МаксСписок=6999;
	
	
	Док=СоздатьОбъект("Документ");
	СписокДок=СоздатьОбъект("СписокЗначений");
	Док.ВыбратьДокументы(ДатаНачала,ПолучитьДатуТА());
	Пока Док.ПолучитьДокумент()=1 Цикл
		ВидДок=Док.Вид();
		Если Док.СравнитьТА()>0 Тогда
			Прервать;
		КонецЕсли;
		Если СписокВидовДок.НайтиЗначение(ВидДок)=0 Тогда
			Продолжить;
		КонецЕсли;
		Если Док.Проведен()=0 Тогда
			Продолжить;
		КонецЕсли;
		Если Док.ПометкаУдаления()=1 Тогда
			Продолжить;
		КонецЕсли;
		Если СравнитьДокументы(ТочкаНачала,Док)>0 Тогда
			Продолжить;
		КонецЕсли;
		
		ВывСообщить("В список: "+Док+" от "+Док.ДатаДок);
		СписокДок.ДобавитьЗначение(Док.ТекущийДокумент());
//		Если СписокДок.РазмерСписка()>МаксСписок Тогда
//			Если Проводка()=1 Тогда
//				Возврат;
//			КонецЕсли;
//			СписокДок.УдалитьВсе();
//		КонецЕсли;
	КонецЦикла;
	Если Проводка()=1 Тогда
		Возврат;
	КонецЕсли;
	ВывСообщить("Процесс перепроведения удачно ЗАВЕРШЕН! "+ТекущаяДата()+" в "+ТекущееВремя());
	
КонецПроцедуры      
//---------------------

ДокПодч=СоздатьОбъект("Документ");
ДатаКонца=РабочаяДата();
ЧислоСообщений=0;
//Если (НазваниеНабораПрав()="Администратор") Тогда 
//	Форма.Перепровести.Доступность(1);
//Иначе
//	Форма.Перепровести.Доступность(0);
//Конецесли;
